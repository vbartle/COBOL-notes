000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PHNLST01.
000300*---------------------------------------------------
000400* This program displays the contents of the
000500* phone file.
000600*--------------------------------------------------
000700 ENVIRONMENT DIVISION.
000800 INPUT-OUTPUT SECTION.
000900 FILE-CONTROL.
001000     SELECT OPTIONAL PHONE-FILE
001100*or  SELECT PHONE-FILE
001200         ASSIGN TO "phone.dat"
001300*or      ASSIGN TO "phone"
001400         ORGANIZATION IS SEQUENTIAL.
001500
001600 DATA DIVISION.
001700 FILE SECTION.
001800 FD  PHONE-FILE
001900     LABEL RECORDS ARE STANDARD.
002000 01  PHONE-RECORD.
002100     05  PHONE-LAST-NAME      PIC X(20).
002200     05  PHONE-FIRST-NAME     PIC X(20).
002300     05  PHONE-NUMBER         PIC X(15).
002400
002500 WORKING-STORAGE SECTION.
002600
002700* Structure for SCREEN DISPLAY
002800 01  FIELDS-TO-DISPLAY.
002900     05  PROMPT-1             PIC X(10) VALUE "Last Name:".
003000     05  DISPLAY-LAST-NAME    PIC X(20).
003100     05  PROMPT-2             PIC X(6) VALUE "First:".
003200     05  DISPLAY-FIRST-NAME   PIC X(20).
003300     05  PROMPT-3             PIC X(3) VALUE "NO:".
003400     05  DISPLAY-NUMBER       PIC X(15).
003500
003600 01  END-OF-FILE            PIC X.
003700
003800 01  SCREEN-LINES           PIC 99.
003900 01  A-DUMMY                PIC X.
004000
004100 PROCEDURE DIVISION.
004200 MAIN-LOGIC SECTION.
004300 PROGRAM-BEGIN.
004400
004500     PERFORM OPENING-PROCEDURE.
004600     MOVE ZEROES TO SCREEN-LINES.
004700     MOVE "N" TO END-OF-FILE.
004800     PERFORM READ-NEXT-RECORD.
004900     PERFORM DISPLAY-RECORDS
005000         UNTIL END-OF-FILE = "Y".
005100     PERFORM CLOSING-PROCEDURE.
005200
005300 PROGRAM-DONE.
005400     STOP RUN.
005500
005600 OPENING-PROCEDURE.
005700     OPEN INPUT PHONE-FILE.
005800
005900 CLOSING-PROCEDURE.
006000     CLOSE PHONE-FILE.
006100
006200 DISPLAY-RECORDS.
006300     PERFORM DISPLAY-FIELDS.
006400     PERFORM READ-NEXT-RECORD.
006500
006600 DISPLAY-FIELDS.
006700     IF SCREEN-LINES = 15
006800         PERFORM PRESS-ENTER.
006900     MOVE PHONE-LAST-NAME TO DISPLAY-LAST-NAME.
007000     MOVE PHONE-FIRST-NAME TO DISPLAY-FIRST-NAME.
007100     MOVE PHONE-NUMBER TO DISPLAY-NUMBER.
007200     DISPLAY FIELDS-TO-DISPLAY.
007300
007400     ADD 1 TO SCREEN-LINES.
007500
007600 READ-NEXT-RECORD.
007700     READ PHONE-FILE NEXT RECORD
007800        AT END
007900        MOVE "Y" TO END-OF-FILE.
008000
008100 PRESS-ENTER.
008200     DISPLAY "Press ENTER to continue . . ".
008300     ACCEPT A-DUMMY.
008400     MOVE ZEROES TO SCREEN-LINES.
008500
