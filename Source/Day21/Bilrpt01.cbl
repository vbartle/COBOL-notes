000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. BILRPT01.
000300*---------------------------------
000400* Bills report in Due Date order
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVOUCH.CBL".
001100
001200     COPY "SLVND02.CBL".
001300
001400     SELECT PRINTER-FILE
001500         ASSIGN TO PRINTER
001600         ORGANIZATION IS LINE SEQUENTIAL.
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100     COPY "FDVOUCH.CBL".
002200
002300     COPY "FDVND04.CBL".
002400
002500 FD  PRINTER-FILE
002600     LABEL RECORDS ARE OMITTED.
002700 01  PRINTER-RECORD             PIC X(80).
002800
002900 WORKING-STORAGE SECTION.
003000
003100 77  OK-TO-PROCESS         PIC X.
003200
003300     COPY "WSCASE01.CBL".
003400
003500 01  DETAIL-LINE.
003600     05  PRINT-NUMBER      PIC ZZZZ9.
003700     05  FILLER            PIC X(3) VALUE SPACE.
003800     05  PRINT-NAME        PIC X(30).
003900     05  FILLER            PIC X(1) VALUE SPACE.
004000     05  PRINT-DUE-DATE    PIC Z9/99/9999.
004100     05  FILLER            PIC X(1) VALUE SPACE.
004200     05  PRINT-AMOUNT      PIC ZZZ,ZZ9.99.
004300     05  FILLER            PIC X(1) VALUE SPACE.
004400     05  PRINT-INVOICE     PIC X(15).
004500     05  FILLER            PIC X(1) VALUE SPACE.
004600     05  PRINT-SELECTED    PIC X(1) VALUE SPACE.
004700
004800 01  TOTAL-THRU.
004900     05  FILLER            PIC X(20) VALUE SPACE.
005000     05  FILLER            PIC X(10) VALUE "TOTAL THRU".
005100
005200 01  COLUMN-LINE.
005300     05  FILLER         PIC X(7)  VALUE "VOUCHER".
005400     05  FILLER         PIC X(1)  VALUE SPACE.
005500     05  FILLER         PIC X(10) VALUE "VENDOR/For".
005600     05  FILLER         PIC X(23) VALUE SPACE.
005700     05  FILLER         PIC X(8)  VALUE "DUE DATE".
005800     05  FILLER         PIC X(1)  VALUE SPACE.
005900     05  FILLER         PIC X(10) VALUE "AMOUNT DUE".
006000     05  FILLER         PIC X(1)  VALUE SPACE.
006100     05  FILLER         PIC X(7)  VALUE "INVOICE".
006200     05  FILLER         PIC X(9)  VALUE SPACE.
006300     05  FILLER         PIC X(1)  VALUE "S".
006400
006500 01  TITLE-LINE.
006600     05  FILLER              PIC X(30) VALUE SPACE.
006700     05  FILLER              PIC X(12)
006800         VALUE "BILLS REPORT".
006900     05  FILLER              PIC X(19) VALUE SPACE.
007000     05  FILLER              PIC X(5) VALUE "PAGE:".
007100     05  FILLER              PIC X(1) VALUE SPACE.
007200     05  PRINT-PAGE-NUMBER   PIC ZZZ9.
007300
007400 77  VOUCHER-FILE-AT-END     PIC X.
007500 77  VENDOR-RECORD-FOUND     PIC X.
007600
007700 77  LINE-COUNT              PIC 999 VALUE ZERO.
007800 77  PAGE-NUMBER             PIC 9999 VALUE ZERO.
007900 77  MAXIMUM-LINES           PIC 999 VALUE 55.
008000
008100 77  RECORD-COUNT            PIC 9999 VALUE ZEROES.
008200
008300     COPY "WSDATE01.CBL".
008400
008500 PROCEDURE DIVISION.
008600 PROGRAM-BEGIN.
008700
008800     PERFORM OPENING-PROCEDURE.
008900     PERFORM MAIN-PROCESS.
009000     PERFORM CLOSING-PROCEDURE.
009100
009200 PROGRAM-EXIT.
009300     EXIT PROGRAM.
009400
009500 PROGRAM-DONE.
009600     STOP RUN.
009700
009800 OPENING-PROCEDURE.
009900     OPEN I-O VENDOR-FILE.
010000
010100     OPEN OUTPUT PRINTER-FILE.
010200
010300 MAIN-PROCESS.
010400     PERFORM GET-OK-TO-PROCESS.
010500     IF OK-TO-PROCESS = "Y"
010600         PERFORM PRINT-THE-REPORT.
010700
010800 CLOSING-PROCEDURE.
010900     CLOSE VENDOR-FILE.
011000     PERFORM END-LAST-PAGE.
011100     CLOSE PRINTER-FILE.
011200
011300 GET-OK-TO-PROCESS.
011400     PERFORM ACCEPT-OK-TO-PROCESS.
011500     PERFORM RE-ACCEPT-OK-TO-PROCESS
011600         UNTIL OK-TO-PROCESS = "Y" OR "N".
011700
011800 ACCEPT-OK-TO-PROCESS.
011900     DISPLAY "PRINT BILLS REPORT (Y/N)?".
012000     ACCEPT OK-TO-PROCESS.
012100     INSPECT OK-TO-PROCESS
012200       CONVERTING LOWER-ALPHA
012300       TO         UPPER-ALPHA.
012400
012500 RE-ACCEPT-OK-TO-PROCESS.
012600     DISPLAY "YOU MUST ENTER YES OR NO".
012700     PERFORM ACCEPT-OK-TO-PROCESS.
012800
012900 PRINT-THE-REPORT.
013000     OPEN INPUT VOUCHER-FILE.
013100     PERFORM START-ONE-REPORT.
013200     PERFORM PROCESS-VOUCHERS.
013300     PERFORM END-ONE-REPORT.
013400     CLOSE VOUCHER-FILE.
013500
013600 START-ONE-REPORT.
013700     PERFORM INITIALIZE-REPORT.
013800     PERFORM START-NEW-PAGE.
013900
014000 INITIALIZE-REPORT.
014100     MOVE ZEROES TO LINE-COUNT PAGE-NUMBER.
014200
014300 END-ONE-REPORT.
014400     IF RECORD-COUNT = ZEROES
014500         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
014600         PERFORM WRITE-TO-PRINTER.
014700
014800 PROCESS-VOUCHERS.
014900     PERFORM READ-FIRST-VALID-VOUCHER.
015000     PERFORM PROCESS-ALL-VOUCHERS
015100         UNTIL VOUCHER-FILE-AT-END = "Y".
015200
015300 PROCESS-ALL-VOUCHERS.
015400     PERFORM PROCESS-THIS-VOUCHER.
015500     PERFORM READ-NEXT-VALID-VOUCHER.
015600
015700 PROCESS-THIS-VOUCHER.
015800     ADD 1 TO RECORD-COUNT.
015900     IF LINE-COUNT > MAXIMUM-LINES
016000         PERFORM START-NEXT-PAGE.
016100     PERFORM PRINT-THE-RECORD.
016200
016300 PRINT-THE-RECORD.
016400     PERFORM PRINT-LINE-1.
016500     PERFORM PRINT-LINE-2.
016600     PERFORM LINE-FEED.
016700
016800 PRINT-LINE-1.
016900     MOVE SPACE TO DETAIL-LINE.
017000     MOVE VOUCHER-NUMBER TO PRINT-NUMBER.
017100
017200     MOVE VOUCHER-VENDOR TO VENDOR-NUMBER.
017300     PERFORM READ-VENDOR-RECORD.
017400     IF VENDOR-RECORD-FOUND = "Y"
017500         MOVE VENDOR-NAME TO PRINT-NAME
017600     ELSE
017700         MOVE "*VENDOR NOT ON FILE*" TO PRINT-NAME.
017800
017900     MOVE VOUCHER-DUE TO DATE-CCYYMMDD.
018000     PERFORM CONVERT-TO-MMDDCCYY.
018100     MOVE DATE-MMDDCCYY TO PRINT-DUE-DATE.
018200
018300     MOVE VOUCHER-AMOUNT TO PRINT-AMOUNT.
018400     MOVE VOUCHER-INVOICE TO PRINT-INVOICE.
018500
018600     IF VOUCHER-SELECTED = "Y"
018700         MOVE VOUCHER-SELECTED TO PRINT-SELECTED
018800     ELSE
018900         MOVE SPACE TO PRINT-SELECTED.
019000
019100     MOVE DETAIL-LINE TO PRINTER-RECORD.
019200     PERFORM WRITE-TO-PRINTER.
019300
019400 PRINT-LINE-2.
019500     MOVE SPACE TO DETAIL-LINE.
019600     MOVE VOUCHER-FOR TO PRINT-NAME.
019700     MOVE DETAIL-LINE TO PRINTER-RECORD.
019800     PERFORM WRITE-TO-PRINTER.
019900
020000 WRITE-TO-PRINTER.
020100     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
020200     ADD 1 TO LINE-COUNT.
020300
020400 LINE-FEED.
020500     MOVE SPACE TO PRINTER-RECORD.
020600     PERFORM WRITE-TO-PRINTER.
020700
020800 START-NEXT-PAGE.
020900     PERFORM END-LAST-PAGE.
021000     PERFORM START-NEW-PAGE.
021100
021200 START-NEW-PAGE.
021300     ADD 1 TO PAGE-NUMBER.
021400     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
021500     MOVE TITLE-LINE TO PRINTER-RECORD.
021600     PERFORM WRITE-TO-PRINTER.
021700     PERFORM LINE-FEED.
021800     MOVE COLUMN-LINE TO PRINTER-RECORD.
021900     PERFORM WRITE-TO-PRINTER.
022000     PERFORM LINE-FEED.
022100
022200 END-LAST-PAGE.
022300     PERFORM FORM-FEED.
022400     MOVE ZERO TO LINE-COUNT.
022500
022600 FORM-FEED.
022700     MOVE SPACE TO PRINTER-RECORD.
022800     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
022900
023000*---------------------------------
023100* Read first, read next routines
023200*---------------------------------
023300 READ-FIRST-VALID-VOUCHER.
023400     PERFORM READ-NEXT-VALID-VOUCHER.
023500
023600 READ-NEXT-VALID-VOUCHER.
023700     PERFORM READ-NEXT-VOUCHER-RECORD.
023800     PERFORM READ-NEXT-VOUCHER-RECORD
023900         UNTIL VOUCHER-FILE-AT-END = "Y"
024000            OR VOUCHER-PAID-DATE = ZEROES.
024100
024200 READ-NEXT-VOUCHER-RECORD.
024300     MOVE "N" TO VOUCHER-FILE-AT-END.
024400     READ VOUCHER-FILE NEXT RECORD
024500         AT END MOVE "Y" TO VOUCHER-FILE-AT-END.
024600
024700*---------------------------------
024800* Other File IO routines
024900*---------------------------------
025000 READ-VENDOR-RECORD.
025100     MOVE "Y" TO VENDOR-RECORD-FOUND.
025200     READ VENDOR-FILE RECORD
025300         INVALID KEY
025400         MOVE "N" TO VENDOR-RECORD-FOUND.
025500
025600*---------------------------------
025700* Utility Routines
025800*---------------------------------
025900     COPY "PLDATE01.CBL".
026000
