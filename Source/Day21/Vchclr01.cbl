000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VCHCLR01.
000300*---------------------------------
000400* Asks the user if all selected
000500* vouchers should be cleared.
000600*
000700* 1. Searches the voucher file for
000800*    unpaid vouchers that are
000900*    selected and clears the
001000*    selected flag
001100*---------------------------------
001200 ENVIRONMENT DIVISION.
001300 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500
001600     COPY "SLVOUCH.CBL".
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100     COPY "FDVOUCH.CBL".
002200
002300 WORKING-STORAGE SECTION.
002400
002500 77  OK-TO-CLEAR             PIC X.
002600 77  VOUCHER-FILE-AT-END     PIC X.
002700
002800     COPY "WSCASE01.CBL".
002900
003000 PROCEDURE DIVISION.
003100 PROGRAM-BEGIN.
003200     PERFORM OPENING-PROCEDURE.
003300     PERFORM MAIN-PROCESS.
003400     PERFORM CLOSING-PROCEDURE.
003500
003600 PROGRAM-EXIT.
003700     EXIT PROGRAM.
003800
003900 PROGRAM-DONE.
004000     STOP RUN.
004100
004200 OPENING-PROCEDURE.
004300     OPEN I-O VOUCHER-FILE.
004400
004500 CLOSING-PROCEDURE.
004600     CLOSE VOUCHER-FILE.
004700
004800 MAIN-PROCESS.
004900     PERFORM GET-OK-TO-CLEAR.
005000     IF OK-TO-CLEAR = "Y"
005100         PERFORM PROCESS-VOUCHERS.
005200
005300 GET-OK-TO-CLEAR.
005400     PERFORM ACCEPT-OK-TO-CLEAR.
005500     PERFORM RE-ACCEPT-OK-TO-CLEAR
005600         UNTIL OK-TO-CLEAR = "Y" OR "N".
005700
005800 ACCEPT-OK-TO-CLEAR.
005900     DISPLAY "CLEAR ALL PREVIOUS SELECTIONS (Y/N)?".
006000     ACCEPT OK-TO-CLEAR.
006100     INSPECT OK-TO-CLEAR
006200       CONVERTING LOWER-ALPHA
006300       TO         UPPER-ALPHA.
006400
006500
006600 RE-ACCEPT-OK-TO-CLEAR.
006700     DISPLAY "YOU MUST ENTER YES OR NO".
006800     PERFORM ACCEPT-OK-TO-CLEAR.
006900
007000*---------------------------------
007100* Clear all previous selections.
007200*---------------------------------
007300 PROCESS-VOUCHERS.
007400     PERFORM READ-FIRST-VALID-VOUCHER.
007500     PERFORM PROCESS-ALL-VOUCHERS
007600         UNTIL VOUCHER-FILE-AT-END = "Y".
007700
007800 PROCESS-ALL-VOUCHERS.
007900     PERFORM PROCESS-THIS-VOUCHER.
008000     PERFORM READ-NEXT-VALID-VOUCHER.
008100
008200 PROCESS-THIS-VOUCHER.
008300     MOVE "N" TO VOUCHER-SELECTED
008400     PERFORM REWRITE-VOUCHER-RECORD.
008500
008600*---------------------------------
008700* Read first, read next routines
008800*---------------------------------
008900 READ-FIRST-VALID-VOUCHER.
009000     PERFORM READ-NEXT-VALID-VOUCHER.
009100
009200 READ-NEXT-VALID-VOUCHER.
009300     PERFORM READ-NEXT-VOUCHER-RECORD.
009400     PERFORM READ-NEXT-VOUCHER-RECORD
009500         UNTIL VOUCHER-FILE-AT-END = "Y"
009600            OR (    VOUCHER-PAID-DATE = ZEROES
009700                AND VOUCHER-SELECTED = "Y").
009800
009900 READ-NEXT-VOUCHER-RECORD.
010000     MOVE  "N" TO VOUCHER-FILE-AT-END.
010100     READ VOUCHER-FILE NEXT RECORD
010200        AT END
010300         MOVE "Y" TO VOUCHER-FILE-AT-END.
010400
010500*---------------------------------
010600* Other File I-O routines.
010700*---------------------------------
010800 REWRITE-VOUCHER-RECORD.
010900     REWRITE VOUCHER-RECORD
011000         INVALID KEY
011100         DISPLAY "ERROR REWRITING VENDOR RECORD".
011200
