000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. MULT07.
000300*--------------------------------------------------
000400* This program asks the user for a number for a
000500* multiplication table, and a table size
000600* and then displays a table for that number
000700* times the values 1 through HOW-MANY.
000800*
000900* The display is paused after each 15 lines.
001000*--------------------------------------------------
001100 ENVIRONMENT DIVISION.
001200 DATA DIVISION.
001300 WORKING-STORAGE SECTION.
001400
001500 01  THE-TABLE          PIC 99.
001600 01  THE-ENTRY          PIC 999.
001700 01  THE-PRODUCT        PIC 9999.
001800 01  HOW-MANY-ENTRIES   PIC 99.
001900 01  SCREEN-LINES       PIC 99.
002000
002100 01  A-DUMMY            PIC X.
002200
002300 01  YES-NO             PIC X.
002400
002500 PROCEDURE DIVISION.
002600
002700 PROGRAM-BEGIN.
002800     MOVE "Y" TO YES-NO.
002900     PERFORM DISPLAY-ONE-TABLE
003000         UNTIL YES-NO = "N".
003100
003200 PROGRAM-DONE.
003300     STOP RUN.
003400
003500 DISPLAY-ONE-TABLE.
003600     PERFORM GET-WHICH-TABLE.
003700     PERFORM DISPLAY-THE-TABLE.
003800     PERFORM GO-AGAIN.
003900
004000 GET-WHICH-TABLE.
004100     DISPLAY
004200      "Which multiplication table(01-99)?".
004300     ACCEPT THE-TABLE.
004400
004500 DISPLAY-THE-TABLE.
004600     PERFORM GET-HOW-MANY-ENTRIES.
004700
004800     MOVE 0 TO SCREEN-LINES.
004900
005000     PERFORM DISPLAY-ONE-ENTRY
005100         VARYING THE-ENTRY
005200           FROM 1 BY 1
005300           UNTIL THE-ENTRY > HOW-MANY-ENTRIES.
005400
005500 GO-AGAIN.
005600     DISPLAY "Go Again (Y/N)?".
005700     ACCEPT YES-NO.
005800     IF YES-NO = "y"
005900         MOVE "Y" TO YES-NO.
006000     IF YES-NO NOT = "Y"
006100         MOVE "N" TO YES-NO.
006200
006300 GET-HOW-MANY-ENTRIES.
006400     DISPLAY
006500      "How many entries would you like (01-99)?".
006600     ACCEPT HOW-MANY-ENTRIES.
006700
006800 DISPLAY-ONE-ENTRY.
006900
007000     IF SCREEN-LINES = 15
007100         PERFORM PRESS-ENTER.
007200     COMPUTE THE-PRODUCT = THE-TABLE * THE-ENTRY.
007300     DISPLAY
007400         THE-TABLE " * " THE-ENTRY " = " THE-PRODUCT.
007500
007600     ADD 1 TO SCREEN-LINES.
007700
007800 PRESS-ENTER.
007900     DISPLAY "Press ENTER to continue . . .".
008000     ACCEPT A-DUMMY.
008100     MOVE 0 TO SCREEN-LINES.
008200
