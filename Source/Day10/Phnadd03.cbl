000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PHNADD03.
000300*---------------------------------------------------
000400* This program creates a new data file if necessary
000500* and adds records to the file from user entered
000600* data. The records are written to the data file
000700* and echoed to the printer.
000800*--------------------------------------------------
000900 ENVIRONMENT DIVISION.
001000 INPUT-OUTPUT SECTION.
001100 FILE-CONTROL.
001200     SELECT PHONE-FILE
001300         ASSIGN TO "phone.dat"
001400         ORGANIZATION IS SEQUENTIAL.
001500
001600     SELECT PRINTER-FILE
001700         ASSIGN TO PRINTER
001800         ORGANIZATION IS LINE SEQUENTIAL.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200 FD  PHONE-FILE
002300     LABEL RECORDS ARE STANDARD.
002400 01  PHONE-RECORD.
002500     05  PHONE-LAST-NAME      PIC X(20).
002600     05  PHONE-FIRST-NAME     PIC X(20).
002700     05  PHONE-NUMBER         PIC X(15).
002800     05  PHONE-EXTENSION      PIC X(5).
002900
003000 FD  PRINTER-FILE
003100     LABEL RECORDS ARE OMITTED.
003200 01  PRINTER-RECORD           PIC X(80).
003300
003400 WORKING-STORAGE SECTION.
003500
003600* Variables for SCREEN ENTRY
003700 01  PROMPT-1               PIC X(9)  VALUE "Last Name".
003800 01  PROMPT-2               PIC X(10) VALUE "First Name".
003900 01  PROMPT-3               PIC X(6)  VALUE "Number".
004000 01  PROMPT-4               PIC X(9)  VALUE "Extension".
004100
004200 01  YES-NO                 PIC X.
004300 01  ENTRY-OK               PIC X.
004400
004500 PROCEDURE DIVISION.
004600 MAIN-LOGIC SECTION.
004700 PROGRAM-BEGIN.
004800
004900     PERFORM OPENING-PROCEDURE.
005000     MOVE "Y" TO YES-NO.
005100     PERFORM ADD-RECORDS
005200         UNTIL YES-NO = "N".
005300     PERFORM CLOSING-PROCEDURE.
005400
005500 PROGRAM-DONE.
005600     STOP RUN.
005700
005800* OPENING AND CLOSING
005900
006000 OPENING-PROCEDURE.
006100     OPEN EXTEND PHONE-FILE.
006200     OPEN OUTPUT PRINTER-FILE.
006300
006400 CLOSING-PROCEDURE.
006500     CLOSE PHONE-FILE.
006600     MOVE SPACE TO PRINTER-RECORD.
006700     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
006800     CLOSE PRINTER-FILE.
006900
007000 ADD-RECORDS.
007100     MOVE "N" TO ENTRY-OK.
007200     PERFORM GET-FIELDS
007300        UNTIL ENTRY-OK = "Y".
007400     PERFORM ADD-THIS-RECORD.
007500     PERFORM GO-AGAIN.
007600
007700 GET-FIELDS.
007800     MOVE SPACE TO PHONE-RECORD.
007900     DISPLAY PROMPT-1 " ? ".
008000     ACCEPT PHONE-LAST-NAME.
008100     DISPLAY PROMPT-2 " ? ".
008200     ACCEPT PHONE-FIRST-NAME.
008300     DISPLAY PROMPT-3 " ? ".
008400     ACCEPT PHONE-NUMBER.
008500     DISPLAY PROMPT-4 " ? ".
008600     ACCEPT PHONE-EXTENSION.
008700     PERFORM VALIDATE-FIELDS.
008800
008900 VALIDATE-FIELDS.
009000     MOVE "Y" TO ENTRY-OK.
009100     IF PHONE-LAST-NAME = SPACE
009200         DISPLAY "LAST NAME MUST BE ENTERED"
009300         MOVE "N" TO ENTRY-OK.
009400
009500 ADD-THIS-RECORD.
009600     MOVE PHONE-RECORD TO PRINTER-RECORD.
009700     WRITE PHONE-RECORD.
009800     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
009900
010000 GO-AGAIN.
010100     DISPLAY "GO AGAIN?".
010200     ACCEPT YES-NO.
010300     IF YES-NO = "y"
010400         MOVE "Y" TO YES-NO.
010500     IF YES-NO NOT = "Y"
010600         MOVE "N" TO YES-NO.
010700
