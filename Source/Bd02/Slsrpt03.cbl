000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. SLSRPT03.
000300*---------------------------------
000400* Print test sales data
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000*---------------------------------
001100* SLSALES.CBL
001200*---------------------------------
001300     SELECT SALES-FILE
001400         ASSIGN TO "SALES"
001500         ORGANIZATION IS SEQUENTIAL.
001600
001700     SELECT WORK-FILE
001800         ASSIGN TO "WORK"
001900         ORGANIZATION IS SEQUENTIAL.
002000
002100     SELECT SORT-FILE
002200         ASSIGN TO "SORT".
002300
002400     SELECT PRINTER-FILE
002500         ASSIGN TO PRINTER
002600         ORGANIZATION IS LINE SEQUENTIAL.
002700
002800 DATA DIVISION.
002900 FILE SECTION.
003000
003100*---------------------------------
003200* FDSALES.CBL
003300* Temporary daily sales file.
003400*---------------------------------
003500 FD  SALES-FILE
003600     LABEL RECORDS ARE STANDARD.
003700 01  SALES-RECORD.
003800     05  SALES-STORE              PIC 9(2).
003900     05  SALES-DIVISION           PIC 9(2).
004000     05  SALES-DEPARTMENT         PIC 9(2).
004100     05  SALES-CATEGORY           PIC 9(2).
004200     05  SALES-AMOUNT             PIC S9(6)V99.
004300
004400 FD  WORK-FILE
004500     LABEL RECORDS ARE STANDARD.
004600 01  WORK-RECORD.
004700     05  WORK-STORE              PIC 9(2).
004800     05  WORK-DIVISION           PIC 9(2).
004900     05  WORK-DEPARTMENT         PIC 9(2).
005000     05  WORK-CATEGORY           PIC 9(2).
005100     05  WORK-AMOUNT             PIC S9(6)V99.
005200
005300 SD  SORT-FILE.
005400    
005500 01  SORT-RECORD.
005600     05  SORT-STORE              PIC 9(2).
005700     05  SORT-DIVISION           PIC 9(2).
005800     05  SORT-DEPARTMENT         PIC 9(2).
005900     05  SORT-CATEGORY           PIC 9(2).
006000     05  SORT-AMOUNT             PIC S9(6)V99.
006100
006200 FD  PRINTER-FILE
006300     LABEL RECORDS ARE OMITTED.
006400 01  PRINTER-RECORD              PIC X(80).
006500
006600 WORKING-STORAGE SECTION.
006700
006800 01  THE-DIVISIONS.
006900     05  FILLER       PIC 99 VALUE 01.
007000     05  FILLER       PIC X(15) VALUE "ATHLETICS".
007100     05  FILLER       PIC 99 VALUE 02.
007200     05  FILLER       PIC X(15) VALUE "SPORTING GOODS".
007300     05  FILLER       PIC 99 VALUE 03.
007400     05  FILLER       PIC X(15) VALUE "CAMPING".
007500 01  FILLER REDEFINES THE-DIVISIONS.
007600     05  DIVISION-TABLE OCCURS 3 TIMES
007700          INDEXED BY DIVISION-INDEX.
007800         10  DIVISION-NUMBER          PIC 99.
007900         10  DIVISION-NAME            PIC X(15).
008000
008100 01  THE-DEPARTMENTS.
008200     05  FILLER       PIC 99 VALUE 01.
008300     05  FILLER       PIC X(15) VALUE "EXERCISE".
008400     05  FILLER       PIC 99 VALUE 02.
008500     05  FILLER       PIC X(15) VALUE "MISCELLANEOUS".
008600     05  FILLER       PIC 99 VALUE 03.
008700     05  FILLER       PIC X(15) VALUE "SPORT CLOTHES".
008800     05  FILLER       PIC 99 VALUE 04.
008900     05  FILLER       PIC X(15) VALUE "EQUIPMENT".
009000     05  FILLER       PIC 99 VALUE 05.
009100     05  FILLER       PIC X(15) VALUE "CAMP EQUIPMENT".
009200     05  FILLER       PIC 99 VALUE 06.
009300     05  FILLER       PIC X(15) VALUE "CAMPING CLOTHES".
009400 01  FILLER REDEFINES THE-DEPARTMENTS.
009500     05  DEPARTMENT-TABLE OCCURS 6 TIMES
009600          INDEXED BY DEPARTMENT-INDEX.
009700         10  DEPARTMENT-NUMBER          PIC 99.
009800         10  DEPARTMENT-NAME            PIC X(15).
009900
010000 01  THE-CATEGORIES.
010100     05  FILLER       PIC 99 VALUE 01.
010200     05  FILLER       PIC X(15) VALUE "WEIGHTS".
010300     05  FILLER       PIC 99 VALUE 02.
010400     05  FILLER       PIC X(15) VALUE "MACHINES".
010500     05  FILLER       PIC 99 VALUE 03.
010600     05  FILLER       PIC X(15) VALUE "SUN GLASSES".
010700     05  FILLER       PIC 99 VALUE 04.
010800     05  FILLER       PIC X(15) VALUE "VITAMINS".
010900     05  FILLER       PIC 99 VALUE 05.
011000     05  FILLER       PIC X(15) VALUE "MEN'S CLOTHES".
011100     05  FILLER       PIC 99 VALUE 06.
011200     05  FILLER       PIC X(15) VALUE "WOMEN'S CLOTHES".
011300     05  FILLER       PIC 99 VALUE 07.
011400     05  FILLER       PIC X(15) VALUE "TENNIS".
011500     05  FILLER       PIC 99 VALUE 08.
011600     05  FILLER       PIC X(15) VALUE "SOCCER".
011700     05  FILLER       PIC 99 VALUE 09.
011800     05  FILLER       PIC X(15) VALUE "TENTS".
011900     05  FILLER       PIC 99 VALUE 10.
012000     05  FILLER       PIC X(15) VALUE "SLEEPING BAGS".
012100     05  FILLER       PIC 99 VALUE 11.
012200     05  FILLER       PIC X(15) VALUE "CLOTHING".
012300     05  FILLER       PIC 99 VALUE 12.
012400     05  FILLER       PIC X(15) VALUE "HIKING BOOTS".
012500 01  FILLER REDEFINES THE-CATEGORIES.
012600     05  CATEGORY-TABLE OCCURS 12 TIMES
012700          INDEXED BY CATEGORY-INDEX.
012800         10  CATEGORY-NUMBER          PIC 99.
012900         10  CATEGORY-NAME            PIC X(15).
013000
013100 77  OK-TO-PROCESS         PIC X.
013200
013300     COPY "WSCASE01.CBL".
013400
013500 01  LEGEND-LINE.
013600     05  FILLER            PIC X(6) VALUE "STORE:".
013700     05  FILLER            PIC X(1) VALUE SPACE.
013800     05  PRINT-STORE       PIC Z9.
013900
014000 01  DETAIL-LINE.
014100     05  FILLER               PIC X(3) VALUE SPACE.
014200     05  PRINT-DIVISION       PIC Z9.
014300     05  FILLER               PIC X(4) VALUE SPACE.
014400     05  FILLER               PIC X(3) VALUE SPACE.
014500     05  PRINT-DEPARTMENT     PIC Z9.
014600     05  FILLER               PIC X(6) VALUE SPACE.
014700     05  FILLER               PIC X(3) VALUE SPACE.
014800     05  PRINT-CATEGORY       PIC Z9.
014900     05  FILLER               PIC X(4) VALUE SPACE.
015000     05  PRINT-CATEGORY-NAME  PIC X(15).
015100     05  FILLER               PIC X(1) VALUE SPACE.
015200     05  PRINT-AMOUNT         PIC ZZZ,ZZ9.99-.
015300
015400 01  COLUMN-LINE.
015500     05  FILLER         PIC X(8)  VALUE "DIVISION".
015600     05  FILLER         PIC X(1)  VALUE SPACE.
015700     05  FILLER         PIC X(10) VALUE "DEPARTMENT".
015800     05  FILLER         PIC X(1)  VALUE SPACE.
015900     05  FILLER         PIC X(8)  VALUE "CATEGORY".
016000     05  FILLER         PIC X(1)  VALUE SPACE.
016100     05  FILLER         PIC X(15)  VALUE SPACE.
016200     05  FILLER         PIC X(5)  VALUE SPACE.
016300     05  FILLER         PIC X(6)  VALUE "AMOUNT".
016400
016500 01  TITLE-LINE.
016600     05  FILLER              PIC X(30) VALUE SPACE.
016700     05  FILLER              PIC X(12)
016800         VALUE "SALES REPORT".
016900     05  FILLER              PIC X(16) VALUE SPACE.
017000     05  FILLER              PIC X(5) VALUE "PAGE:".
017100     05  FILLER              PIC X(1) VALUE SPACE.
017200     05  PRINT-PAGE-NUMBER   PIC ZZZ9.
017300
017400 01  TOTAL-LINE.
017500     05  FILLER              PIC X(11) VALUE SPACE.
017600     05  TOTAL-TYPE          PIC X(8).
017700     05  FILLER              PIC X(1) VALUE SPACE.
017800     05  TOTAL-NUMBER        PIC Z9.
017900     05  FILLER              PIC X(1) VALUE SPACE.
018000     05  TOTAL-NAME          PIC X(15) VALUE SPACE.
018100     05  FILLER              PIC X(1) VALUE SPACE.
018200     05  TOTAL-LITERAL       PIC X(5) VALUE "TOTAL".
018300     05  FILLER              PIC X(1) VALUE SPACE.
018400     05  PRINT-TOTAL         PIC ZZZ,ZZ9.99-.
018500
018600 77  GRAND-TOTAL-LITERAL      PIC X(8) VALUE "   GRAND".
018700 77  STORE-TOTAL-LITERAL      PIC X(8) VALUE "   STORE".
018800 77  DIVISION-TOTAL-LITERAL   PIC X(8) VALUE "DIVISION".
018900 77  DEPARTMENT-TOTAL-LITERAL PIC X(8) VALUE "    DEPT".
019000
019100 77  WORK-FILE-AT-END        PIC X.
019200
019300 77  LINE-COUNT              PIC 999 VALUE ZERO.
019400 77  PAGE-NUMBER             PIC 9999 VALUE ZERO.
019500 77  MAXIMUM-LINES           PIC 999 VALUE 55.
019600
019700 77  RECORD-COUNT            PIC 9999 VALUE ZEROES.
019800
019900* Control break current values for store, division
020000* department.
020100 77  CURRENT-STORE          PIC 99.
020200 77  CURRENT-DIVISION       PIC 99.
020300 77  CURRENT-DEPARTMENT     PIC 99.
020400
020500* Control break accumulators
020600* GRAND TOTAL is the level 1 accumulator for the whole file
020700* STORE TOTAL is the level 2 accumulator
020800* DIVISION TOTAL is the level 3 accumulator
020900* DEPARTMENT TOTAL is the level 4 accumulator.
021000 77  GRAND-TOTAL            PIC S9(6)V99.
021100 77  STORE-TOTAL            PIC S9(6)V99.
021200 77  DIVISION-TOTAL         PIC S9(6)V99.
021300 77  DEPARTMENT-TOTAL       PIC S9(6)V99.
021400
021500 PROCEDURE DIVISION.
021600 PROGRAM-BEGIN.
021700
021800     PERFORM OPENING-PROCEDURE.
021900     PERFORM MAIN-PROCESS.
022000     PERFORM CLOSING-PROCEDURE.
022100
022200 PROGRAM-EXIT.
022300     EXIT PROGRAM.
022400
022500 PROGRAM-DONE.
022600     STOP RUN.
022700
022800 OPENING-PROCEDURE.
022900
023000     OPEN OUTPUT PRINTER-FILE.
023100
023200 MAIN-PROCESS.
023300     PERFORM GET-OK-TO-PROCESS.
023400     PERFORM PROCESS-THE-FILE
023500         UNTIL OK-TO-PROCESS = "N".
023600
023700 CLOSING-PROCEDURE.
023800     CLOSE PRINTER-FILE.
023900
024000 GET-OK-TO-PROCESS.
024100     PERFORM ACCEPT-OK-TO-PROCESS.
024200     PERFORM RE-ACCEPT-OK-TO-PROCESS
024300         UNTIL OK-TO-PROCESS = "Y" OR "N".
024400
024500 ACCEPT-OK-TO-PROCESS.
024600     DISPLAY "PRINT SALES REPORT (Y/N)?".
024700     ACCEPT OK-TO-PROCESS.
024800     INSPECT OK-TO-PROCESS
024900       CONVERTING LOWER-ALPHA
025000       TO         UPPER-ALPHA.
025100
025200 RE-ACCEPT-OK-TO-PROCESS.
025300     DISPLAY "YOU MUST ENTER YES OR NO".
025400     PERFORM ACCEPT-OK-TO-PROCESS.
025500
025600 PROCESS-THE-FILE.
025700     PERFORM START-THE-FILE.
025800     PERFORM PRINT-ONE-REPORT.
025900     PERFORM END-THE-FILE.
026000
026100*    PERFORM GET-OK-TO-PROCESS.
026200     MOVE "N" TO OK-TO-PROCESS.
026300
026400 START-THE-FILE.
026500     PERFORM SORT-DATA-FILE.
026600     OPEN INPUT WORK-FILE.
026700
026800 END-THE-FILE.
026900     CLOSE WORK-FILE.
027000
027100 SORT-DATA-FILE.
027200     SORT SORT-FILE
027300         ON ASCENDING KEY SORT-STORE
027400            ASCENDING KEY SORT-DIVISION
027500            ASCENDING KEY SORT-DEPARTMENT
027600            ASCENDING KEY SORT-CATEGORY
027700          USING SALES-FILE
027800          GIVING WORK-FILE.
027900
028000* LEVEL 1 CONTROL BREAK
028100 PRINT-ONE-REPORT.
028200     PERFORM START-ONE-REPORT.
028300     PERFORM PROCESS-ALL-STORES
028400         UNTIL WORK-FILE-AT-END = "Y".
028500     PERFORM END-ONE-REPORT.
028600
028700 START-ONE-REPORT.
028800     PERFORM READ-FIRST-VALID-WORK.
028900     MOVE ZEROES TO GRAND-TOTAL.
029000
029100     PERFORM START-NEW-REPORT.
029200
029300 START-NEW-REPORT.
029400     MOVE SPACE TO DETAIL-LINE.
029500     MOVE ZEROES TO LINE-COUNT PAGE-NUMBER.
029600
029700 END-ONE-REPORT.
029800     IF RECORD-COUNT = ZEROES
029900         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
030000         PERFORM WRITE-TO-PRINTER
030100     ELSE
030200         PERFORM PRINT-GRAND-TOTAL.
030300
030400     PERFORM END-LAST-PAGE.
030500
030600 PRINT-GRAND-TOTAL.
030700     MOVE SPACE TO TOTAL-LINE.
030800     MOVE GRAND-TOTAL TO PRINT-TOTAL.
030900     MOVE GRAND-TOTAL-LITERAL TO TOTAL-TYPE.
031000     MOVE "TOTAL" TO TOTAL-LITERAL.
031100     MOVE TOTAL-LINE TO PRINTER-RECORD.
031200     PERFORM WRITE-TO-PRINTER.
031300     PERFORM LINE-FEED 2 TIMES.
031400     MOVE SPACE TO DETAIL-LINE.
031500
031600* LEVEL 2 CONTROL BREAK
031700 PROCESS-ALL-STORES.
031800     PERFORM START-ONE-STORE.
031900
032000     PERFORM PROCESS-ALL-DIVISIONS
032100         UNTIL WORK-FILE-AT-END = "Y"
032200            OR WORK-STORE NOT = CURRENT-STORE.
032300
032400     PERFORM END-ONE-STORE.
032500
032600 START-ONE-STORE.
032700     MOVE WORK-STORE TO CURRENT-STORE.
032800     MOVE ZEROES TO STORE-TOTAL.
032900     MOVE WORK-STORE TO PRINT-STORE.
033000
033100     PERFORM START-NEXT-PAGE.
033200
033300 END-ONE-STORE.
033400     PERFORM PRINT-STORE-TOTAL.
033500     ADD STORE-TOTAL TO GRAND-TOTAL.
033600
033700 PRINT-STORE-TOTAL.
033800     MOVE SPACE TO TOTAL-LINE.
033900     MOVE STORE-TOTAL TO PRINT-TOTAL.
034000     MOVE CURRENT-STORE TO TOTAL-NUMBER.
034100     MOVE STORE-TOTAL-LITERAL TO TOTAL-TYPE.
034200     MOVE "TOTAL" TO TOTAL-LITERAL.
034300     MOVE TOTAL-LINE TO PRINTER-RECORD.
034400     PERFORM WRITE-TO-PRINTER.
034500     PERFORM LINE-FEED.
034600     MOVE SPACE TO DETAIL-LINE.
034700
034800* LEVEL 3 CONTROL BREAK
034900 PROCESS-ALL-DIVISIONS.
035000     PERFORM START-ONE-DIVISION.
035100
035200     PERFORM PROCESS-ALL-DEPARTMENTS
035300         UNTIL WORK-FILE-AT-END = "Y"
035400            OR WORK-STORE NOT = CURRENT-STORE
035500            OR WORK-DIVISION NOT = CURRENT-DIVISION.
035600
035700     PERFORM END-ONE-DIVISION.
035800
035900 START-ONE-DIVISION.
036000     MOVE WORK-DIVISION TO CURRENT-DIVISION.
036100     MOVE ZEROES TO DIVISION-TOTAL.
036200     MOVE WORK-DIVISION TO PRINT-DIVISION.
036300
036400 END-ONE-DIVISION.
036500     PERFORM PRINT-DIVISION-TOTAL.
036600     ADD DIVISION-TOTAL TO STORE-TOTAL.
036700
036800 PRINT-DIVISION-TOTAL.
036900     MOVE SPACE TO TOTAL-LINE.
037000     MOVE DIVISION-TOTAL TO PRINT-TOTAL.
037100     MOVE CURRENT-DIVISION TO TOTAL-NUMBER.
037200     MOVE DIVISION-TOTAL-LITERAL TO TOTAL-TYPE.
037300     MOVE "TOTAL" TO TOTAL-LITERAL.
037400     PERFORM LOAD-DIVISION-NAME.
037500     MOVE TOTAL-LINE TO PRINTER-RECORD.
037600     PERFORM WRITE-TO-PRINTER.
037700     PERFORM LINE-FEED.
037800     MOVE SPACE TO DETAIL-LINE.
037900
038000 LOAD-DIVISION-NAME.
038100     SET DIVISION-INDEX TO 1.
038200     SEARCH DIVISION-TABLE
038300         AT END
038400           MOVE "NOT FOUND" TO TOTAL-NAME
038500         WHEN
038600           DIVISION-NUMBER(DIVISION-INDEX) =
038700              CURRENT-DIVISION
038800              MOVE DIVISION-NAME(DIVISION-INDEX) TO
038900                   TOTAL-NAME.
039000
039100* LEVEL 4 CONTROL BREAK
039200 PROCESS-ALL-DEPARTMENTS.
039300     PERFORM START-ONE-DEPARTMENT.
039400
039500     PERFORM PROCESS-ALL-CATEGORIES
039600         UNTIL WORK-FILE-AT-END = "Y"
039700            OR WORK-STORE NOT = CURRENT-STORE
039800            OR WORK-DIVISION NOT = CURRENT-DIVISION
039900            OR WORK-DEPARTMENT NOT = CURRENT-DEPARTMENT.
040000
040100     PERFORM END-ONE-DEPARTMENT.
040200
040300 START-ONE-DEPARTMENT.
040400     MOVE WORK-DEPARTMENT TO CURRENT-DEPARTMENT.
040500     MOVE ZEROES TO DEPARTMENT-TOTAL.
040600     MOVE WORK-DEPARTMENT TO PRINT-DEPARTMENT.
040700
040800 END-ONE-DEPARTMENT.
040900     PERFORM PRINT-DEPARTMENT-TOTAL.
041000     ADD DEPARTMENT-TOTAL TO DIVISION-TOTAL.
041100
041200 PRINT-DEPARTMENT-TOTAL.
041300     MOVE SPACE TO TOTAL-LINE.
041400     MOVE DEPARTMENT-TOTAL TO PRINT-TOTAL.
041500     MOVE CURRENT-DEPARTMENT TO TOTAL-NUMBER.
041600     MOVE DEPARTMENT-TOTAL-LITERAL TO TOTAL-TYPE.
041700     MOVE "TOTAL" TO TOTAL-LITERAL.
041800     PERFORM LOAD-DEPARTMENT-NAME.
041900     MOVE TOTAL-LINE TO PRINTER-RECORD.
042000     PERFORM WRITE-TO-PRINTER.
042100     PERFORM LINE-FEED.
042200     MOVE SPACE TO DETAIL-LINE.
042300
042400 LOAD-DEPARTMENT-NAME.
042500     SET DEPARTMENT-INDEX TO 1.
042600     SEARCH DEPARTMENT-TABLE
042700         AT END
042800           MOVE "NOT FOUND" TO TOTAL-NAME
042900         WHEN
043000           DEPARTMENT-NUMBER(DEPARTMENT-INDEX) =
043100              CURRENT-DEPARTMENT
043200              MOVE DEPARTMENT-NAME(DEPARTMENT-INDEX) TO
043300                   TOTAL-NAME.
043400
043500* PROCESS ONE RECORD LEVEL
043600 PROCESS-ALL-CATEGORIES.
043700     PERFORM PROCESS-THIS-CATEGORY.
043800     ADD WORK-AMOUNT TO DEPARTMENT-TOTAL.
043900     ADD 1 TO RECORD-COUNT.
044000     PERFORM READ-NEXT-VALID-WORK.
044100
044200 PROCESS-THIS-CATEGORY.
044300     IF LINE-COUNT > MAXIMUM-LINES
044400         PERFORM START-NEXT-PAGE.
044500     PERFORM PRINT-THE-RECORD.
044600
044700 PRINT-THE-RECORD.
044800     MOVE WORK-CATEGORY TO PRINT-CATEGORY.
044900
045000     PERFORM LOAD-CATEGORY-NAME.
045100
045200     MOVE WORK-AMOUNT TO PRINT-AMOUNT.
045300
045400     MOVE DETAIL-LINE TO PRINTER-RECORD.
045500     PERFORM WRITE-TO-PRINTER.
045600     MOVE SPACE TO DETAIL-LINE.
045700
045800 LOAD-CATEGORY-NAME.
045900     SET CATEGORY-INDEX TO 1.
046000     SEARCH CATEGORY-TABLE
046100         AT END
046200           MOVE "NOT FOUND" TO TOTAL-NAME
046300         WHEN
046400           CATEGORY-NUMBER(CATEGORY-INDEX) =
046500              WORK-CATEGORY
046600              MOVE CATEGORY-NAME(CATEGORY-INDEX) TO
046700                   PRINT-CATEGORY-NAME.
046800
046900* PRINTING ROUTINES
047000 WRITE-TO-PRINTER.
047100     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
047200     ADD 1 TO LINE-COUNT.
047300
047400 LINE-FEED.
047500     MOVE SPACE TO PRINTER-RECORD.
047600     PERFORM WRITE-TO-PRINTER.
047700
047800 START-NEXT-PAGE.
047900     PERFORM END-LAST-PAGE.
048000     PERFORM START-NEW-PAGE.
048100
048200 START-NEW-PAGE.
048300     ADD 1 TO PAGE-NUMBER.
048400     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
048500     MOVE TITLE-LINE TO PRINTER-RECORD.
048600     PERFORM WRITE-TO-PRINTER.
048700     PERFORM LINE-FEED.
048800     MOVE LEGEND-LINE TO PRINTER-RECORD.
048900     PERFORM WRITE-TO-PRINTER.
049000     PERFORM LINE-FEED.
049100     MOVE COLUMN-LINE TO PRINTER-RECORD.
049200     PERFORM WRITE-TO-PRINTER.
049300     PERFORM LINE-FEED.
049400
049500 END-LAST-PAGE.
049600     IF PAGE-NUMBER > 0
049700         PERFORM FORM-FEED.
049800     MOVE ZERO TO LINE-COUNT.
049900
050000 FORM-FEED.
050100     MOVE SPACE TO PRINTER-RECORD.
050200     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
050300
050400*---------------------------------
050500* Read first, read next routines
050600*---------------------------------
050700 READ-FIRST-VALID-WORK.
050800     PERFORM READ-NEXT-VALID-WORK.
050900
051000 READ-NEXT-VALID-WORK.
051100     PERFORM READ-NEXT-WORK-RECORD.
051200
051300 READ-NEXT-WORK-RECORD.
051400     MOVE "N" TO WORK-FILE-AT-END.
051500     READ WORK-FILE NEXT RECORD
051600         AT END MOVE "Y" TO WORK-FILE-AT-END.
051700
