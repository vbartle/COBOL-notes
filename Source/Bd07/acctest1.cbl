000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. ACCTEST.
000300
000400 ENVIRONMENT DIVISION.
000500
000600 CLASS-CONTROL.
000700     ACCOUNT IS CLASS "acctcls".
000800
000900 DATA DIVISION.
001000 WORKING-STORAGE SECTION.
001100 01  ACCT-NUM         PIC 9(8).
001200 01  AMT              PIC S9(8)V99.
001300 01  WITHDRAWAL       PIC S9(8)V99.
001400 01  BALANCE          PIC S9(8)V99.
001500 01  FORMATTED-AMT    PIC Z(3)9.99-.
001600 01  FORMATTED-BAL    PIC Z(3)9.99-.
001700 01  FORMATTED-WD     PIC Z(3)9.99-.
001800 01  THE-MSG          PIC X(30).
001900
002000 01  DEPOSIT-RESULT   PIC X(79).
002100
002200 01  FirstAccount     OBJECT REFERENCE.
002300 01  SecondAccount    OBJECT REFERENCE.
002400 01  WorkAccount      OBJECT REFERENCE.
002500
002600
002700 PROCEDURE DIVISION.
002800 MAIN-LOGIC.
002900
003000     INVOKE ACCOUNT "new" RETURNING FirstAccount.
003100     MOVE 1 TO ACCT-NUM.
003200     INVOKE FirstAccount "SetAccountNumber" USING ACCT-NUM.
003300
003400     SET WorkAccount TO FirstAccount.
003500
003600     PERFORM MAKE-DEPOSITS
003700
003800     INVOKE ACCOUNT "new" RETURNING SecondAccount.
003900     MOVE 117 TO ACCT-NUM.
004000     INVOKE SecondAccount "SetAccountNumber" USING ACCT-NUM.
004100
004200     SET WorkAccount TO SecondAccount.
004300
004400     PERFORM MAKE-DEPOSITS.
004500     PERFORM TRY-WITHDRAWALS.
004600     PERFORM PRINT-BALANCES.
004700
004800 EXIT-PROGRAM.
004900     EXIT PROGRAM.
005000
005100 STOP-RUN.
005200     STOP RUN.
005300
005400 MAKE-DEPOSITS.
005500     INVOKE WorkAccount "GetAccountNumber" RETURNING ACCT-NUM.
005600     PERFORM
005700         VARYING AMT FROM 1 BY 3 UNTIL AMT > 20
005800         INVOKE WorkAccount "MakeDeposit" USING AMT
005900         INVOKE WorkAccount "GetBalance" RETURNING BALANCE
006000         MOVE AMT TO FORMATTED-AMT
006100         MOVE BALANCE TO FORMATTED-BAL
006200         DISPLAY "ACCT=" ACCT-NUM " BAL AFTER DEPOSIT "
006300                 FORMATTED-AMT " = "
006400                 FORMATTED-BAL
006500     END-PERFORM.
006600
006700 TRY-WITHDRAWALS.
006800     MOVE 0 TO WITHDRAWAL.
006900     PERFORM TRY-WITHDRAWAL.
007000     MOVE -99 TO WITHDRAWAL.
007100     PERFORM TRY-WITHDRAWAL.
007200     MOVE 100 TO WITHDRAWAL.
007300     PERFORM TRY-WITHDRAWAL.
007400     MOVE 19 TO WITHDRAWAL.
007500     PERFORM TRY-WITHDRAWAL.
007600     MOVE 55 TO WITHDRAWAL.
007700     PERFORM TRY-WITHDRAWAL.
007800
007900 TRY-WITHDRAWAL.
008000     MOVE WITHDRAWAL TO AMT.
008100     INVOKE WorkAccount "GetAccountNumber" RETURNING ACCT-NUM.
008200
008300     MOVE WITHDRAWAL TO FORMATTED-WD.
008400
008500     INVOKE WorkAccount "MakeWithdrawal" USING AMT.
008600     INVOKE WorkAccount "GetBalance" RETURNING BALANCE.
008700     INVOKE WorkAccount "GetMessage" RETURNING THE-MSG.
008800
008900     MOVE AMT TO FORMATTED-AMT.
009000     MOVE BALANCE TO FORMATTED-BAL.
009100
009200     IF AMT = 0 THEN
009300         DISPLAY "WD=" FORMATTED-WD " ACCT=" ACCT-NUM
009400                 " FAILED-" THE-MSG " BAL=" FORMATTED-BAL
009500     ELSE
009600         DISPLAY "WD=" FORMATTED-AMT " ACCT=" ACCT-NUM
009700                 " SUCCESS-NEW BAL=" FORMATTED-BAL
009800     END-IF.
009900
010000 PRINT-BALANCES.
010100     INVOKE FirstAccount "GetAccountNumber" RETURNING ACCT-NUM.
010200     INVOKE FirstAccount "GetBalance" RETURNING BALANCE.
010300
010400     MOVE BALANCE TO FORMATTED-BAL.
010500     DISPLAY " ACCT=" ACCT-NUM " BAL=" FORMATTED-BAL.
010600
010700     INVOKE SecondAccount "GetAccountNumber" RETURNING ACCT-NUM.
010800     INVOKE SecondAccount "GetBalance" RETURNING BALANCE.
010900
011000     MOVE BALANCE TO FORMATTED-BAL.
011100     DISPLAY " ACCT=" ACCT-NUM " BAL=" FORMATTED-BAL.
011200

