000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDRPT04.
000300*---------------------------------
000400* Report on the Vendor File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND02.CBL".
001100
001200     COPY "SLSTATE.CBL".
001300
001400     SELECT PRINTER-FILE
001500         ASSIGN TO PRINTER
001600         ORGANIZATION IS LINE SEQUENTIAL.
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100     COPY "FDVND04.CBL".
002200
002300     COPY "FDSTATE.CBL".
002400
002500 FD  PRINTER-FILE
002600     LABEL RECORDS ARE OMITTED.
002700 01  PRINTER-RECORD             PIC X(80).
002800
002900 WORKING-STORAGE SECTION.
003000
003100 01  DETAIL-LINE.
003200     05  PRINT-NUMBER      PIC 9(5).
003300     05  FILLER            PIC X     VALUE SPACE.
003400     05  PRINT-NAME        PIC X(30).
003500     05  FILLER            PIC X(15) VALUE SPACE.
003600     05  PRINT-CONTACT     PIC X(30).
003700
003800 01  CITY-STATE-LINE.
003900     05  FILLER            PIC X(6) VALUE SPACE.
004000     05  PRINT-CITY        PIC X(20).
004100     05  FILLER            PIC X VALUE SPACE.
004200     05  PRINT-STATE       PIC X(2).
004300     05  FILLER            PIC X VALUE SPACE.
004400     05  PRINT-STATE-NAME  PIC X(20).
004500     05  FILLER            PIC X(1) VALUE SPACE.
004600     05  PRINT-ZIP         PIC X(10).
004700
004800 01  COLUMN-LINE.
004900     05  FILLER         PIC X(2)  VALUE "NO".
005000     05  FILLER         PIC X(4) VALUE SPACE.
005100     05  FILLER         PIC X(12) VALUE "NAME-ADDRESS".
005200     05  FILLER         PIC X(33) VALUE SPACE.
005300     05  FILLER         PIC X(17) VALUE "CONTACT-PHONE-ZIP".
005400
005500 01  TITLE-LINE.
005600     05  FILLER              PIC X(25) VALUE SPACE.
005700     05  FILLER              PIC X(11)
005800         VALUE "VENDOR LIST".
005900     05  FILLER              PIC X(19) VALUE SPACE.
006000     05  FILLER              PIC X(5) VALUE "PAGE:".
006100     05  FILLER              PIC X(1) VALUE SPACE.
006200     05  PRINT-PAGE-NUMBER PIC ZZZZ9.
006300
006400 77  FILE-AT-END             PIC X.
006500 77  STATE-FILE-AT-END       PIC X VALUE "N".
006600 77  LINE-COUNT              PIC 999 VALUE ZERO.
006700 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
006800 77  MAXIMUM-LINES           PIC 999 VALUE 55.
006900
007000 01  TABLE-STATE-RECORD OCCURS 50 TIMES
007100      INDEXED BY STATE-INDEX.
007200     05  TABLE-STATE-CODE          PIC XX.
007300     05  TABLE-STATE-NAME          PIC X(20).
007400
007500 PROCEDURE DIVISION.
007600 PROGRAM-BEGIN.
007700
007800     PERFORM OPENING-PROCEDURE.
007900     MOVE ZEROES TO LINE-COUNT
008000                    PAGE-NUMBER.
008100
008200     PERFORM START-NEW-PAGE.
008300
008400     MOVE "N" TO FILE-AT-END.
008500     PERFORM READ-NEXT-RECORD.
008600     IF FILE-AT-END = "Y"
008700         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
008800         PERFORM WRITE-TO-PRINTER
008900     ELSE
009000         PERFORM PRINT-VENDOR-FIELDS
009100             UNTIL FILE-AT-END = "Y".
009200
009300     PERFORM CLOSING-PROCEDURE.
009400
009500 PROGRAM-EXIT.
009600     EXIT PROGRAM.
009700
009800 PROGRAM-DONE.
009900     STOP RUN.
010000
010100 OPENING-PROCEDURE.
010200     OPEN I-O VENDOR-FILE.
010300
010400     OPEN I-O STATE-FILE.
010500     PERFORM LOAD-STATE-TABLE.
010600     CLOSE STATE-FILE.
010700
010800     OPEN OUTPUT PRINTER-FILE.
010900
011000 LOAD-STATE-TABLE.
011100     PERFORM CLEAR-TABLE.
011200     SET STATE-INDEX TO 1.
011300     PERFORM READ-NEXT-STATE-RECORD.
011400     PERFORM LOAD-ONE-STATE-RECORD
011500         UNTIL STATE-FILE-AT-END = "Y" OR
011600               STATE-INDEX > 50.
011700
011800 CLEAR-TABLE.
011900     PERFORM CLEAR-ONE-TABLE-ROW
012000         VARYING STATE-INDEX FROM 1 BY 1
012100          UNTIL STATE-INDEX > 50.
012200
012300 CLEAR-ONE-TABLE-ROW.
012400     MOVE SPACE TO TABLE-STATE-RECORD(STATE-INDEX).
012500
012600 LOAD-ONE-STATE-RECORD.
012700     MOVE STATE-CODE TO TABLE-STATE-CODE(STATE-INDEX).
012800     MOVE STATE-NAME TO TABLE-STATE-NAME(STATE-INDEX).
012900
013000     PERFORM READ-NEXT-STATE-RECORD.
013100
013200     IF STATE-FILE-AT-END NOT = "Y"
013300         SET STATE-INDEX UP BY 1
013400         IF STATE-INDEX > 50
013500             DISPLAY "TABLE FULL".
013600
013700 CLOSING-PROCEDURE.
013800     CLOSE VENDOR-FILE.
013900     PERFORM END-LAST-PAGE.
014000     CLOSE PRINTER-FILE.
014100
014200 PRINT-VENDOR-FIELDS.
014300     IF LINE-COUNT > MAXIMUM-LINES
014400         PERFORM START-NEXT-PAGE.
014500     PERFORM PRINT-THE-RECORD.
014600     PERFORM READ-NEXT-RECORD.
014700
014800 PRINT-THE-RECORD.
014900     PERFORM PRINT-LINE-1.
015000     PERFORM PRINT-LINE-2.
015100     PERFORM PRINT-LINE-3.
015200     PERFORM PRINT-LINE-4.
015300     PERFORM LINE-FEED.
015400
015500 PRINT-LINE-1.
015600     MOVE SPACE TO DETAIL-LINE.
015700     MOVE VENDOR-NUMBER TO PRINT-NUMBER.
015800     MOVE VENDOR-NAME TO PRINT-NAME.
015900     MOVE VENDOR-CONTACT TO PRINT-CONTACT.
016000     MOVE DETAIL-LINE TO PRINTER-RECORD.
016100     PERFORM WRITE-TO-PRINTER.
016200
016300 PRINT-LINE-2.
016400     MOVE SPACE TO DETAIL-LINE.
016500     MOVE VENDOR-ADDRESS-1 TO PRINT-NAME.
016600     MOVE VENDOR-PHONE TO PRINT-CONTACT.
016700     MOVE DETAIL-LINE TO PRINTER-RECORD.
016800     PERFORM WRITE-TO-PRINTER.
016900
017000 PRINT-LINE-3.
017100     MOVE SPACE TO DETAIL-LINE.
017200     MOVE VENDOR-ADDRESS-2 TO PRINT-NAME.
017300     IF VENDOR-ADDRESS-2 NOT = SPACE
017400         MOVE DETAIL-LINE TO PRINTER-RECORD
017500         PERFORM WRITE-TO-PRINTER.
017600
017700 PRINT-LINE-4.
017800     MOVE SPACE TO CITY-STATE-LINE.
017900     MOVE VENDOR-CITY TO PRINT-CITY.
018000     MOVE VENDOR-STATE TO PRINT-STATE.
018100
018200     PERFORM LOOK-UP-STATE-CODE.
018300
018400     MOVE VENDOR-ZIP TO PRINT-ZIP.
018500     MOVE CITY-STATE-LINE TO PRINTER-RECORD.
018600     PERFORM WRITE-TO-PRINTER.
018700
018800 LOOK-UP-STATE-CODE.
018900     SET STATE-INDEX TO 1.
019000     SEARCH TABLE-STATE-RECORD
019100         AT END
019200          MOVE "***Not Found***" TO PRINT-STATE-NAME
019300         WHEN VENDOR-STATE = TABLE-STATE-CODE(STATE-INDEX)
019400          MOVE TABLE-STATE-NAME(STATE-INDEX)
019500            TO PRINT-STATE-NAME.
019600
019700     IF STATE-NAME = SPACE
019800          MOVE "*State is Blank*" TO PRINT-STATE-NAME.
019900
020000 READ-NEXT-RECORD.
020100     READ VENDOR-FILE NEXT RECORD
020200         AT END MOVE "Y" TO FILE-AT-END.
020300
020400 WRITE-TO-PRINTER.
020500     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
020600     ADD 1 TO LINE-COUNT.
020700
020800 LINE-FEED.
020900     MOVE SPACE TO PRINTER-RECORD.
021000     PERFORM WRITE-TO-PRINTER.
021100
021200 START-NEXT-PAGE.
021300     PERFORM END-LAST-PAGE.
021400     PERFORM START-NEW-PAGE.
021500
021600 START-NEW-PAGE.
021700     ADD 1 TO PAGE-NUMBER.
021800     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
021900     MOVE TITLE-LINE TO PRINTER-RECORD.
022000     PERFORM WRITE-TO-PRINTER.
022100     PERFORM LINE-FEED.
022200     MOVE COLUMN-LINE TO PRINTER-RECORD.
022300     PERFORM WRITE-TO-PRINTER.
022400     PERFORM LINE-FEED.
022500
022600 END-LAST-PAGE.
022700     PERFORM FORM-FEED.
022800     MOVE ZERO TO LINE-COUNT.
022900
023000 FORM-FEED.
023100     MOVE SPACE TO PRINTER-RECORD.
023200     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
023300
023400 READ-NEXT-STATE-RECORD.
023500     MOVE "N" TO STATE-FILE-AT-END.
023600     READ STATE-FILE NEXT RECORD
023700         AT END
023800         MOVE "Y" TO STATE-FILE-AT-END.
023900
024000
