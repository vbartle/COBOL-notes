000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDDSP03.
000300*------------------------------------------------
000400* Display records in the Vendor File.
000500*------------------------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND02.CBL".
001100
001200 DATA DIVISION.
001300 FILE SECTION.
001400
001500     COPY "FDVND04.CBL".
001600
001700 WORKING-STORAGE SECTION.
001800
001900 01  DETAIL-LINE.
002000     05  DISPLAY-NUMBER      PIC 9(5).
002100     05  FILLER              PIC X     VALUE SPACE.
002200     05  DISPLAY-NAME        PIC X(30).
002300     05  FILLER              PIC X     VALUE SPACE.
002400     05  DISPLAY-CONTACT     PIC X(30).
002500
002600 01  CITY-STATE-DETAIL.
002700     05  DISPLAY-CITY        PIC X(20).
002800     05  FILLER              PIC X VALUE SPACE.
002900     05  DISPLAY-STATE       PIC X(2).
003000
003100 01  COLUMN-LINE.
003200     05  FILLER         PIC X(2)  VALUE "NO".
003300     05  FILLER         PIC X(4) VALUE SPACE.
003400     05  FILLER         PIC X(12) VALUE "NAME-ADDRESS".
003500     05  FILLER         PIC X(19) VALUE SPACE.
003600     05  FILLER         PIC X(17) VALUE "CONTACT-PHONE-ZIP".
003700
003800 01  TITLE-LINE.
003900     05  FILLER              PIC X(15) VALUE SPACE.
004000     05  FILLER              PIC X(11)
004100         VALUE "VENDOR LIST".
004200     05  FILLER              PIC X(15) VALUE SPACE.
004300     05  FILLER              PIC X(5) VALUE "PAGE:".
004400     05  FILLER              PIC X(1) VALUE SPACE.
004500     05  DISPLAY-PAGE-NUMBER PIC ZZZZ9.
004600
004700 77  FILE-AT-END             PIC X.
004800 77  A-DUMMY                 PIC X.
004900 77  LINE-COUNT              PIC 999 VALUE ZERO.
005000 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
005100 77  MAXIMUM-LINES           PIC 999 VALUE 15.
005200
005300 77  DISPLAY-RECORD          PIC X(79).
005400
005500 PROCEDURE DIVISION.
005600 PROGRAM-BEGIN.
005700
005800     PERFORM OPENING-PROCEDURE.
005900     MOVE ZEROES TO LINE-COUNT
006000                    PAGE-NUMBER.
006100
006200     PERFORM START-NEW-PAGE.
006300
006400     MOVE "N" TO FILE-AT-END.
006500     PERFORM READ-NEXT-RECORD.
006600     IF FILE-AT-END = "Y"
006700         MOVE "NO RECORDS FOUND" TO DISPLAY-RECORD
006800         PERFORM WRITE-DISPLAY-RECORD
006900     ELSE
007000         PERFORM DISPLAY-VENDOR-FIELDS
007100             UNTIL FILE-AT-END = "Y".
007200
007300     PERFORM CLOSING-PROCEDURE.
007400
007500 PROGRAM-EXIT.
007600     EXIT PROGRAM.
007700
007800 PROGRAM-DONE.
007900     ACCEPT OMITTED. STOP RUN.
008000
008100 OPENING-PROCEDURE.
008200     OPEN I-O VENDOR-FILE.
008300
008400 CLOSING-PROCEDURE.
008500     CLOSE VENDOR-FILE.
008600
008700 DISPLAY-VENDOR-FIELDS.
008800     IF LINE-COUNT > MAXIMUM-LINES
008900         PERFORM START-NEXT-PAGE.
009000     PERFORM DISPLAY-THE-RECORD.
009100     PERFORM READ-NEXT-RECORD.
009200
009300 DISPLAY-THE-RECORD.
009400     PERFORM DISPLAY-LINE-1.
009500     PERFORM DISPLAY-LINE-2.
009600     PERFORM DISPLAY-LINE-3.
009700     PERFORM DISPLAY-LINE-4.
009800     PERFORM LINE-FEED.
009900
010000 DISPLAY-LINE-1.
010100     MOVE SPACE TO DETAIL-LINE.
010200     MOVE VENDOR-NUMBER TO DISPLAY-NUMBER.
010300     MOVE VENDOR-NAME TO DISPLAY-NAME.
010400     MOVE VENDOR-CONTACT TO DISPLAY-CONTACT.
010500     MOVE DETAIL-LINE TO DISPLAY-RECORD.
010600     PERFORM WRITE-DISPLAY-RECORD.
010700
010800 DISPLAY-LINE-2.
010900     MOVE SPACE TO DETAIL-LINE.
011000     MOVE VENDOR-ADDRESS-1 TO DISPLAY-NAME.
011100     MOVE VENDOR-PHONE TO DISPLAY-CONTACT.
011200     MOVE DETAIL-LINE TO DISPLAY-RECORD.
011300     PERFORM WRITE-DISPLAY-RECORD.
011400
011500 DISPLAY-LINE-3.
011600     MOVE SPACE TO DETAIL-LINE.
011700     MOVE VENDOR-ADDRESS-2 TO DISPLAY-NAME.
011800     IF VENDOR-ADDRESS-2 NOT = SPACE
011900         MOVE DETAIL-LINE TO DISPLAY-RECORD
012000         PERFORM WRITE-DISPLAY-RECORD.
012100
012200 DISPLAY-LINE-4.
012300     MOVE SPACE TO DETAIL-LINE.
012400     MOVE VENDOR-CITY TO DISPLAY-CITY.
012500     MOVE VENDOR-STATE TO DISPLAY-STATE.
012600     MOVE CITY-STATE-DETAIL TO DISPLAY-NAME.
012700     MOVE VENDOR-ZIP TO DISPLAY-CONTACT.
012800     MOVE DETAIL-LINE TO DISPLAY-RECORD.
012900     PERFORM WRITE-DISPLAY-RECORD.
013000
013100 READ-NEXT-RECORD.
013200     READ VENDOR-FILE NEXT RECORD
013300         AT END MOVE "Y" TO FILE-AT-END.
013400
013500 WRITE-DISPLAY-RECORD.
013600     DISPLAY DISPLAY-RECORD.
013700     ADD 1 TO LINE-COUNT.
013800
013900 LINE-FEED.
014000     MOVE SPACE TO DISPLAY-RECORD.
014100     PERFORM WRITE-DISPLAY-RECORD.
014200
014300 START-NEXT-PAGE.
014400
014500     PERFORM END-LAST-PAGE.
014600     PERFORM START-NEW-PAGE.
014700
014800 START-NEW-PAGE.
014900     ADD 1 TO PAGE-NUMBER.
015000     MOVE PAGE-NUMBER TO DISPLAY-PAGE-NUMBER.
015100     MOVE TITLE-LINE TO DISPLAY-RECORD.
015200     PERFORM WRITE-DISPLAY-RECORD.
015300     PERFORM LINE-FEED.
015400     MOVE COLUMN-LINE TO DISPLAY-RECORD.
015500     PERFORM WRITE-DISPLAY-RECORD.
015600     PERFORM LINE-FEED.
015700
015800 END-LAST-PAGE.
015900     PERFORM PRESS-ENTER.
016000     MOVE ZERO TO LINE-COUNT.
016100
016200 PRESS-ENTER.
016300     DISPLAY "PRESS ENTER TO CONTINUE. . .".
016400     ACCEPT A-DUMMY.
016500
