000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. SLSRPT05.
000300*---------------------------------
000400* Print test sales data
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000*---------------------------------
001100* SLSALES.CBL
001200*---------------------------------
001300     SELECT SALES-FILE
001400         ASSIGN TO "SALES"
001500         ORGANIZATION IS SEQUENTIAL.
001600
001700     SELECT WORK-FILE
001800         ASSIGN TO "WORK"
001900         ORGANIZATION IS SEQUENTIAL.
002000
002100     SELECT SORT-FILE
002200         ASSIGN TO "SORT".
002300
002400     SELECT PRINTER-FILE
002500         ASSIGN TO PRINTER
002600         ORGANIZATION IS LINE SEQUENTIAL.
002700
002800 DATA DIVISION.
002900 FILE SECTION.
003000
003100*---------------------------------
003200* FDSALES.CBL
003300* Temporary daily sales file.
003400*---------------------------------
003500 FD  SALES-FILE
003600     LABEL RECORDS ARE STANDARD.
003700 01  SALES-RECORD.
003800     05  SALES-STORE              PIC 9(2).
003900     05  SALES-DIVISION           PIC 9(2).
004000     05  SALES-DEPARTMENT         PIC 9(2).
004100     05  SALES-CATEGORY           PIC 9(2).
004200     05  SALES-AMOUNT             PIC S9(6)V99.
004300
004400 FD  WORK-FILE
004500     LABEL RECORDS ARE STANDARD.
004600 01  WORK-RECORD.
004700     05  WORK-STORE              PIC 9(2).
004800     05  WORK-DIVISION           PIC 9(2).
004900     05  WORK-DEPARTMENT         PIC 9(2).
005000     05  WORK-CATEGORY           PIC 9(2).
005100     05  WORK-AMOUNT             PIC S9(6)V99.
005200
005300 SD  SORT-FILE.
005400    
005500 01  SORT-RECORD.
005600     05  SORT-STORE              PIC 9(2).
005700     05  SORT-DIVISION           PIC 9(2).
005800     05  SORT-DEPARTMENT         PIC 9(2).
005900     05  SORT-CATEGORY           PIC 9(2).
006000     05  SORT-AMOUNT             PIC S9(6)V99.
006100
006200 FD  PRINTER-FILE
006300     LABEL RECORDS ARE OMITTED.
006400 01  PRINTER-RECORD              PIC X(80).
006500
006600 WORKING-STORAGE SECTION.
006700
006800 01  THE-DIVISIONS.
006900     05  FILLER       PIC 99 VALUE 01.
007000     05  FILLER       PIC X(15) VALUE "ATHLETICS".
007100     05  FILLER       PIC 99 VALUE 02.
007200     05  FILLER       PIC X(15) VALUE "SPORTING GOODS".
007300     05  FILLER       PIC 99 VALUE 03.
007400     05  FILLER       PIC X(15) VALUE "CAMPING".
007500 01  FILLER REDEFINES THE-DIVISIONS.
007600     05  DIVISION-TABLE OCCURS 3 TIMES
007700          INDEXED BY DIVISION-INDEX.
007800         10  DIVISION-NUMBER          PIC 99.
007900         10  DIVISION-NAME            PIC X(15).
008000
008100 01  THE-DEPARTMENTS.
008200     05  FILLER       PIC 99 VALUE 01.
008300     05  FILLER       PIC X(15) VALUE "EXERCISE".
008400     05  FILLER       PIC 99 VALUE 02.
008500     05  FILLER       PIC X(15) VALUE "MISCELLANEOUS".
008600     05  FILLER       PIC 99 VALUE 03.
008700     05  FILLER       PIC X(15) VALUE "SPORT CLOTHES".
008800     05  FILLER       PIC 99 VALUE 04.
008900     05  FILLER       PIC X(15) VALUE "EQUIPMENT".
009000     05  FILLER       PIC 99 VALUE 05.
009100     05  FILLER       PIC X(15) VALUE "CAMP EQUIPMENT".
009200     05  FILLER       PIC 99 VALUE 06.
009300     05  FILLER       PIC X(15) VALUE "CAMPING CLOTHES".
009400 01  FILLER REDEFINES THE-DEPARTMENTS.
009500     05  DEPARTMENT-TABLE OCCURS 6 TIMES
009600          INDEXED BY DEPARTMENT-INDEX.
009700         10  DEPARTMENT-NUMBER          PIC 99.
009800         10  DEPARTMENT-NAME            PIC X(15).
009900
010000 01  THE-CATEGORIES.
010100     05  FILLER       PIC 99 VALUE 01.
010200     05  FILLER       PIC X(15) VALUE "WEIGHTS".
010300     05  FILLER       PIC 99 VALUE 02.
010400     05  FILLER       PIC X(15) VALUE "MACHINES".
010500     05  FILLER       PIC 99 VALUE 03.
010600     05  FILLER       PIC X(15) VALUE "SUN GLASSES".
010700     05  FILLER       PIC 99 VALUE 04.
010800     05  FILLER       PIC X(15) VALUE "VITAMINS".
010900     05  FILLER       PIC 99 VALUE 05.
011000     05  FILLER       PIC X(15) VALUE "MEN'S CLOTHES".
011100     05  FILLER       PIC 99 VALUE 06.
011200     05  FILLER       PIC X(15) VALUE "WOMEN'S CLOTHES".
011300     05  FILLER       PIC 99 VALUE 07.
011400     05  FILLER       PIC X(15) VALUE "TENNIS".
011500     05  FILLER       PIC 99 VALUE 08.
011600     05  FILLER       PIC X(15) VALUE "SOCCER".
011700     05  FILLER       PIC 99 VALUE 09.
011800     05  FILLER       PIC X(15) VALUE "TENTS".
011900     05  FILLER       PIC 99 VALUE 10.
012000     05  FILLER       PIC X(15) VALUE "SLEEPING BAGS".
012100     05  FILLER       PIC 99 VALUE 11.
012200     05  FILLER       PIC X(15) VALUE "CLOTHING".
012300     05  FILLER       PIC 99 VALUE 12.
012400     05  FILLER       PIC X(15) VALUE "HIKING BOOTS".
012500 01  FILLER REDEFINES THE-CATEGORIES.
012600     05  CATEGORY-TABLE OCCURS 12 TIMES
012700          INDEXED BY CATEGORY-INDEX.
012800         10  CATEGORY-NUMBER          PIC 99.
012900         10  CATEGORY-NAME            PIC X(15).
013000
013100 77  OK-TO-PROCESS         PIC X.
013200
013300     COPY "WSCASE01.CBL".
013400
013500 01  LEGEND-LINE.
013600     05  FILLER            PIC X(6) VALUE "STORE:".
013700     05  FILLER            PIC X(1) VALUE SPACE.
013800     05  PRINT-STORE       PIC Z9.
013900
014000 01  DETAIL-LINE.
014100     05  FILLER               PIC X(3) VALUE SPACE.
014200     05  PRINT-DIVISION       PIC Z9.
014300     05  FILLER               PIC X(4) VALUE SPACE.
014400     05  FILLER               PIC X(3) VALUE SPACE.
014500     05  PRINT-DEPARTMENT     PIC Z9.
014600     05  FILLER               PIC X(6) VALUE SPACE.
014700     05  FILLER               PIC X(3) VALUE SPACE.
014800     05  PRINT-CATEGORY       PIC Z9.
014900     05  FILLER               PIC X(4) VALUE SPACE.
015000     05  PRINT-CATEGORY-NAME  PIC X(15).
015100     05  FILLER               PIC X(1) VALUE SPACE.
015200     05  PRINT-AMOUNT         PIC ZZZ,ZZ9.99-.
015300
015400 01  COLUMN-LINE.
015500     05  FILLER         PIC X(8)  VALUE "DIVISION".
015600     05  FILLER         PIC X(1)  VALUE SPACE.
015700     05  FILLER         PIC X(10) VALUE "DEPARTMENT".
015800     05  FILLER         PIC X(1)  VALUE SPACE.
015900     05  FILLER         PIC X(8)  VALUE "CATEGORY".
016000     05  FILLER         PIC X(1)  VALUE SPACE.
016100     05  FILLER         PIC X(15)  VALUE SPACE.
016200     05  FILLER         PIC X(5)  VALUE SPACE.
016300     05  FILLER         PIC X(6)  VALUE "AMOUNT".
016400
016500 01  TITLE-LINE.
016600     05  FILLER              PIC X(4) VALUE "RUN:".
016700     05  FORMATTED-RUN-DATE  PIC X(10).
016800     05  FILLER              PIC X(4) VALUE " AT ".
016900     05  FORMATTED-RUN-TIME  PIC X(8).
017000     05  FILLER              PIC X(10) VALUE SPACE.
017100     05  FILLER              PIC X(12)
017200         VALUE "SALES REPORT".
017300     05  FILLER              PIC X(10) VALUE SPACE.
017400     05  FILLER              PIC X(5) VALUE "PAGE:".
017500     05  FILLER              PIC X(1) VALUE SPACE.
017600     05  PRINT-PAGE-NUMBER   PIC ZZZ9.
017700
017800 01  TOTAL-LINE.
017900     05  FILLER              PIC X(11) VALUE SPACE.
018000     05  TOTAL-TYPE          PIC X(8).
018100     05  FILLER              PIC X(1) VALUE SPACE.
018200     05  TOTAL-NUMBER        PIC Z9.
018300     05  FILLER              PIC X(1) VALUE SPACE.
018400     05  TOTAL-NAME          PIC X(15) VALUE SPACE.
018500     05  FILLER              PIC X(1) VALUE SPACE.
018600     05  TOTAL-LITERAL       PIC X(5) VALUE "TOTAL".
018700     05  FILLER              PIC X(1) VALUE SPACE.
018800     05  PRINT-TOTAL         PIC ZZZ,ZZ9.99-.
018900
019000 77  GRAND-TOTAL-LITERAL      PIC X(8) VALUE "   GRAND".
019100 77  STORE-TOTAL-LITERAL      PIC X(8) VALUE "   STORE".
019200 77  DIVISION-TOTAL-LITERAL   PIC X(8) VALUE "DIVISION".
019300 77  DEPARTMENT-TOTAL-LITERAL PIC X(8) VALUE "    DEPT".
019400
019500 77  WORK-FILE-AT-END        PIC X.
019600
019700 77  LINE-COUNT              PIC 999 VALUE ZERO.
019800 77  PAGE-NUMBER             PIC 9999 VALUE ZERO.
019900 77  MAXIMUM-LINES           PIC 999 VALUE 55.
020000
020100 77  RECORD-COUNT            PIC 9999 VALUE ZEROES.
020200
020300* Control break current values for store, division
020400* department.
020500 77  CURRENT-STORE          PIC 99.
020600 77  CURRENT-DIVISION       PIC 99.
020700 77  CURRENT-DEPARTMENT     PIC 99.
020800
020900* Control break accumulators
021000* GRAND TOTAL is the level 1 accumulator for the whole file
021100* STORE TOTAL is the level 2 accumulator
021200* DIVISION TOTAL is the level 3 accumulator
021300* DEPARTMENT TOTAL is the level 4 accumulator.
021400 77  GRAND-TOTAL            PIC S9(6)V99.
021500 77  STORE-TOTAL            PIC S9(6)V99.
021600 77  DIVISION-TOTAL         PIC S9(6)V99.
021700 77  DEPARTMENT-TOTAL       PIC S9(6)V99.
021800
021900* System date and time
022000 77  RUN-DATE           PIC 9(6).
022100 77  RUN-TIME           PIC 9(8).
022200
022300*---------------------------------
022400* Fields for date routines.
022500*---------------------------------
022600 77  FORMATTED-DATE     PIC Z9/99/9999.
022700 77  DATE-MMDDCCYY      PIC 9(8).
022800 01  DATE-CCYYMMDD      PIC 9(8).
022900 01  FILLER REDEFINES DATE-CCYYMMDD.
023000     05  DATE-CC        PIC 99.
023100     05  DATE-YY        PIC 99.
023200     05  DATE-MM        PIC 99.
023300     05  DATE-DD        PIC 99.
023400
023500*---------------------------------
023600* Fields for TIME routines.
023700*---------------------------------
023800 77  FORMATTED-TIME     PIC Z9/99/99.
023900
024000 01  TIME-HHMMSS      PIC 9(6).
024100 01  FILLER REDEFINES TIME-HHMMSS.
024200     05  TIME-HH        PIC 99.
024300     05  TIME-MM        PIC 99.
024400     05  TIME-SS        PIC 99.
024500
024600 PROCEDURE DIVISION.
024700 1000-PROGRAM-BEGIN.
024800
024900     PERFORM 1300-OPENING-PROCEDURE.
025000     PERFORM 2000-MAIN-PROCESS.
025100     PERFORM 1400-CLOSING-PROCEDURE.
025200
025300 1100-PROGRAM-EXIT.
025400     EXIT PROGRAM.
025500
025600 1200-PROGRAM-DONE.
025700     ACCEPT OMITTED. STOP RUN.
025800
025900 1300-OPENING-PROCEDURE.
026000
026100     OPEN OUTPUT PRINTER-FILE.
026200
026300 1400-CLOSING-PROCEDURE.
026400     CLOSE PRINTER-FILE.
026500
026600 2000-MAIN-PROCESS.
026700     PERFORM 2100-GET-OK-TO-PROCESS.
026800     PERFORM 3000-PROCESS-THE-FILE
026900         UNTIL OK-TO-PROCESS = "N".
027000
027100 2100-GET-OK-TO-PROCESS.
027200     PERFORM 2110-ACCEPT-OK-TO-PROCESS.
027300     PERFORM 2120-RE-ACCEPT-OK-TO-PROCESS
027400         UNTIL OK-TO-PROCESS = "Y" OR "N".
027500
027600 2110-ACCEPT-OK-TO-PROCESS.
027700     DISPLAY "PRINT SALES REPORT (Y/N)?".
027800     ACCEPT OK-TO-PROCESS.
027900     INSPECT OK-TO-PROCESS
028000       CONVERTING LOWER-ALPHA
028100       TO         UPPER-ALPHA.
028200
028300 2120-RE-ACCEPT-OK-TO-PROCESS.
028400     DISPLAY "YOU MUST ENTER YES OR NO".
028500     PERFORM 2110-ACCEPT-OK-TO-PROCESS.
028600
028700 3000-PROCESS-THE-FILE.
028800     PERFORM 3100-START-THE-FILE.
028900     PERFORM 4000-PRINT-ONE-REPORT.
029000     PERFORM 3200-END-THE-FILE.
029100
029200     MOVE "N" TO OK-TO-PROCESS.
029300
029400 3100-START-THE-FILE.
029500     PERFORM 3110-SORT-DATA-FILE.
029600     OPEN INPUT WORK-FILE.
029700
029800 3110-SORT-DATA-FILE.
029900     SORT SORT-FILE
030000         ON ASCENDING KEY SORT-STORE
030100            ASCENDING KEY SORT-DIVISION
030200            ASCENDING KEY SORT-DEPARTMENT
030300            ASCENDING KEY SORT-CATEGORY
030400          USING SALES-FILE
030500          GIVING WORK-FILE.
030600
030700 3200-END-THE-FILE.
030800     CLOSE WORK-FILE.
030900
031000* LEVEL 1 CONTROL BREAK
031100 4000-PRINT-ONE-REPORT.
031200     PERFORM 4100-START-ONE-REPORT.
031300     PERFORM 5000-PROCESS-ALL-STORES
031400         UNTIL WORK-FILE-AT-END = "Y".
031500     PERFORM 4200-END-ONE-REPORT.
031600
031700 4100-START-ONE-REPORT.
031800     PERFORM READ-FIRST-VALID-WORK.
031900     MOVE ZEROES TO GRAND-TOTAL.
032000
032100     PERFORM 4110-START-NEW-REPORT.
032200
032300 4110-START-NEW-REPORT.
032400     MOVE SPACE TO DETAIL-LINE.
032500     MOVE ZEROES TO LINE-COUNT PAGE-NUMBER.
032600
032700     ACCEPT RUN-DATE FROM DATE.
032800     MOVE RUN-DATE TO DATE-CCYYMMDD.
032900     IF DATE-YY > 90
033000         MOVE 19 TO DATE-CC
033100     ELSE
033200         MOVE 20 TO DATE-CC.
033300
033400     PERFORM FORMAT-THE-DATE.
033500     MOVE FORMATTED-DATE TO FORMATTED-RUN-DATE.
033600
033700     ACCEPT RUN-TIME FROM TIME.
033800     COMPUTE TIME-HHMMSS = RUN-TIME / 100.
033900     PERFORM FORMAT-THE-TIME.
034000     MOVE FORMATTED-TIME TO FORMATTED-RUN-TIME.
034100
034200
034300 4200-END-ONE-REPORT.
034400     IF RECORD-COUNT = ZEROES
034500         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
034600         PERFORM WRITE-TO-PRINTER
034700     ELSE
034800         PERFORM 4210-PRINT-GRAND-TOTAL.
034900
035000     PERFORM END-LAST-PAGE.
035100
035200 4210-PRINT-GRAND-TOTAL.
035300     MOVE SPACE TO TOTAL-LINE.
035400     MOVE GRAND-TOTAL TO PRINT-TOTAL.
035500     MOVE GRAND-TOTAL-LITERAL TO TOTAL-TYPE.
035600     MOVE "TOTAL" TO TOTAL-LITERAL.
035700     MOVE TOTAL-LINE TO PRINTER-RECORD.
035800     PERFORM WRITE-TO-PRINTER.
035900     PERFORM LINE-FEED 2 TIMES.
036000     MOVE SPACE TO DETAIL-LINE.
036100
036200* LEVEL 2 CONTROL BREAK
036300 5000-PROCESS-ALL-STORES.
036400     PERFORM 5100-START-ONE-STORE.
036500
036600     PERFORM 6000-PROCESS-ALL-DIVISIONS
036700         UNTIL WORK-FILE-AT-END = "Y"
036800            OR WORK-STORE NOT = CURRENT-STORE.
036900
037000     PERFORM 5200-END-ONE-STORE.
037100
037200 5100-START-ONE-STORE.
037300     MOVE WORK-STORE TO CURRENT-STORE.
037400     MOVE ZEROES TO STORE-TOTAL.
037500     MOVE WORK-STORE TO PRINT-STORE.
037600
037700     PERFORM START-NEXT-PAGE.
037800
037900 5200-END-ONE-STORE.
038000     PERFORM 5210-PRINT-STORE-TOTAL.
038100     ADD STORE-TOTAL TO GRAND-TOTAL.
038200
038300 5210-PRINT-STORE-TOTAL.
038400     MOVE SPACE TO TOTAL-LINE.
038500     MOVE STORE-TOTAL TO PRINT-TOTAL.
038600     MOVE CURRENT-STORE TO TOTAL-NUMBER.
038700     MOVE STORE-TOTAL-LITERAL TO TOTAL-TYPE.
038800     MOVE "TOTAL" TO TOTAL-LITERAL.
038900     MOVE TOTAL-LINE TO PRINTER-RECORD.
039000     PERFORM WRITE-TO-PRINTER.
039100     PERFORM LINE-FEED.
039200     MOVE SPACE TO DETAIL-LINE.
039300
039400* LEVEL 3 CONTROL BREAK
039500 6000-PROCESS-ALL-DIVISIONS.
039600     PERFORM 6100-START-ONE-DIVISION.
039700
039800     PERFORM 7000-PROCESS-ALL-DEPARTMENTS
039900         UNTIL WORK-FILE-AT-END = "Y"
040000            OR WORK-STORE NOT = CURRENT-STORE
040100            OR WORK-DIVISION NOT = CURRENT-DIVISION.
040200
040300     PERFORM 6200-END-ONE-DIVISION.
040400
040500 6100-START-ONE-DIVISION.
040600     MOVE WORK-DIVISION TO CURRENT-DIVISION.
040700     MOVE ZEROES TO DIVISION-TOTAL.
040800     MOVE WORK-DIVISION TO PRINT-DIVISION.
040900
041000 6200-END-ONE-DIVISION.
041100     PERFORM 6210-PRINT-DIVISION-TOTAL.
041200     ADD DIVISION-TOTAL TO STORE-TOTAL.
041300
041400 6210-PRINT-DIVISION-TOTAL.
041500     MOVE SPACE TO TOTAL-LINE.
041600     MOVE DIVISION-TOTAL TO PRINT-TOTAL.
041700     MOVE CURRENT-DIVISION TO TOTAL-NUMBER.
041800     MOVE DIVISION-TOTAL-LITERAL TO TOTAL-TYPE.
041900     MOVE "TOTAL" TO TOTAL-LITERAL.
042000     PERFORM 6220-LOAD-DIVISION-NAME.
042100     MOVE TOTAL-LINE TO PRINTER-RECORD.
042200     PERFORM WRITE-TO-PRINTER.
042300     PERFORM LINE-FEED.
042400     MOVE SPACE TO DETAIL-LINE.
042500
042600 6220-LOAD-DIVISION-NAME.
042700     SET DIVISION-INDEX TO 1.
042800     SEARCH DIVISION-TABLE
042900         AT END
043000           MOVE "NOT FOUND" TO TOTAL-NAME
043100         WHEN
043200           DIVISION-NUMBER(DIVISION-INDEX) =
043300              CURRENT-DIVISION
043400              MOVE DIVISION-NAME(DIVISION-INDEX) TO
043500                   TOTAL-NAME.
043600
043700* LEVEL 4 CONTROL BREAK
043800 7000-PROCESS-ALL-DEPARTMENTS.
043900     PERFORM 7100-START-ONE-DEPARTMENT.
044000
044100     PERFORM 8000-PROCESS-ALL-CATEGORIES
044200         UNTIL WORK-FILE-AT-END = "Y"
044300            OR WORK-STORE NOT = CURRENT-STORE
044400            OR WORK-DIVISION NOT = CURRENT-DIVISION
044500            OR WORK-DEPARTMENT NOT = CURRENT-DEPARTMENT.
044600
044700     PERFORM 7200-END-ONE-DEPARTMENT.
044800
044900 7100-START-ONE-DEPARTMENT.
045000     MOVE WORK-DEPARTMENT TO CURRENT-DEPARTMENT.
045100     MOVE ZEROES TO DEPARTMENT-TOTAL.
045200     MOVE WORK-DEPARTMENT TO PRINT-DEPARTMENT.
045300
045400 7200-END-ONE-DEPARTMENT.
045500     PERFORM 7210-PRINT-DEPARTMENT-TOTAL.
045600     ADD DEPARTMENT-TOTAL TO DIVISION-TOTAL.
045700
045800 7210-PRINT-DEPARTMENT-TOTAL.
045900     MOVE SPACE TO TOTAL-LINE.
046000     MOVE DEPARTMENT-TOTAL TO PRINT-TOTAL.
046100     MOVE CURRENT-DEPARTMENT TO TOTAL-NUMBER.
046200     MOVE DEPARTMENT-TOTAL-LITERAL TO TOTAL-TYPE.
046300     MOVE "TOTAL" TO TOTAL-LITERAL.
046400     PERFORM LOAD-DEPARTMENT-NAME.
046500     MOVE TOTAL-LINE TO PRINTER-RECORD.
046600     PERFORM WRITE-TO-PRINTER.
046700     PERFORM LINE-FEED.
046800     MOVE SPACE TO DETAIL-LINE.
046900
047000 LOAD-DEPARTMENT-NAME.
047100     SET DEPARTMENT-INDEX TO 1.
047200     SEARCH DEPARTMENT-TABLE
047300         AT END
047400           MOVE "NOT FOUND" TO TOTAL-NAME
047500         WHEN
047600           DEPARTMENT-NUMBER(DEPARTMENT-INDEX) =
047700              CURRENT-DEPARTMENT
047800              MOVE DEPARTMENT-NAME(DEPARTMENT-INDEX) TO
047900                   TOTAL-NAME.
048000
048100* PROCESS ONE RECORD LEVEL
048200 8000-PROCESS-ALL-CATEGORIES.
048300     PERFORM 8100-PROCESS-THIS-CATEGORY.
048400     ADD WORK-AMOUNT TO DEPARTMENT-TOTAL.
048500     ADD 1 TO RECORD-COUNT.
048600     PERFORM READ-NEXT-VALID-WORK.
048700
048800 8100-PROCESS-THIS-CATEGORY.
048900     IF LINE-COUNT > MAXIMUM-LINES
049000         PERFORM START-NEXT-PAGE.
049100     PERFORM 8110-PRINT-THE-RECORD.
049200
049300 8110-PRINT-THE-RECORD.
049400     MOVE WORK-CATEGORY TO PRINT-CATEGORY.
049500
049600     PERFORM 8120-LOAD-CATEGORY-NAME.
049700
049800     MOVE WORK-AMOUNT TO PRINT-AMOUNT.
049900
050000     MOVE DETAIL-LINE TO PRINTER-RECORD.
050100     PERFORM WRITE-TO-PRINTER.
050200     MOVE SPACE TO DETAIL-LINE.
050300
050400 8120-LOAD-CATEGORY-NAME.
050500     SET CATEGORY-INDEX TO 1.
050600     SEARCH CATEGORY-TABLE
050700         AT END
050800           MOVE "NOT FOUND" TO TOTAL-NAME
050900         WHEN
051000           CATEGORY-NUMBER(CATEGORY-INDEX) =
051100              WORK-CATEGORY
051200              MOVE CATEGORY-NAME(CATEGORY-INDEX) TO
051300                   PRINT-CATEGORY-NAME.
051400
051500* PRINTING ROUTINES
051600 WRITE-TO-PRINTER.
051700     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
051800     ADD 1 TO LINE-COUNT.
051900
052000 LINE-FEED.
052100     MOVE SPACE TO PRINTER-RECORD.
052200     PERFORM WRITE-TO-PRINTER.
052300
052400 START-NEXT-PAGE.
052500     PERFORM END-LAST-PAGE.
052600     PERFORM START-NEW-PAGE.
052700
052800 START-NEW-PAGE.
052900     ADD 1 TO PAGE-NUMBER.
053000     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
053100     MOVE TITLE-LINE TO PRINTER-RECORD.
053200     PERFORM WRITE-TO-PRINTER.
053300     PERFORM LINE-FEED.
053400     MOVE LEGEND-LINE TO PRINTER-RECORD.
053500     PERFORM WRITE-TO-PRINTER.
053600     PERFORM LINE-FEED.
053700     MOVE COLUMN-LINE TO PRINTER-RECORD.
053800     PERFORM WRITE-TO-PRINTER.
053900     PERFORM LINE-FEED.
054000
054100 END-LAST-PAGE.
054200     IF PAGE-NUMBER > 0
054300         PERFORM FORM-FEED.
054400     MOVE ZERO TO LINE-COUNT.
054500
054600 FORM-FEED.
054700     MOVE SPACE TO PRINTER-RECORD.
054800     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
054900
055000*---------------------------------
055100* Read first, read next routines
055200*---------------------------------
055300 READ-FIRST-VALID-WORK.
055400     PERFORM READ-NEXT-VALID-WORK.
055500
055600 READ-NEXT-VALID-WORK.
055700     PERFORM READ-NEXT-WORK-RECORD.
055800
055900 READ-NEXT-WORK-RECORD.
056000     MOVE "N" TO WORK-FILE-AT-END.
056100     READ WORK-FILE NEXT RECORD
056200         AT END MOVE "Y" TO WORK-FILE-AT-END.
056300
056400* Date and time routines
056500 FORMAT-THE-DATE.
056600     PERFORM CONVERT-TO-MMDDCCYY.
056700     MOVE DATE-MMDDCCYY TO FORMATTED-DATE.
056800
056900 CONVERT-TO-MMDDCCYY.
057000     COMPUTE DATE-MMDDCCYY =
057100             DATE-CCYYMMDD * 10000.0001.
057200
057300 FORMAT-THE-TIME.
057400     MOVE TIME-HHMMSS TO FORMATTED-TIME.
057500     INSPECT FORMATTED-TIME
057600       REPLACING ALL "/" BY ":".
057700
