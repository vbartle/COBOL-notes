000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. STCRPT01.
000300*---------------------------------
000400* Report on the STATE File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLSTATE.CBL".
001100
001200     SELECT PRINTER-FILE
001300         ASSIGN TO PRINTER
001400         ORGANIZATION IS LINE SEQUENTIAL.
001500
001600 DATA DIVISION.
001700 FILE SECTION.
001800
001900     COPY "FDSTATE.CBL".
002000
002100 FD  PRINTER-FILE
002200     LABEL RECORDS ARE OMITTED.
002300 01  PRINTER-RECORD             PIC X(80).
002400
002500 WORKING-STORAGE SECTION.
002600
002700 01  DETAIL-LINE.
002800     05  PRINT-CODE        PIC XX.
002900     05  FILLER            PIC XXXX     VALUE SPACE.
003000     05  PRINT-NAME        PIC X(20).
003100
003200 01  COLUMN-LINE.
003300     05  FILLER         PIC X(4)  VALUE "CODE".
003400     05  FILLER         PIC X(2) VALUE SPACE.
003500     05  FILLER         PIC X(4) VALUE "NAME".
003600
003700 01  TITLE-LINE.
003800     05  FILLER              PIC X(25) VALUE SPACE.
003900     05  FILLER              PIC X(11)
004000         VALUE "STATE CODES".
004100     05  FILLER              PIC X(15) VALUE SPACE.
004200     05  FILLER              PIC X(5) VALUE "PAGE:".
004300     05  FILLER              PIC X(1) VALUE SPACE.
004400     05  PRINT-PAGE-NUMBER   PIC ZZZZ9.
004500
004600 77  FILE-AT-END             PIC X.
004700 77  LINE-COUNT              PIC 999 VALUE ZERO.
004800 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
004900 77  MAXIMUM-LINES           PIC 999 VALUE 55.
005000
005100 PROCEDURE DIVISION.
005200 PROGRAM-BEGIN.
005300
005400     PERFORM OPENING-PROCEDURE.
005500     MOVE ZEROES TO LINE-COUNT
005600                    PAGE-NUMBER.
005700
005800     PERFORM START-NEW-PAGE.
005900
006000     MOVE "N" TO FILE-AT-END.
006100     PERFORM READ-NEXT-RECORD.
006200     IF FILE-AT-END = "Y"
006300         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
006400         PERFORM WRITE-TO-PRINTER
006500     ELSE
006600         PERFORM PRINT-STATE-FIELDS
006700             UNTIL FILE-AT-END = "Y".
006800
006900     PERFORM CLOSING-PROCEDURE.
007000
007100 PROGRAM-DONE.
007200     ACCEPT OMITTED. STOP RUN.
007300
007400 OPENING-PROCEDURE.
007500     OPEN I-O STATE-FILE.
007600     OPEN OUTPUT PRINTER-FILE.
007700
007800 CLOSING-PROCEDURE.
007900     CLOSE STATE-FILE.
008000     PERFORM END-LAST-PAGE.
008100     CLOSE PRINTER-FILE.
008200
008300 PRINT-STATE-FIELDS.
008400     IF LINE-COUNT > MAXIMUM-LINES
008500         PERFORM START-NEXT-PAGE.
008600     PERFORM PRINT-THE-RECORD.
008700     PERFORM READ-NEXT-RECORD.
008800
008900 PRINT-THE-RECORD.
009000     MOVE SPACE TO DETAIL-LINE.
009100     MOVE STATE-CODE TO PRINT-CODE.
009200     MOVE STATE-NAME TO PRINT-NAME.
009300     MOVE DETAIL-LINE TO PRINTER-RECORD.
009400     PERFORM WRITE-TO-PRINTER.
009500
009600 READ-NEXT-RECORD.
009700     READ STATE-FILE NEXT RECORD
009800         AT END MOVE "Y" TO FILE-AT-END.
009900
010000 WRITE-TO-PRINTER.
010100     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
010200     ADD 1 TO LINE-COUNT.
010300
010400 LINE-FEED.
010500     MOVE SPACE TO PRINTER-RECORD.
010600     PERFORM WRITE-TO-PRINTER.
010700
010800 START-NEXT-PAGE.
010900
011000     PERFORM END-LAST-PAGE.
011100     PERFORM START-NEW-PAGE.
011200
011300 START-NEW-PAGE.
011400     ADD 1 TO PAGE-NUMBER.
011500     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
011600     MOVE TITLE-LINE TO PRINTER-RECORD.
011700     PERFORM WRITE-TO-PRINTER.
011800     PERFORM LINE-FEED.
011900     MOVE COLUMN-LINE TO PRINTER-RECORD.
012000     PERFORM WRITE-TO-PRINTER.
012100     PERFORM LINE-FEED.
012200
012300 END-LAST-PAGE.
012400     PERFORM FORM-FEED.
012500     MOVE ZERO TO LINE-COUNT.
012600
012700 FORM-FEED.
012800     MOVE SPACE TO PRINTER-RECORD.
012900     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
