000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDRPT01.
000300*---------------------------------
000400* Report on the Vendor File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND01.CBL".
001100
001200     SELECT PRINTER-FILE
001300         ASSIGN TO PRINTER
001400         ORGANIZATION IS LINE SEQUENTIAL.
001500
001600 DATA DIVISION.
001700 FILE SECTION.
001800
001900     COPY "FDVND02.CBL".
002000
002100 FD  PRINTER-FILE
002200     LABEL RECORDS ARE OMITTED.
002300 01  PRINTER-RECORD             PIC X(80).
002400
002500 WORKING-STORAGE SECTION.
002600
002700 01  DETAIL-LINE.
002800     05  PRINT-NUMBER      PIC 9(5).
002900     05  FILLER            PIC X     VALUE SPACE.
003000     05  PRINT-NAME        PIC X(30).
003100     05  FILLER            PIC X     VALUE SPACE.
003200     05  PRINT-CONTACT     PIC X(30).
003300
003400 01  CITY-STATE-DETAIL.
003500     05  PRINT-CITY        PIC X(20).
003600     05  FILLER            PIC X VALUE SPACE.
003700     05  PRINT-STATE       PIC X(2).
003800
003900 01  COLUMN-LINE.
004000     05  FILLER         PIC X(2)  VALUE "NO".
004100     05  FILLER         PIC X(4) VALUE SPACE.
004200     05  FILLER         PIC X(12) VALUE "NAME-ADDRESS".
004300     05  FILLER         PIC X(19) VALUE SPACE.
004400     05  FILLER         PIC X(17) VALUE "CONTACT-PHONE-ZIP".
004500
004600 01  TITLE-LINE.
004700     05  FILLER              PIC X(25) VALUE SPACE.
004800     05  FILLER              PIC X(11)
004900         VALUE "VENDOR LIST".
005000     05  FILLER              PIC X(15) VALUE SPACE.
005100     05  FILLER              PIC X(5) VALUE "PAGE:".
005200     05  FILLER              PIC X(1) VALUE SPACE.
005300     05  PRINT-PAGE-NUMBER PIC ZZZZ9.
005400
005500 77  FILE-AT-END             PIC X.
005600 77  LINE-COUNT              PIC 999 VALUE ZERO.
005700 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
005800 77  MAXIMUM-LINES           PIC 999 VALUE 55.
005900
006000 PROCEDURE DIVISION.
006100 PROGRAM-BEGIN.
006200
006300     PERFORM OPENING-PROCEDURE.
006400     MOVE ZEROES TO LINE-COUNT
006500                    PAGE-NUMBER.
006600
006700     PERFORM START-NEW-PAGE.
006800
006900     MOVE "N" TO FILE-AT-END.
007000     PERFORM READ-NEXT-RECORD.
007100     IF FILE-AT-END = "Y"
007200         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
007300         PERFORM WRITE-TO-PRINTER
007400     ELSE
007500         PERFORM PRINT-VENDOR-FIELDS
007600             UNTIL FILE-AT-END = "Y".
007700
007800     PERFORM CLOSING-PROCEDURE.
007900
008000 PROGRAM-DONE.
008100     ACCEPT OMITTED. STOP RUN.
008200
008300 OPENING-PROCEDURE.
008400     OPEN I-O VENDOR-FILE.
008500     OPEN OUTPUT PRINTER-FILE.
008600
008700 CLOSING-PROCEDURE.
008800     CLOSE VENDOR-FILE.
008900     PERFORM END-LAST-PAGE.
009000     CLOSE PRINTER-FILE.
009100
009200 PRINT-VENDOR-FIELDS.
009300     IF LINE-COUNT > MAXIMUM-LINES
009400         PERFORM START-NEXT-PAGE.
009500     PERFORM PRINT-THE-RECORD.
009600     PERFORM READ-NEXT-RECORD.
009700
009800 PRINT-THE-RECORD.
009900     PERFORM PRINT-LINE-1.
010000     PERFORM PRINT-LINE-2.
010100     PERFORM PRINT-LINE-3.
010200     PERFORM PRINT-LINE-4.
010300     PERFORM LINE-FEED.
010400
010500 PRINT-LINE-1.
010600     MOVE SPACE TO DETAIL-LINE.
010700     MOVE VENDOR-NUMBER TO PRINT-NUMBER.
010800     MOVE VENDOR-NAME TO PRINT-NAME.
010900     MOVE VENDOR-CONTACT TO PRINT-CONTACT.
011000     MOVE DETAIL-LINE TO PRINTER-RECORD.
011100     PERFORM WRITE-TO-PRINTER.
011200
011300 PRINT-LINE-2.
011400     MOVE SPACE TO DETAIL-LINE.
011500     MOVE VENDOR-ADDRESS-1 TO PRINT-NAME.
011600     MOVE VENDOR-PHONE TO PRINT-CONTACT.
011700     MOVE DETAIL-LINE TO PRINTER-RECORD.
011800     PERFORM WRITE-TO-PRINTER.
011900
012000 PRINT-LINE-3.
012100     MOVE SPACE TO DETAIL-LINE.
012200     MOVE VENDOR-ADDRESS-2 TO PRINT-NAME.
012300     IF VENDOR-ADDRESS-2 NOT = SPACE
012400         MOVE DETAIL-LINE TO PRINTER-RECORD
012500         PERFORM WRITE-TO-PRINTER.
012600
012700 PRINT-LINE-4.
012800     MOVE SPACE TO DETAIL-LINE.
012900     MOVE VENDOR-CITY TO PRINT-CITY.
013000     MOVE VENDOR-STATE TO PRINT-STATE.
013100     MOVE CITY-STATE-DETAIL TO PRINT-NAME.
013200     MOVE VENDOR-ZIP TO PRINT-CONTACT.
013300     MOVE DETAIL-LINE TO PRINTER-RECORD.
013400     PERFORM WRITE-TO-PRINTER.
013500
013600 READ-NEXT-RECORD.
013700     READ VENDOR-FILE NEXT RECORD
013800         AT END MOVE "Y" TO FILE-AT-END.
013900
014000 WRITE-TO-PRINTER.
014100     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
014200     ADD 1 TO LINE-COUNT.
014300
014400 LINE-FEED.
014500     MOVE SPACE TO PRINTER-RECORD.
014600     PERFORM WRITE-TO-PRINTER.
014700
014800 START-NEXT-PAGE.
014900
015000     PERFORM END-LAST-PAGE.
015100     PERFORM START-NEW-PAGE.
015200
015300 START-NEW-PAGE.
015400     ADD 1 TO PAGE-NUMBER.
015500     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
015600     MOVE TITLE-LINE TO PRINTER-RECORD.
015700     PERFORM WRITE-TO-PRINTER.
015800     PERFORM LINE-FEED.
015900     MOVE COLUMN-LINE TO PRINTER-RECORD.
016000     PERFORM WRITE-TO-PRINTER.
016100     PERFORM LINE-FEED.
016200
016300 END-LAST-PAGE.
016400     PERFORM FORM-FEED.
016500     MOVE ZERO TO LINE-COUNT.
016600
016700 FORM-FEED.
016800     MOVE SPACE TO PRINTER-RECORD.
016900     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
017000
