000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. DATE02.
000300*---------------------------------
000400* Demo of Date Entry and validation
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000 DATA DIVISION.
001100 FILE SECTION.
001200
001300 WORKING-STORAGE SECTION.
001400
001500 77  DATE-ENTRY-FIELD   PIC Z9/99/9999.
001600 77  DATE-MMDDCCYY      PIC 9(8).
001700 77  DATE-QUOTIENT      PIC 9999.
001800 77  DATE-REMAINDER     PIC 9999.
001900
002000 77  VALID-DATE-FLAG    PIC X.
002100     88  DATE-IS-INVALID  VALUE "N".
002200     88  DATE-IS-ZERO     VALUE "0".
002300     88  DATE-IS-VALID    VALUE "Y".
002400     88  DATE-IS-OK       VALUES "Y" "0".
002500
002600 01  DATE-CCYYMMDD      PIC 9(8).
002700 01  FILLER REDEFINES DATE-CCYYMMDD.
002800     05  DATE-CCYY      PIC 9999.
002900     05  DATE-MM        PIC 99.
003000     05  DATE-DD        PIC 99.
003100
003200
003300 PROCEDURE DIVISION.
003400 PROGRAM-BEGIN.
003500     PERFORM OPENING-PROCEDURE.
003600     PERFORM MAIN-PROCESS.
003700     PERFORM CLOSING-PROCEDURE.
003800
003900 PROGRAM-EXIT.
004000     EXIT PROGRAM.
004100
004200 PROGRAM-DONE.
004300     ACCEPT OMITTED. STOP RUN.
004400
004500 OPENING-PROCEDURE.
004600
004700 CLOSING-PROCEDURE.
004800
004900 MAIN-PROCESS.
005000     PERFORM GET-A-DATE.
005100     PERFORM DISPLAY-AND-GET-DATE
005200         UNTIL DATE-MMDDCCYY = ZEROES.
005300
005400 GET-A-DATE.
005500     PERFORM ACCEPT-A-DATE.
005600     PERFORM RE-ACCEPT-A-DATE
005700         UNTIL DATE-IS-OK.
005800
005900 ACCEPT-A-DATE.
006000     DISPLAY "ENTER A DATE (MM/DD/CCYY)".
006100     ACCEPT DATE-ENTRY-FIELD.
006200
006300     PERFORM EDIT-CHECK-DATE.
006400
006500 RE-ACCEPT-A-DATE.
006600     DISPLAY "INVALID DATE".
006700     PERFORM ACCEPT-A-DATE.
006800
006900 EDIT-CHECK-DATE.
007000     PERFORM EDIT-DATE.
007100     PERFORM CHECK-DATE.
007200
007300 EDIT-DATE.
007400     MOVE DATE-ENTRY-FIELD TO DATE-MMDDCCYY.
007500     COMPUTE DATE-CCYYMMDD =
007600             DATE-MMDDCCYY * 10000.0001.
007700
007800*---------------------------------
007900* Assume that the date is good, then
008000* test the date in the following
008100* steps. The routine stops if any
008200* of these conditions is true,
008300* and sets the valid date flag.
008400* Condition 1 returns the valid date
008500* flag set to "0".
008600* If any other condition is true,
008700* the valid date flag is set to "N".
008800* 1.  Is the date zeroes
008900* 2.  Month > 12 or < 1
009000* 3.  Day < 1 or  > 31
009100* 4.  Day > 30 and
009200*     Month = 2 (February)  or
009300*             4 (April)     or
009400*             6 (June)      or
009500*             9 (September) or
009600*            11 (November)
009700*     Day > 29 and
009800*     Month = 2 (February)
009900* 5.  Day = 29 and Month = 2 and
010000*     Not a leap year
010100* ( A leap year is any year evenly
010200*   divisible by 4, but does not
010300*   end in 00 and that is
010400*   not evenly divisible by 400).
010500*---------------------------------
010600 CHECK-DATE.
010700     MOVE "Y" TO VALID-DATE-FLAG.
010800     PERFORM CHECK-IF-DATE-ZEROES.
010900     IF DATE-IS-VALID
011000         PERFORM CHECK-MM.
011100     IF DATE-IS-VALID
011200         PERFORM CHECK-DD.
011300     IF DATE-IS-VALID
011400         PERFORM CHECK-MMDD.
011500     IF DATE-IS-VALID
011600         PERFORM CHECK-LEAP-YEAR.
011700
011800 CHECK-IF-DATE-ZEROES.
011900     IF DATE-CCYYMMDD = ZEROES
012000         MOVE "0" TO VALID-DATE-FLAG.
012100
012200 CHECK-MM.
012300     IF DATE-MM < 1 OR DATE-MM > 12
012400         MOVE "N" TO VALID-DATE-FLAG.
012500
012600 CHECK-DD.
012700     IF DATE-DD < 1 OR DATE-DD > 31
012800         MOVE "N" TO VALID-DATE-FLAG.
012900
013000 CHECK-MMDD.
013100     IF (DATE-DD > 30) AND
013200        (DATE-MM = 2 OR 4 OR 6 OR 9 OR 11)
013300         MOVE "N" TO VALID-DATE-FLAG
013400     ELSE
013500     IF DATE-DD > 29 AND DATE-MM = 2
013600         MOVE "N" TO VALID-DATE-FLAG.
013700
013800 CHECK-LEAP-YEAR.
013900     IF DATE-DD = 29 AND DATE-MM = 2
014000         DIVIDE DATE-CCYY BY 400 GIVING DATE-QUOTIENT
014100                REMAINDER DATE-REMAINDER
014200         IF DATE-REMAINDER = 0
014300             MOVE "Y" TO VALID-DATE-FLAG
014400         ELSE
014500             DIVIDE DATE-CCYY BY 100 GIVING DATE-QUOTIENT
014600                    REMAINDER DATE-REMAINDER
014700             IF DATE-REMAINDER = 0
014800                 MOVE "N" TO VALID-DATE-FLAG
014900             ELSE
015000                 DIVIDE DATE-CCYY BY 4 GIVING DATE-QUOTIENT
015100                        REMAINDER DATE-REMAINDER
015200                 IF DATE-REMAINDER = 0
015300                     MOVE "Y" TO VALID-DATE-FLAG
015400                 ELSE
015500                     MOVE "N" TO VALID-DATE-FLAG.
015600
015700 DISPLAY-AND-GET-DATE.
015800     PERFORM DISPLAY-A-DATE.
015900     PERFORM GET-A-DATE.
016000
016100 DISPLAY-A-DATE.
016200     MOVE DATE-MMDDCCYY TO DATE-ENTRY-FIELD.
016300     DISPLAY "FORMATTED DATE IS " DATE-ENTRY-FIELD.
016400     DISPLAY "DATE-MMDDCCYY IS " DATE-MMDDCCYY.
016500     DISPLAY "DATE-CCYYMMDD IS " DATE-CCYYMMDD.
016600
