000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDRPT02.
000300*---------------------------------
000400* Report on the Vendor File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND01.CBL".
001100
001200     COPY "SLSTATE.CBL".
001300
001400     SELECT PRINTER-FILE
001500         ASSIGN TO PRINTER
001600         ORGANIZATION IS LINE SEQUENTIAL.
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100     COPY "FDVND02.CBL".
002200
002300     COPY "FDSTATE.CBL".
002400
002500 FD  PRINTER-FILE
002600     LABEL RECORDS ARE OMITTED.
002700 01  PRINTER-RECORD             PIC X(80).
002800
002900 WORKING-STORAGE SECTION.
003000
003100 01  DETAIL-LINE.
003200     05  PRINT-NUMBER      PIC 9(5).
003300     05  FILLER            PIC X     VALUE SPACE.
003400     05  PRINT-NAME        PIC X(30).
003500     05  FILLER            PIC X(15) VALUE SPACE.
003600     05  PRINT-CONTACT     PIC X(30).
003700
003800 01  CITY-STATE-LINE.
003900     05  FILLER            PIC X(6) VALUE SPACE.
004000     05  PRINT-CITY        PIC X(20).
004100     05  FILLER            PIC X VALUE SPACE.
004200     05  PRINT-STATE       PIC X(2).
004300     05  FILLER            PIC X VALUE SPACE.
004400     05  PRINT-STATE-NAME  PIC X(20).
004500     05  FILLER            PIC X(1) VALUE SPACE.
004600     05  PRINT-ZIP         PIC X(10).
004700
004800 01  COLUMN-LINE.
004900     05  FILLER         PIC X(2)  VALUE "NO".
005000     05  FILLER         PIC X(4) VALUE SPACE.
005100     05  FILLER         PIC X(12) VALUE "NAME-ADDRESS".
005200     05  FILLER         PIC X(33) VALUE SPACE.
005300     05  FILLER         PIC X(17) VALUE "CONTACT-PHONE-ZIP".
005400
005500 01  TITLE-LINE.
005600     05  FILLER              PIC X(25) VALUE SPACE.
005700     05  FILLER              PIC X(11)
005800         VALUE "VENDOR LIST".
005900     05  FILLER              PIC X(19) VALUE SPACE.
006000     05  FILLER              PIC X(5) VALUE "PAGE:".
006100     05  FILLER              PIC X(1) VALUE SPACE.
006200     05  PRINT-PAGE-NUMBER PIC ZZZZ9.
006300
006400 77  FILE-AT-END             PIC X.
006500 77  STATE-RECORD-FOUND      PIC X VALUE "N".
006600 77  LINE-COUNT              PIC 999 VALUE ZERO.
006700 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
006800 77  MAXIMUM-LINES           PIC 999 VALUE 55.
006900
007000 PROCEDURE DIVISION.
007100 PROGRAM-BEGIN.
007200
007300     PERFORM OPENING-PROCEDURE.
007400     MOVE ZEROES TO LINE-COUNT
007500                    PAGE-NUMBER.
007600
007700     PERFORM START-NEW-PAGE.
007800
007900     MOVE "N" TO FILE-AT-END.
008000     PERFORM READ-NEXT-RECORD.
008100     IF FILE-AT-END = "Y"
008200         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
008300         PERFORM WRITE-TO-PRINTER
008400     ELSE
008500         PERFORM PRINT-VENDOR-FIELDS
008600             UNTIL FILE-AT-END = "Y".
008700
008800     PERFORM CLOSING-PROCEDURE.
008900
009000 PROGRAM-DONE.
009100     ACCEPT OMITTED. STOP RUN.
009200
009300 OPENING-PROCEDURE.
009400     OPEN I-O VENDOR-FILE.
009500     OPEN I-O STATE-FILE.
009600     OPEN OUTPUT PRINTER-FILE.
009700
009800 CLOSING-PROCEDURE.
009900     CLOSE VENDOR-FILE.
010000     CLOSE STATE-FILE.
010100     PERFORM END-LAST-PAGE.
010200     CLOSE PRINTER-FILE.
010300
010400 PRINT-VENDOR-FIELDS.
010500     IF LINE-COUNT > MAXIMUM-LINES
010600         PERFORM START-NEXT-PAGE.
010700     PERFORM PRINT-THE-RECORD.
010800     PERFORM READ-NEXT-RECORD.
010900
011000 PRINT-THE-RECORD.
011100     PERFORM PRINT-LINE-1.
011200     PERFORM PRINT-LINE-2.
011300     PERFORM PRINT-LINE-3.
011400     PERFORM PRINT-LINE-4.
011500     PERFORM LINE-FEED.
011600
011700 PRINT-LINE-1.
011800     MOVE SPACE TO DETAIL-LINE.
011900     MOVE VENDOR-NUMBER TO PRINT-NUMBER.
012000     MOVE VENDOR-NAME TO PRINT-NAME.
012100     MOVE VENDOR-CONTACT TO PRINT-CONTACT.
012200     MOVE DETAIL-LINE TO PRINTER-RECORD.
012300     PERFORM WRITE-TO-PRINTER.
012400
012500 PRINT-LINE-2.
012600     MOVE SPACE TO DETAIL-LINE.
012700     MOVE VENDOR-ADDRESS-1 TO PRINT-NAME.
012800     MOVE VENDOR-PHONE TO PRINT-CONTACT.
012900     MOVE DETAIL-LINE TO PRINTER-RECORD.
013000     PERFORM WRITE-TO-PRINTER.
013100
013200 PRINT-LINE-3.
013300     MOVE SPACE TO DETAIL-LINE.
013400     MOVE VENDOR-ADDRESS-2 TO PRINT-NAME.
013500     IF VENDOR-ADDRESS-2 NOT = SPACE
013600         MOVE DETAIL-LINE TO PRINTER-RECORD
013700         PERFORM WRITE-TO-PRINTER.
013800
013900 PRINT-LINE-4.
014000     MOVE SPACE TO CITY-STATE-LINE.
014100     MOVE VENDOR-CITY TO PRINT-CITY.
014200     MOVE VENDOR-STATE TO PRINT-STATE.
014300
014400     MOVE VENDOR-STATE TO STATE-CODE.
014500     PERFORM READ-STATE-RECORD.
014600     IF STATE-RECORD-FOUND = "N"
014700         MOVE "***Not Found***" TO STATE-NAME.
014800     MOVE STATE-NAME TO PRINT-STATE-NAME.
014900
015000     MOVE VENDOR-ZIP TO PRINT-ZIP.
015100     MOVE CITY-STATE-LINE TO PRINTER-RECORD.
015200     PERFORM WRITE-TO-PRINTER.
015300
015400 READ-NEXT-RECORD.
015500     READ VENDOR-FILE NEXT RECORD
015600         AT END MOVE "Y" TO FILE-AT-END.
015700
015800 WRITE-TO-PRINTER.
015900     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
016000     ADD 1 TO LINE-COUNT.
016100
016200 LINE-FEED.
016300     MOVE SPACE TO PRINTER-RECORD.
016400     PERFORM WRITE-TO-PRINTER.
016500
016600 START-NEXT-PAGE.
016700     PERFORM END-LAST-PAGE.
016800     PERFORM START-NEW-PAGE.
016900
017000 START-NEW-PAGE.
017100     ADD 1 TO PAGE-NUMBER.
017200     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
017300     MOVE TITLE-LINE TO PRINTER-RECORD.
017400     PERFORM WRITE-TO-PRINTER.
017500     PERFORM LINE-FEED.
017600     MOVE COLUMN-LINE TO PRINTER-RECORD.
017700     PERFORM WRITE-TO-PRINTER.
017800     PERFORM LINE-FEED.
017900
018000 END-LAST-PAGE.
018100     PERFORM FORM-FEED.
018200     MOVE ZERO TO LINE-COUNT.
018300
018400 FORM-FEED.
018500     MOVE SPACE TO PRINTER-RECORD.
018600     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
018700
018800 READ-STATE-RECORD.
018900     MOVE "Y" TO STATE-RECORD-FOUND.
019000     READ STATE-FILE RECORD
019100         INVALID KEY
019200         MOVE "N" TO STATE-RECORD-FOUND.
019300
