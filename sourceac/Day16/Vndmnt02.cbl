000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDMNT02.
000300*---------------------------------
000400* Add, Change, Inquire and Delete
000500* for the Vendor File.
000600*---------------------------------
000700 ENVIRONMENT DIVISION.
000800 INPUT-OUTPUT SECTION.
000900 FILE-CONTROL.
001000
001100     COPY "SLVND01.CBL".
001200
001300     COPY "SLSTATE.CBL".
001400
001500 DATA DIVISION.
001600 FILE SECTION.
001700
001800     COPY "FDVND03.CBL".
001900
002000     COPY "FDSTATE.CBL".
002100
002200 WORKING-STORAGE SECTION.
002300
002400 77  MENU-PICK                    PIC 9.
002500     88  MENU-PICK-IS-VALID       VALUES 0 THRU 4.
002600
002700 77  THE-MODE                     PIC X(7).
002800 77  WHICH-FIELD                  PIC 9.
002900 77  OK-TO-DELETE                 PIC X.
003000 77  VENDOR-RECORD-FOUND          PIC X.
003100 77  STATE-RECORD-FOUND           PIC X.
003200
003300
003400 77  VENDOR-NUMBER-FIELD          PIC Z(5).
003500
003600 77  ERROR-MESSAGE                PIC X(79) VALUE SPACE.
003700
003800 77  UPPER-ALPHA                  PIC X(26) VALUE
003900     "ABCDEFGHIJKLMNOPQRSTUVWXYZ".
004000 77  LOWER-ALPHA                  PIC X(26) VALUE
004100     "abcdefghijklmnopqrstuvwxyz".
004200
004300 PROCEDURE DIVISION.
004400 PROGRAM-BEGIN.
004500     PERFORM OPENING-PROCEDURE.
004600     PERFORM MAIN-PROCESS.
004700     PERFORM CLOSING-PROCEDURE.
004800
004900 PROGRAM-DONE.
005000     ACCEPT OMITTED. STOP RUN.
005100
005200 OPENING-PROCEDURE.
005300     OPEN I-O VENDOR-FILE.
005400     OPEN I-O STATE-FILE.
005500
005600 CLOSING-PROCEDURE.
005700     CLOSE VENDOR-FILE.
005800     CLOSE STATE-FILE.
005900
006000 MAIN-PROCESS.
006100     PERFORM GET-MENU-PICK.
006200     PERFORM MAINTAIN-THE-FILE
006300         UNTIL MENU-PICK = 0.
006400
006500*---------------------------------
006600* MENU
006700*---------------------------------
006800 GET-MENU-PICK.
006900     PERFORM DISPLAY-THE-MENU.
007000     PERFORM ACCEPT-MENU-PICK.
007100     PERFORM RE-ACCEPT-MENU-PICK
007200         UNTIL MENU-PICK-IS-VALID.
007300
007400 DISPLAY-THE-MENU.
007500     PERFORM CLEAR-SCREEN.
007600     DISPLAY "    PLEASE SELECT:".
007700     DISPLAY " ".
007800     DISPLAY "          1.  ADD RECORDS".
007900     DISPLAY "          2.  CHANGE A RECORD".
008000     DISPLAY "          3.  LOOK UP A RECORD".
008100     DISPLAY "          4.  DELETE A RECORD".
008200     DISPLAY " ".
008300     DISPLAY "          0.  EXIT".
008400     PERFORM SCROLL-LINE 8 TIMES.
008500
008600 ACCEPT-MENU-PICK.
008700     DISPLAY "YOUR CHOICE (0-4)?".
008800     ACCEPT MENU-PICK.
008900
009000 RE-ACCEPT-MENU-PICK.
009100     DISPLAY "INVALID SELECTION - PLEASE RE-TRY.".
009200     PERFORM ACCEPT-MENU-PICK.
009300
009400 CLEAR-SCREEN.
009500     PERFORM SCROLL-LINE 25 TIMES.
009600
009700 SCROLL-LINE.
009800     DISPLAY " ".
009900
010000 MAINTAIN-THE-FILE.
010100     PERFORM DO-THE-PICK.
010200     PERFORM GET-MENU-PICK.
010300
010400 DO-THE-PICK.
010500     IF MENU-PICK = 1
010600         PERFORM ADD-MODE
010700     ELSE
010800     IF MENU-PICK = 2
010900         PERFORM CHANGE-MODE
011000     ELSE
011100     IF MENU-PICK = 3
011200         PERFORM INQUIRE-MODE
011300     ELSE
011400     IF MENU-PICK = 4
011500         PERFORM DELETE-MODE.
011600
011700*---------------------------------
011800* ADD
011900*---------------------------------
012000 ADD-MODE.
012100     MOVE "ADD" TO THE-MODE.
012200     PERFORM GET-NEW-RECORD-KEY.
012300     PERFORM ADD-RECORDS
012400        UNTIL VENDOR-NUMBER = ZEROES.
012500
012600 GET-NEW-RECORD-KEY.
012700     PERFORM ACCEPT-NEW-RECORD-KEY.
012800     PERFORM RE-ACCEPT-NEW-RECORD-KEY
012900         UNTIL VENDOR-RECORD-FOUND = "N" OR
013000               VENDOR-NUMBER = ZEROES.
013100
013200 ACCEPT-NEW-RECORD-KEY.
013300     PERFORM INIT-VENDOR-RECORD.
013400     PERFORM ENTER-VENDOR-NUMBER.
013500     IF VENDOR-NUMBER NOT = ZEROES
013600         PERFORM READ-VENDOR-RECORD.
013700
013800 RE-ACCEPT-NEW-RECORD-KEY.
013900     DISPLAY "RECORD ALREADY ON FILE"
014000     PERFORM ACCEPT-NEW-RECORD-KEY.
014100
014200 ADD-RECORDS.
014300     PERFORM ENTER-REMAINING-FIELDS.
014400     PERFORM WRITE-VENDOR-RECORD.
014500     PERFORM GET-NEW-RECORD-KEY.
014600
014700 ENTER-REMAINING-FIELDS.
014800     PERFORM ENTER-VENDOR-NAME.
014900     PERFORM ENTER-VENDOR-ADDRESS-1.
015000     PERFORM ENTER-VENDOR-ADDRESS-2.
015100     PERFORM ENTER-VENDOR-CITY.
015200     PERFORM ENTER-VENDOR-STATE.
015300     PERFORM ENTER-VENDOR-ZIP.
015400     PERFORM ENTER-VENDOR-CONTACT.
015500     PERFORM ENTER-VENDOR-PHONE.
015600
015700*---------------------------------
015800* CHANGE
015900*---------------------------------
016000 CHANGE-MODE.
016100     MOVE "CHANGE" TO THE-MODE.
016200     PERFORM GET-EXISTING-RECORD.
016300     PERFORM CHANGE-RECORDS
016400        UNTIL VENDOR-NUMBER = ZEROES.
016500
016600 CHANGE-RECORDS.
016700     PERFORM GET-FIELD-TO-CHANGE.
016800     PERFORM CHANGE-ONE-FIELD
016900         UNTIL WHICH-FIELD = ZERO.
017000     PERFORM GET-EXISTING-RECORD.
017100
017200 GET-FIELD-TO-CHANGE.
017300     PERFORM DISPLAY-ALL-FIELDS.
017400     PERFORM ASK-WHICH-FIELD.
017500
017600 ASK-WHICH-FIELD.
017700     PERFORM ACCEPT-WHICH-FIELD.
017800     PERFORM RE-ACCEPT-WHICH-FIELD
017900         UNTIL WHICH-FIELD < 9.
018000
018100 ACCEPT-WHICH-FIELD.
018200     DISPLAY "ENTER THE NUMBER OF THE FIELD".
018300     DISPLAY "TO CHANGE (1-8) OR 0 TO EXIT".
018400     ACCEPT WHICH-FIELD.
018500
018600 RE-ACCEPT-WHICH-FIELD.
018700     DISPLAY "INVALID ENTRY".
018800     PERFORM ACCEPT-WHICH-FIELD.
018900
019000 CHANGE-ONE-FIELD.
019100     PERFORM CHANGE-THIS-FIELD.
019200     PERFORM GET-FIELD-TO-CHANGE.
019300
019400 CHANGE-THIS-FIELD.
019500     IF WHICH-FIELD = 1
019600         PERFORM ENTER-VENDOR-NAME.
019700     IF WHICH-FIELD = 2
019800         PERFORM ENTER-VENDOR-ADDRESS-1.
019900     IF WHICH-FIELD = 3
020000         PERFORM ENTER-VENDOR-ADDRESS-2.
020100     IF WHICH-FIELD = 4
020200         PERFORM ENTER-VENDOR-CITY.
020300     IF WHICH-FIELD = 5
020400         PERFORM ENTER-VENDOR-STATE.
020500     IF WHICH-FIELD = 6
020600         PERFORM ENTER-VENDOR-ZIP.
020700     IF WHICH-FIELD = 7
020800         PERFORM ENTER-VENDOR-CONTACT.
020900     IF WHICH-FIELD = 8
021000         PERFORM ENTER-VENDOR-PHONE.
021100
021200     PERFORM REWRITE-VENDOR-RECORD.
021300
021400*---------------------------------
021500* INQUIRE
021600*---------------------------------
021700 INQUIRE-MODE.
021800     MOVE "DISPLAY" TO THE-MODE.
021900     PERFORM GET-EXISTING-RECORD.
022000     PERFORM INQUIRE-RECORDS
022100        UNTIL VENDOR-NUMBER = ZEROES.
022200
022300 INQUIRE-RECORDS.
022400     PERFORM DISPLAY-ALL-FIELDS.
022500     PERFORM GET-EXISTING-RECORD.
022600
022700*---------------------------------
022800* DELETE
022900*---------------------------------
023000 DELETE-MODE.
023100     MOVE "DELETE" TO THE-MODE.
023200     PERFORM GET-EXISTING-RECORD.
023300     PERFORM DELETE-RECORDS
023400        UNTIL VENDOR-NUMBER = ZEROES.
023500
023600 DELETE-RECORDS.
023700     PERFORM DISPLAY-ALL-FIELDS.
023800
023900     PERFORM ASK-OK-TO-DELETE.
024000
024100     IF OK-TO-DELETE = "Y"
024200         PERFORM DELETE-VENDOR-RECORD.
024300
024400     PERFORM GET-EXISTING-RECORD.
024500
024600 ASK-OK-TO-DELETE.
024700     PERFORM ACCEPT-OK-TO-DELETE.
024800
024900     PERFORM RE-ACCEPT-OK-TO-DELETE
025000        UNTIL OK-TO-DELETE = "Y" OR "N".
025100
025200 ACCEPT-OK-TO-DELETE.
025300     DISPLAY "DELETE THIS RECORD (Y/N)?".
025400     ACCEPT OK-TO-DELETE.
025500     INSPECT OK-TO-DELETE
025600      CONVERTING LOWER-ALPHA TO UPPER-ALPHA.
025700
025800 RE-ACCEPT-OK-TO-DELETE.
025900     DISPLAY "YOU MUST ENTER YES OR NO".
026000     PERFORM ACCEPT-OK-TO-DELETE.
026100
026200*---------------------------------
026300* Routines shared by all modes
026400*---------------------------------
026500 INIT-VENDOR-RECORD.
026600     MOVE SPACE TO VENDOR-RECORD.
026700     MOVE ZEROES TO VENDOR-NUMBER.
026800
026900 ENTER-VENDOR-NUMBER.
027000     DISPLAY " ".
027100     DISPLAY "ENTER VENDOR NUMBER OF THE VENDOR" .
027200     DISPLAY "TO " THE-MODE " (1-99999)".
027300     DISPLAY "ENTER 0 TO STOP ENTRY".
027400     ACCEPT VENDOR-NUMBER-FIELD.
027500*OR  ACCEPT VENDOR-NUMBER-FIELD WITH CONVERSION.
027600
027700     MOVE VENDOR-NUMBER-FIELD TO VENDOR-NUMBER.
027800
027900*---------------------------------
028000* Routines shared Add and Change
028100*---------------------------------
028200 ENTER-VENDOR-NAME.
028300     PERFORM ACCEPT-VENDOR-NAME.
028400     PERFORM RE-ACCEPT-VENDOR-NAME
028500         UNTIL VENDOR-NAME NOT = SPACE.
028600
028700 ACCEPT-VENDOR-NAME.
028800     DISPLAY "ENTER VENDOR NAME".
028900     ACCEPT VENDOR-NAME.
029000     INSPECT VENDOR-NAME
029100         CONVERTING LOWER-ALPHA
029200         TO         UPPER-ALPHA.
029300
029400 RE-ACCEPT-VENDOR-NAME.
029500     DISPLAY "VENDOR NAME MUST BE ENTERED".
029600     PERFORM ACCEPT-VENDOR-NAME.
029700
029800 ENTER-VENDOR-ADDRESS-1.
029900     PERFORM ACCEPT-VENDOR-ADDRESS-1.
030000     PERFORM RE-ACCEPT-VENDOR-ADDRESS-1
030100         UNTIL VENDOR-ADDRESS-1 NOT = SPACE.
030200
030300 ACCEPT-VENDOR-ADDRESS-1.
030400     DISPLAY "ENTER VENDOR ADDRESS-1".
030500     ACCEPT VENDOR-ADDRESS-1.
030600     INSPECT VENDOR-ADDRESS-1
030700         CONVERTING LOWER-ALPHA
030800         TO         UPPER-ALPHA.
030900
031000 RE-ACCEPT-VENDOR-ADDRESS-1.
031100     DISPLAY "VENDOR ADDRESS-1 MUST BE ENTERED".
031200     PERFORM ACCEPT-VENDOR-ADDRESS-1.
031300
031400 ENTER-VENDOR-ADDRESS-2.
031500     DISPLAY "ENTER VENDOR ADDRESS-2".
031600     ACCEPT VENDOR-ADDRESS-2.
031700     INSPECT VENDOR-ADDRESS-2
031800         CONVERTING LOWER-ALPHA
031900         TO         UPPER-ALPHA.
032000
032100 ENTER-VENDOR-CITY.
032200     PERFORM ACCEPT-VENDOR-CITY.
032300     PERFORM RE-ACCEPT-VENDOR-CITY
032400         UNTIL VENDOR-CITY NOT = SPACE.
032500
032600 ACCEPT-VENDOR-CITY.
032700     DISPLAY "ENTER VENDOR CITY".
032800     ACCEPT VENDOR-CITY.
032900     INSPECT VENDOR-CITY
033000         CONVERTING LOWER-ALPHA
033100         TO         UPPER-ALPHA.
033200
033300 RE-ACCEPT-VENDOR-CITY.
033400     DISPLAY "VENDOR CITY MUST BE ENTERED".
033500     PERFORM ACCEPT-VENDOR-CITY.
033600
033700 ENTER-VENDOR-STATE.
033800     PERFORM ACCEPT-VENDOR-STATE.
033900     PERFORM RE-ACCEPT-VENDOR-STATE
034000         UNTIL VENDOR-STATE NOT = SPACES AND
034100               STATE-RECORD-FOUND = "Y".
034200
034300 ACCEPT-VENDOR-STATE.
034400     DISPLAY "ENTER VENDOR STATE".
034500     ACCEPT VENDOR-STATE.
034600     PERFORM EDIT-CHECK-VENDOR-STATE.
034700
034800 RE-ACCEPT-VENDOR-STATE.
034900     DISPLAY ERROR-MESSAGE.
035000     PERFORM ACCEPT-VENDOR-STATE.
035100
035200 EDIT-CHECK-VENDOR-STATE.
035300     PERFORM EDIT-VENDOR-STATE.
035400     PERFORM CHECK-VENDOR-STATE.
035500
035600 EDIT-VENDOR-STATE.
035700     INSPECT VENDOR-STATE
035800         CONVERTING LOWER-ALPHA
035900         TO         UPPER-ALPHA.
036000
036100 CHECK-VENDOR-STATE.
036200     PERFORM VENDOR-STATE-REQUIRED.
036300     IF VENDOR-STATE NOT = SPACES
036400         PERFORM VENDOR-STATE-ON-FILE.
036500
036600 VENDOR-STATE-REQUIRED.
036700     IF VENDOR-STATE = SPACE
036800         MOVE "VENDOR STATE MUST BE ENTERED"
036900           TO ERROR-MESSAGE.
037000
037100 VENDOR-STATE-ON-FILE.
037200     MOVE VENDOR-STATE TO STATE-CODE.
037300     PERFORM READ-STATE-RECORD.
037400     IF STATE-RECORD-FOUND = "N"
037500         MOVE "STATE CODE NOT FOUND IN CODES FILE"
037600           TO ERROR-MESSAGE.
037700
037800 ENTER-VENDOR-ZIP.
037900     PERFORM ACCEPT-VENDOR-ZIP.
038000     PERFORM RE-ACCEPT-VENDOR-ZIP
038100         UNTIL VENDOR-ZIP NOT = SPACE.
038200
038300 ACCEPT-VENDOR-ZIP.
038400     DISPLAY "ENTER VENDOR ZIP".
038500     ACCEPT VENDOR-ZIP.
038600     INSPECT VENDOR-ZIP
038700         CONVERTING LOWER-ALPHA
038800         TO         UPPER-ALPHA.
038900
039000 RE-ACCEPT-VENDOR-ZIP.
039100     DISPLAY "VENDOR ZIP MUST BE ENTERED".
039200     PERFORM ACCEPT-VENDOR-ZIP.
039300
039400 ENTER-VENDOR-CONTACT.
039500     DISPLAY "ENTER VENDOR CONTACT".
039600     ACCEPT VENDOR-CONTACT.
039700     INSPECT VENDOR-CONTACT
039800         CONVERTING LOWER-ALPHA
039900         TO         UPPER-ALPHA.
040000
040100 ENTER-VENDOR-PHONE.
040200     PERFORM ACCEPT-VENDOR-PHONE.
040300     PERFORM RE-ACCEPT-VENDOR-PHONE
040400         UNTIL VENDOR-PHONE NOT = SPACE.
040500
040600 ACCEPT-VENDOR-PHONE.
040700     DISPLAY "ENTER VENDOR PHONE".
040800     ACCEPT VENDOR-PHONE.
040900     INSPECT VENDOR-PHONE
041000         CONVERTING LOWER-ALPHA
041100         TO         UPPER-ALPHA.
041200
041300 RE-ACCEPT-VENDOR-PHONE.
041400     DISPLAY "VENDOR PHONE MUST BE ENTERED".
041500     PERFORM ACCEPT-VENDOR-PHONE.
041600
041700*---------------------------------
041800* Routines shared by Change,
041900* Inquire and Delete
042000*---------------------------------
042100 GET-EXISTING-RECORD.
042200     PERFORM ACCEPT-EXISTING-KEY.
042300     PERFORM RE-ACCEPT-EXISTING-KEY
042400         UNTIL VENDOR-RECORD-FOUND = "Y" OR
042500               VENDOR-NUMBER = ZEROES.
042600
042700 ACCEPT-EXISTING-KEY.
042800     PERFORM INIT-VENDOR-RECORD.
042900     PERFORM ENTER-VENDOR-NUMBER.
043000     IF VENDOR-NUMBER NOT = ZEROES
043100         PERFORM READ-VENDOR-RECORD.
043200
043300 RE-ACCEPT-EXISTING-KEY.
043400     DISPLAY "RECORD NOT FOUND"
043500     PERFORM ACCEPT-EXISTING-KEY.
043600
043700 DISPLAY-ALL-FIELDS.
043800     DISPLAY " ".
043900     PERFORM DISPLAY-VENDOR-NUMBER.
044000     PERFORM DISPLAY-VENDOR-NAME.
044100     PERFORM DISPLAY-VENDOR-ADDRESS-1.
044200     PERFORM DISPLAY-VENDOR-ADDRESS-2.
044300     PERFORM DISPLAY-VENDOR-CITY.
044400     PERFORM DISPLAY-VENDOR-STATE.
044500     PERFORM DISPLAY-VENDOR-ZIP.
044600     PERFORM DISPLAY-VENDOR-CONTACT.
044700     PERFORM DISPLAY-VENDOR-PHONE.
044800     DISPLAY " ".
044900
045000 DISPLAY-VENDOR-NUMBER.
045100     DISPLAY "   VENDOR NUMBER: " VENDOR-NUMBER.
045200
045300 DISPLAY-VENDOR-NAME.
045400     DISPLAY "1. VENDOR NAME: " VENDOR-NAME.
045500
045600 DISPLAY-VENDOR-ADDRESS-1.
045700     DISPLAY "2. VENDOR ADDRESS-1: " VENDOR-ADDRESS-1.
045800
045900 DISPLAY-VENDOR-ADDRESS-2.
046000     DISPLAY "3. VENDOR ADDRESS-2: " VENDOR-ADDRESS-2.
046100
046200 DISPLAY-VENDOR-CITY.
046300     DISPLAY "4. VENDOR CITY: " VENDOR-CITY.
046400
046500 DISPLAY-VENDOR-STATE.
046600     PERFORM VENDOR-STATE-ON-FILE.
046700     IF STATE-RECORD-FOUND = "N"
046800         MOVE "**Not found**" TO STATE-NAME.
046900     DISPLAY "5. VENDOR STATE: "
047000             VENDOR-STATE " "
047100             STATE-NAME.
047200
047300 DISPLAY-VENDOR-ZIP.
047400     DISPLAY "6. VENDOR ZIP: " VENDOR-ZIP.
047500
047600 DISPLAY-VENDOR-CONTACT.
047700     DISPLAY "7. VENDOR CONTACT: " VENDOR-CONTACT.
047800
047900 DISPLAY-VENDOR-PHONE.
048000     DISPLAY "8. VENDOR PHONE: " VENDOR-PHONE.
048100
048200*---------------------------------
048300* File I-O Routines
048400*---------------------------------
048500 READ-VENDOR-RECORD.
048600     MOVE "Y" TO VENDOR-RECORD-FOUND.
048700     READ VENDOR-FILE RECORD
048800       INVALID KEY
048900          MOVE "N" TO VENDOR-RECORD-FOUND.
049000
049100*or  READ VENDOR-FILE RECORD WITH LOCK
049200*      INVALID KEY
049300*         MOVE "N" TO VENDOR-RECORD-FOUND.
049400
049500*or  READ VENDOR-FILE RECORD WITH HOLD
049600*      INVALID KEY
049700*         MOVE "N" TO VENDOR-RECORD-FOUND.
049800
049900 WRITE-VENDOR-RECORD.
050000     WRITE VENDOR-RECORD
050100         INVALID KEY
050200         DISPLAY "RECORD ALREADY ON FILE".
050300
050400 REWRITE-VENDOR-RECORD.
050500     REWRITE VENDOR-RECORD
050600         INVALID KEY
050700         DISPLAY "ERROR REWRITING VENDOR RECORD".
050800
050900 DELETE-VENDOR-RECORD.
051000     DELETE VENDOR-FILE RECORD
051100         INVALID KEY
051200         DISPLAY "ERROR DELETING VENDOR RECORD".
051300
051400 READ-STATE-RECORD.
051500     MOVE "Y" TO STATE-RECORD-FOUND.
051600     READ STATE-FILE RECORD
051700       INVALID KEY
051800          MOVE "N" TO STATE-RECORD-FOUND.
051900
