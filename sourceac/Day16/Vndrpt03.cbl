000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDRPT03.
000300*---------------------------------
000400* Report on the Vendor File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND01.CBL".
001100
001200     COPY "SLSTATE.CBL".
001300
001400     SELECT PRINTER-FILE
001500         ASSIGN TO PRINTER
001600         ORGANIZATION IS LINE SEQUENTIAL.
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100     COPY "FDVND02.CBL".
002200
002300     COPY "FDSTATE.CBL".
002400
002500 FD  PRINTER-FILE
002600     LABEL RECORDS ARE OMITTED.
002700 01  PRINTER-RECORD             PIC X(80).
002800
002900 WORKING-STORAGE SECTION.
003000
003100 01  DETAIL-LINE.
003200     05  PRINT-NUMBER      PIC 9(5).
003300     05  FILLER            PIC X     VALUE SPACE.
003400     05  PRINT-NAME        PIC X(30).
003500     05  FILLER            PIC X(15) VALUE SPACE.
003600     05  PRINT-CONTACT     PIC X(30).
003700
003800 01  CITY-STATE-LINE.
003900     05  FILLER            PIC X(6) VALUE SPACE.
004000     05  PRINT-CITY        PIC X(20).
004100     05  FILLER            PIC X VALUE SPACE.
004200     05  PRINT-STATE       PIC X(2).
004300     05  FILLER            PIC X VALUE SPACE.
004400     05  PRINT-STATE-NAME  PIC X(20).
004500     05  FILLER            PIC X(1) VALUE SPACE.
004600     05  PRINT-ZIP         PIC X(10).
004700
004800 01  COLUMN-LINE.
004900     05  FILLER         PIC X(2)  VALUE "NO".
005000     05  FILLER         PIC X(4) VALUE SPACE.
005100     05  FILLER         PIC X(12) VALUE "NAME-ADDRESS".
005200     05  FILLER         PIC X(33) VALUE SPACE.
005300     05  FILLER         PIC X(17) VALUE "CONTACT-PHONE-ZIP".
005400
005500 01  TITLE-LINE.
005600     05  FILLER              PIC X(25) VALUE SPACE.
005700     05  FILLER              PIC X(11)
005800         VALUE "VENDOR LIST".
005900     05  FILLER              PIC X(19) VALUE SPACE.
006000     05  FILLER              PIC X(5) VALUE "PAGE:".
006100     05  FILLER              PIC X(1) VALUE SPACE.
006200     05  PRINT-PAGE-NUMBER PIC ZZZZ9.
006300
006400 77  FILE-AT-END             PIC X.
006500 77  STATE-FILE-AT-END       PIC X VALUE "N".
006600 77  LINE-COUNT              PIC 999 VALUE ZERO.
006700 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
006800 77  MAXIMUM-LINES           PIC 999 VALUE 55.
006900
007000 01  TABLE-STATE-RECORD OCCURS 50 TIMES
007100      INDEXED BY STATE-INDEX.
007200     05  TABLE-STATE-CODE          PIC XX.
007300     05  TABLE-STATE-NAME          PIC X(20).
007400
007500 PROCEDURE DIVISION.
007600 PROGRAM-BEGIN.
007700
007800     PERFORM OPENING-PROCEDURE.
007900     MOVE ZEROES TO LINE-COUNT
008000                    PAGE-NUMBER.
008100
008200     PERFORM START-NEW-PAGE.
008300
008400     MOVE "N" TO FILE-AT-END.
008500     PERFORM READ-NEXT-RECORD.
008600     IF FILE-AT-END = "Y"
008700         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
008800         PERFORM WRITE-TO-PRINTER
008900     ELSE
009000         PERFORM PRINT-VENDOR-FIELDS
009100             UNTIL FILE-AT-END = "Y".
009200
009300     PERFORM CLOSING-PROCEDURE.
009400
009500 PROGRAM-DONE.
009600     ACCEPT OMITTED. STOP RUN.
009700
009800 OPENING-PROCEDURE.
009900     OPEN I-O VENDOR-FILE.
010000
010100     OPEN I-O STATE-FILE.
010200     PERFORM LOAD-STATE-TABLE.
010300     CLOSE STATE-FILE.
010400
010500     OPEN OUTPUT PRINTER-FILE.
010600
010700 LOAD-STATE-TABLE.
010800     PERFORM CLEAR-TABLE.
010900     SET STATE-INDEX TO 1.
011000     PERFORM READ-NEXT-STATE-RECORD.
011100     PERFORM LOAD-ONE-STATE-RECORD
011200         UNTIL STATE-FILE-AT-END = "Y" OR
011300               STATE-INDEX > 50.
011400
011500 CLEAR-TABLE.
011600     PERFORM CLEAR-ONE-TABLE-ROW
011700         VARYING STATE-INDEX FROM 1 BY 1
011800          UNTIL STATE-INDEX > 50.
011900
012000 CLEAR-ONE-TABLE-ROW.
012100     MOVE SPACE TO TABLE-STATE-RECORD(STATE-INDEX).
012200
012300 LOAD-ONE-STATE-RECORD.
012400     MOVE STATE-CODE TO TABLE-STATE-CODE(STATE-INDEX).
012500     MOVE STATE-NAME TO TABLE-STATE-NAME(STATE-INDEX).
012600
012700     PERFORM READ-NEXT-STATE-RECORD.
012800
012900     IF STATE-FILE-AT-END NOT = "Y"
013000         SET STATE-INDEX UP BY 1
013100         IF STATE-INDEX > 50
013200             DISPLAY "TABLE FULL".
013300
013400 CLOSING-PROCEDURE.
013500     CLOSE VENDOR-FILE.
013600     PERFORM END-LAST-PAGE.
013700     CLOSE PRINTER-FILE.
013800
013900 PRINT-VENDOR-FIELDS.
014000     IF LINE-COUNT > MAXIMUM-LINES
014100         PERFORM START-NEXT-PAGE.
014200     PERFORM PRINT-THE-RECORD.
014300     PERFORM READ-NEXT-RECORD.
014400
014500 PRINT-THE-RECORD.
014600     PERFORM PRINT-LINE-1.
014700     PERFORM PRINT-LINE-2.
014800     PERFORM PRINT-LINE-3.
014900     PERFORM PRINT-LINE-4.
015000     PERFORM LINE-FEED.
015100
015200 PRINT-LINE-1.
015300     MOVE SPACE TO DETAIL-LINE.
015400     MOVE VENDOR-NUMBER TO PRINT-NUMBER.
015500     MOVE VENDOR-NAME TO PRINT-NAME.
015600     MOVE VENDOR-CONTACT TO PRINT-CONTACT.
015700     MOVE DETAIL-LINE TO PRINTER-RECORD.
015800     PERFORM WRITE-TO-PRINTER.
015900
016000 PRINT-LINE-2.
016100     MOVE SPACE TO DETAIL-LINE.
016200     MOVE VENDOR-ADDRESS-1 TO PRINT-NAME.
016300     MOVE VENDOR-PHONE TO PRINT-CONTACT.
016400     MOVE DETAIL-LINE TO PRINTER-RECORD.
016500     PERFORM WRITE-TO-PRINTER.
016600
016700 PRINT-LINE-3.
016800     MOVE SPACE TO DETAIL-LINE.
016900     MOVE VENDOR-ADDRESS-2 TO PRINT-NAME.
017000     IF VENDOR-ADDRESS-2 NOT = SPACE
017100         MOVE DETAIL-LINE TO PRINTER-RECORD
017200         PERFORM WRITE-TO-PRINTER.
017300
017400 PRINT-LINE-4.
017500     MOVE SPACE TO CITY-STATE-LINE.
017600     MOVE VENDOR-CITY TO PRINT-CITY.
017700     MOVE VENDOR-STATE TO PRINT-STATE.
017800
017900     PERFORM LOOK-UP-STATE-CODE.
018000
018100     MOVE VENDOR-ZIP TO PRINT-ZIP.
018200     MOVE CITY-STATE-LINE TO PRINTER-RECORD.
018300     PERFORM WRITE-TO-PRINTER.
018400
018500 LOOK-UP-STATE-CODE.
018600     SET STATE-INDEX TO 1.
018700     SEARCH TABLE-STATE-RECORD
018800         AT END
018900          MOVE "***Not Found***" TO PRINT-STATE-NAME
019000         WHEN VENDOR-STATE = TABLE-STATE-CODE(STATE-INDEX)
019100          MOVE TABLE-STATE-NAME(STATE-INDEX)
019200            TO PRINT-STATE-NAME.
019300
019400     IF STATE-NAME = SPACE
019500          MOVE "*State is Blank*" TO PRINT-STATE-NAME.
019600
019700 READ-NEXT-RECORD.
019800     READ VENDOR-FILE NEXT RECORD
019900         AT END MOVE "Y" TO FILE-AT-END.
020000
020100 WRITE-TO-PRINTER.
020200     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
020300     ADD 1 TO LINE-COUNT.
020400
020500 LINE-FEED.
020600     MOVE SPACE TO PRINTER-RECORD.
020700     PERFORM WRITE-TO-PRINTER.
020800
020900 START-NEXT-PAGE.
021000     PERFORM END-LAST-PAGE.
021100     PERFORM START-NEW-PAGE.
021200
021300 START-NEW-PAGE.
021400     ADD 1 TO PAGE-NUMBER.
021500     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
021600     MOVE TITLE-LINE TO PRINTER-RECORD.
021700     PERFORM WRITE-TO-PRINTER.
021800     PERFORM LINE-FEED.
021900     MOVE COLUMN-LINE TO PRINTER-RECORD.
022000     PERFORM WRITE-TO-PRINTER.
022100     PERFORM LINE-FEED.
022200
022300 END-LAST-PAGE.
022400     PERFORM FORM-FEED.
022500     MOVE ZERO TO LINE-COUNT.
022600
022700 FORM-FEED.
022800     MOVE SPACE TO PRINTER-RECORD.
022900     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
023000
023100 READ-NEXT-STATE-RECORD.
023200     MOVE "N" TO STATE-FILE-AT-END.
023300     READ STATE-FILE NEXT RECORD
023400         AT END
023500         MOVE "Y" TO STATE-FILE-AT-END.
023600
