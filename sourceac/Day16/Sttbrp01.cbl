000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. STTBRP01.
000300*---------------------------------
000400* Report on the STATE File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLSTATE.CBL".
001100
001200     SELECT PRINTER-FILE
001300         ASSIGN TO PRINTER
001400         ORGANIZATION IS LINE SEQUENTIAL.
001500
001600 DATA DIVISION.
001700 FILE SECTION.
001800
001900     COPY "FDSTATE.CBL".
002000
002100 FD  PRINTER-FILE
002200     LABEL RECORDS ARE OMITTED.
002300 01  PRINTER-RECORD             PIC X(80).
002400
002500 WORKING-STORAGE SECTION.
002600
002700 01  DETAIL-LINE.
002800     05  PRINT-CODE        PIC XX.
002900     05  FILLER            PIC XXXX     VALUE SPACE.
003000     05  PRINT-NAME        PIC X(20).
003100
003200 01  COLUMN-LINE.
003300     05  FILLER         PIC X(4)  VALUE "CODE".
003400     05  FILLER         PIC X(2) VALUE SPACE.
003500     05  FILLER         PIC X(4) VALUE "NAME".
003600
003700 01  TITLE-LINE.
003800     05  FILLER              PIC X(25) VALUE SPACE.
003900     05  FILLER              PIC X(11)
004000         VALUE "STATE CODES".
004100     05  FILLER              PIC X(15) VALUE SPACE.
004200     05  FILLER              PIC X(5) VALUE "PAGE:".
004300     05  FILLER              PIC X(1) VALUE SPACE.
004400     05  PRINT-PAGE-NUMBER   PIC ZZZZ9.
004500
004600 77  FILE-AT-END             PIC X.
004700 77  LINE-COUNT              PIC 999 VALUE ZERO.
004800 77  PAGE-NUMBER             PIC 99999 VALUE ZERO.
004900 77  MAXIMUM-LINES           PIC 999 VALUE 55.
005000
005100 01  TABLE-STATE-RECORD OCCURS 50 TIMES
005200      INDEXED BY STATE-INDEX.
005300     05  TABLE-STATE-CODE          PIC XX.
005400     05  TABLE-STATE-NAME          PIC X(20).
005500
005600 PROCEDURE DIVISION.
005700 PROGRAM-BEGIN.
005800
005900     PERFORM OPENING-PROCEDURE.
006000     MOVE ZEROES TO LINE-COUNT
006100                    PAGE-NUMBER.
006200
006300     PERFORM START-NEW-PAGE.
006400
006500     SET STATE-INDEX TO 1.
006600     PERFORM PRINT-STATE-FIELDS
006700             UNTIL STATE-INDEX > 50 OR
006800                TABLE-STATE-RECORD(STATE-INDEX) = SPACE.
006900
007000     PERFORM CLOSING-PROCEDURE.
007100
007200 PROGRAM-DONE.
007300     ACCEPT OMITTED. STOP RUN.
007400
007500 OPENING-PROCEDURE.
007600
007700     OPEN I-O STATE-FILE.
007800     PERFORM LOAD-STATE-TABLE.
007900     CLOSE STATE-FILE.
008000
008100     OPEN OUTPUT PRINTER-FILE.
008200
008300 LOAD-STATE-TABLE.
008400     PERFORM CLEAR-TABLE.
008500     SET STATE-INDEX TO 1.
008600     PERFORM READ-NEXT-RECORD.
008700     PERFORM LOAD-ONE-STATE-RECORD
008800         UNTIL FILE-AT-END = "Y" OR
008900               STATE-INDEX > 50.
009000
009100 CLEAR-TABLE.
009200     PERFORM CLEAR-ONE-TABLE-ROW
009300         VARYING STATE-INDEX FROM 1 BY 1
009400          UNTIL STATE-INDEX > 50.
009500
009600 CLEAR-ONE-TABLE-ROW.
009700     MOVE SPACE TO TABLE-STATE-RECORD(STATE-INDEX).
009800
009900 LOAD-ONE-STATE-RECORD.
010000     MOVE STATE-CODE TO TABLE-STATE-CODE(STATE-INDEX).
010100     MOVE STATE-NAME TO TABLE-STATE-NAME(STATE-INDEX).
010200
010300     PERFORM READ-NEXT-RECORD.
010400
010500     IF FILE-AT-END NOT = "Y"
010600         SET STATE-INDEX UP BY 1
010700         IF STATE-INDEX > 50
010800             DISPLAY "TABLE FULL".
010900
011000 CLOSING-PROCEDURE.
011100     PERFORM END-LAST-PAGE.
011200     CLOSE PRINTER-FILE.
011300
011400 PRINT-STATE-FIELDS.
011500     IF LINE-COUNT > MAXIMUM-LINES
011600         PERFORM START-NEXT-PAGE.
011700     PERFORM PRINT-THE-RECORD.
011800     SET STATE-INDEX UP BY 1.
011900
012000 PRINT-THE-RECORD.
012100     MOVE SPACE TO DETAIL-LINE.
012200     MOVE TABLE-STATE-CODE(STATE-INDEX) TO PRINT-CODE.
012300     MOVE TABLE-STATE-NAME(STATE-INDEX) TO PRINT-NAME.
012400     MOVE DETAIL-LINE TO PRINTER-RECORD.
012500     PERFORM WRITE-TO-PRINTER.
012600
012700 READ-NEXT-RECORD.
012800     READ STATE-FILE NEXT RECORD
012900         AT END MOVE "Y" TO FILE-AT-END.
013000
013100 WRITE-TO-PRINTER.
013200     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
013300     ADD 1 TO LINE-COUNT.
013400
013500 LINE-FEED.
013600     MOVE SPACE TO PRINTER-RECORD.
013700     PERFORM WRITE-TO-PRINTER.
013800
013900 START-NEXT-PAGE.
014000
014100     PERFORM END-LAST-PAGE.
014200     PERFORM START-NEW-PAGE.
014300
014400 START-NEW-PAGE.
014500     ADD 1 TO PAGE-NUMBER.
014600     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
014700     MOVE TITLE-LINE TO PRINTER-RECORD.
014800     PERFORM WRITE-TO-PRINTER.
014900     PERFORM LINE-FEED.
015000     MOVE COLUMN-LINE TO PRINTER-RECORD.
015100     PERFORM WRITE-TO-PRINTER.
015200     PERFORM LINE-FEED.
015300
015400 END-LAST-PAGE.
015500     PERFORM FORM-FEED.
015600     MOVE ZERO TO LINE-COUNT.
015700
015800 FORM-FEED.
015900     MOVE SPACE TO PRINTER-RECORD.
016000     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
016100
