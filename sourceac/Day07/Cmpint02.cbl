000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CMPINT02.
000300*------------------------------------------------
000400* Calculates compound interest
000500*------------------------------------------------
000600 ENVIRONMENT DIVISION.
000700 DATA DIVISION.
000800 WORKING-STORAGE SECTION.
000900
001000 01  YES-NO                           PIC X.
001100 01  ENTRY-OK                         PIC X.
001200 01  THE-INTEREST                     PIC 99V9.
001300 01  INTEREST-AS-DECIMAL              PIC V999.
001400 01  THE-PRINCIPAL                    PIC 9(9)V99.
001500 01  THE-NEW-VALUE                    PIC 9(9)V99.
001600 01  EARNED-INTEREST                  PIC 9(9)V99.
001700 01  THE-PERIOD                       PIC 9999.
001800 01  NO-OF-PERIODS                    PIC 999.
001900
002000 01  ENTRY-FIELD                      PIC Z(9).ZZ.
002100 01  DISPLAY-VALUE                    PIC ZZZ,ZZZ,ZZ9.99.
002200
002300 PROCEDURE DIVISION.
002400 PROGRAM-BEGIN.
002500
002600     MOVE "Y" TO YES-NO.
002700     PERFORM GET-AND-DISPLAY-RESULT
002800         UNTIL YES-NO = "N".
002900
003000 PROGRAM-DONE.
003100     ACCEPT OMITTED. STOP RUN.
003200
003300 GET-AND-DISPLAY-RESULT.
003400     PERFORM GET-THE-PRINCIPAL.
003500     PERFORM GET-THE-INTEREST.
003600     PERFORM GET-THE-PERIODS.
003700     PERFORM CALCULATE-THE-RESULT.
003800     PERFORM DISPLAY-THE-RESULT.
003900     PERFORM GO-AGAIN.
004000
004100 GET-THE-PRINCIPAL.
004200     MOVE "N" TO ENTRY-OK.
004300     PERFORM ENTER-THE-PRINCIPAL
004400         UNTIL ENTRY-OK = "Y".
004500
004600 ENTER-THE-PRINCIPAL.
004700     DISPLAY "Principal (.01 TO 999999.99)?".
004800     ACCEPT ENTRY-FIELD WITH CONVERSION.
004900     MOVE ENTRY-FIELD TO THE-PRINCIPAL.
005000     IF THE-PRINCIPAL < .01 OR
005100        THE-PRINCIPAL > 999999.99
005200         DISPLAY "INVALID ENTRY"
005300     ELSE
005400         MOVE "Y" TO ENTRY-OK.
005500
005600 GET-THE-INTEREST.
005700     MOVE "N" TO ENTRY-OK.
005800     PERFORM ENTER-THE-INTEREST
005900         UNTIL ENTRY-OK = "Y".
006000
006100 ENTER-THE-INTEREST.
006200     DISPLAY "Interest (.1% TO 99.9%)?".
006300     ACCEPT ENTRY-FIELD WITH CONVERSION.
006400     MOVE ENTRY-FIELD TO THE-INTEREST.
006500     IF THE-INTEREST < .1 OR
006600        THE-INTEREST > 99.9
006700         DISPLAY "INVALID ENTRY"
006800     ELSE
006900         MOVE "Y" TO ENTRY-OK
007000         COMPUTE INTEREST-AS-DECIMAL =
007100                 THE-INTEREST / 100.
007200
007300 GET-THE-PERIODS.
007400     MOVE "N" TO ENTRY-OK.
007500     PERFORM ENTER-THE-PERIODS
007600         UNTIL ENTRY-OK = "Y".
007700
007800 ENTER-THE-PERIODS.
007900     DISPLAY "Number of periods (1 TO 999)?".
008000     ACCEPT ENTRY-FIELD WITH CONVERSION.
008100     MOVE ENTRY-FIELD TO NO-OF-PERIODS.
008200     IF NO-OF-PERIODS < 1 OR
008300        NO-OF-PERIODS > 999
008400         DISPLAY "INVALID ENTRY"
008500     ELSE
008600         MOVE "Y" TO ENTRY-OK.
008700
008800 CALCULATE-THE-RESULT.
008900     PERFORM CALCULATE-ONE-PERIOD
009000         VARYING THE-PERIOD FROM 1 BY 1
009100          UNTIL THE-PERIOD > NO-OF-PERIODS.
009200
009300 CALCULATE-ONE-PERIOD.
009400     COMPUTE EARNED-INTEREST ROUNDED =
009500         THE-PRINCIPAL * INTEREST-AS-DECIMAL.
009600     COMPUTE THE-NEW-VALUE =
009700             THE-PRINCIPAL + EARNED-INTEREST.
009800     MOVE THE-NEW-VALUE TO THE-PRINCIPAL.
009900
010000 GO-AGAIN.
010100     DISPLAY "GO AGAIN?".
010200     ACCEPT YES-NO.
010300     IF YES-NO = "y"
010400         MOVE "Y" TO YES-NO.
010500     IF YES-NO NOT = "Y"
010600         MOVE "N" TO YES-NO.
010700
010800 DISPLAY-THE-RESULT.
010900     MOVE THE-NEW-VALUE TO DISPLAY-VALUE.
011000     DISPLAY "RESULTING VALUE IS " DISPLAY-VALUE.
011100
