000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. BILRPT02.
000300*---------------------------------
000400* Bills report in Due Date order
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVOUCH.CBL".
001100
001200     COPY "SLVND02.CBL".
001300
001400     COPY "SLSTATE.CBL".
001500
001600     SELECT WORK-FILE
001700         ASSIGN TO "WORK"
001800         ORGANIZATION IS SEQUENTIAL.
001900
002000     SELECT SORT-FILE
002100         ASSIGN TO "SORT".
002200
002300     SELECT PRINTER-FILE
002400         ASSIGN TO PRINTER
002500         ORGANIZATION IS LINE SEQUENTIAL.
002600
002700 DATA DIVISION.
002800 FILE SECTION.
002900
003000     COPY "FDVOUCH.CBL".
003100
003200     COPY "FDVND04.CBL".
003300
003400     COPY "FDSTATE.CBL".
003500
003600 FD  WORK-FILE
003700     LABEL RECORDS ARE STANDARD.
003800 01  WORK-RECORD.
003900     05  WORK-NUMBER           PIC 9(5).
004000     05  WORK-VENDOR           PIC 9(5).
004100     05  WORK-INVOICE          PIC X(15).
004200     05  WORK-FOR              PIC X(30).
004300     05  WORK-AMOUNT           PIC S9(6)V99.
004400     05  WORK-DATE             PIC 9(8).
004500     05  WORK-DUE              PIC 9(8).
004600     05  WORK-DEDUCTIBLE       PIC X.
004700     05  WORK-SELECTED         PIC X.
004800     05  WORK-PAID-AMOUNT      PIC S9(6)V99.
004900     05  WORK-PAID-DATE        PIC 9(8).
005000     05  WORK-CHECK-NO         PIC 9(6).
005100
005200 SD  SORT-FILE.
005300
005400 01  SORT-RECORD.
005500     05  SORT-NUMBER           PIC 9(5).
005600     05  SORT-VENDOR           PIC 9(5).
005700     05  SORT-INVOICE          PIC X(15).
005800     05  SORT-FOR              PIC X(30).
005900     05  SORT-AMOUNT           PIC S9(6)V99.
006000     05  SORT-DATE             PIC 9(8).
006100     05  SORT-DUE              PIC 9(8).
006200     05  SORT-DEDUCTIBLE       PIC X.
006300     05  SORT-SELECTED         PIC X.
006400     05  SORT-PAID-AMOUNT      PIC S9(6)V99.
006500     05  SORT-PAID-DATE        PIC 9(8).
006600     05  SORT-CHECK-NO         PIC 9(6).
006700
006800 FD  PRINTER-FILE
006900     LABEL RECORDS ARE OMITTED.
007000 01  PRINTER-RECORD             PIC X(80).
007100
007200 WORKING-STORAGE SECTION.
007300
007400 77  OK-TO-PROCESS         PIC X.
007500
007600     COPY "WSCASE01.CBL".
007700
007800 01  DETAIL-LINE.
007900     05  PRINT-NUMBER      PIC ZZZZ9.
008000     05  FILLER            PIC X(3) VALUE SPACE.
008100     05  PRINT-NAME        PIC X(30).
008200     05  FILLER            PIC X(1) VALUE SPACE.
008300     05  PRINT-DUE-DATE    PIC Z9/99/9999.
008400     05  FILLER            PIC X(1) VALUE SPACE.
008500     05  PRINT-AMOUNT      PIC ZZZ,ZZ9.99.
008600     05  FILLER            PIC X(1) VALUE SPACE.
008700     05  PRINT-INVOICE     PIC X(15).
008800     05  FILLER            PIC X(1) VALUE SPACE.
008900     05  PRINT-SELECTED    PIC X(1) VALUE SPACE.
009000
009100 01  TOTAL-THRU.
009200     05  FILLER            PIC X(20) VALUE SPACE.
009300     05  FILLER            PIC X(10) VALUE "TOTAL THRU".
009400
009500 01  COLUMN-LINE.
009600     05  FILLER         PIC X(7)  VALUE "VOUCHER".
009700     05  FILLER         PIC X(1)  VALUE SPACE.
009800     05  FILLER         PIC X(10) VALUE "VENDOR/For".
009900     05  FILLER         PIC X(23) VALUE SPACE.
010000     05  FILLER         PIC X(8)  VALUE "DUE DATE".
010100     05  FILLER         PIC X(1)  VALUE SPACE.
010200     05  FILLER         PIC X(10) VALUE "AMOUNT DUE".
010300     05  FILLER         PIC X(1)  VALUE SPACE.
010400     05  FILLER         PIC X(7)  VALUE "INVOICE".
010500     05  FILLER         PIC X(9)  VALUE SPACE.
010600     05  FILLER         PIC X(1)  VALUE "S".
010700
010800 01  TITLE-LINE.
010900     05  FILLER              PIC X(30) VALUE SPACE.
011000     05  FILLER              PIC X(12)
011100         VALUE "BILLS REPORT".
011200     05  FILLER              PIC X(19) VALUE SPACE.
011300     05  FILLER              PIC X(5) VALUE "PAGE:".
011400     05  FILLER              PIC X(1) VALUE SPACE.
011500     05  PRINT-PAGE-NUMBER   PIC ZZZ9.
011600
011700 77  WORK-FILE-AT-END     PIC X.
011800 77  VENDOR-RECORD-FOUND     PIC X.
011900
012000 77  LINE-COUNT              PIC 999 VALUE ZERO.
012100 77  PAGE-NUMBER             PIC 9999 VALUE ZERO.
012200 77  MAXIMUM-LINES           PIC 999 VALUE 55.
012300
012400 77  RECORD-COUNT            PIC 9999 VALUE ZEROES.
012500
012600     COPY "WSDATE01.CBL".
012700
012800 PROCEDURE DIVISION.
012900 PROGRAM-BEGIN.
013000
013100     PERFORM OPENING-PROCEDURE.
013200     PERFORM MAIN-PROCESS.
013300     PERFORM CLOSING-PROCEDURE.
013400
013500 PROGRAM-EXIT.
013600     EXIT PROGRAM.
013700
013800 PROGRAM-DONE.
013900     ACCEPT OMITTED. STOP RUN.
014000
014100 OPENING-PROCEDURE.
014200     OPEN I-O VENDOR-FILE.
014300
014400     OPEN OUTPUT PRINTER-FILE.
014500
014600 MAIN-PROCESS.
014700     PERFORM GET-OK-TO-PROCESS.
014800     IF OK-TO-PROCESS = "Y"
014900         PERFORM SORT-DATA-FILE
015000         PERFORM PRINT-THE-REPORT.
015100
015200 CLOSING-PROCEDURE.
015300     CLOSE VENDOR-FILE.
015400     PERFORM END-LAST-PAGE.
015500     CLOSE PRINTER-FILE.
015600
015700 GET-OK-TO-PROCESS.
015800     PERFORM ACCEPT-OK-TO-PROCESS.
015900     PERFORM RE-ACCEPT-OK-TO-PROCESS
016000         UNTIL OK-TO-PROCESS = "Y" OR "N".
016100
016200 ACCEPT-OK-TO-PROCESS.
016300     DISPLAY "PRINT BILLS REPORT (Y/N)?".
016400     ACCEPT OK-TO-PROCESS.
016500     INSPECT OK-TO-PROCESS
016600       CONVERTING LOWER-ALPHA
016700       TO         UPPER-ALPHA.
016800
016900 RE-ACCEPT-OK-TO-PROCESS.
017000     DISPLAY "YOU MUST ENTER YES OR NO".
017100     PERFORM ACCEPT-OK-TO-PROCESS.
017200
017300*---------------------------------
017400* Sorting logic
017500*---------------------------------
017600 SORT-DATA-FILE.
017700     SORT SORT-FILE
017800         ON ASCENDING KEY SORT-DUE
017900          USING VOUCHER-FILE
018000          GIVING WORK-FILE.
018100
018200 PRINT-THE-REPORT.
018300     OPEN INPUT WORK-FILE.
018400     PERFORM START-ONE-REPORT.
018500     PERFORM PROCESS-VOUCHERS.
018600     PERFORM END-ONE-REPORT.
018700     CLOSE WORK-FILE.
018800
018900 START-ONE-REPORT.
019000     PERFORM INITIALIZE-REPORT.
019100     PERFORM START-NEW-PAGE.
019200
019300 INITIALIZE-REPORT.
019400     MOVE ZEROES TO LINE-COUNT PAGE-NUMBER.
019500
019600 END-ONE-REPORT.
019700     IF RECORD-COUNT = ZEROES
019800         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
019900         PERFORM WRITE-TO-PRINTER.
020000
020100 PROCESS-VOUCHERS.
020200     PERFORM READ-FIRST-VALID-WORK.
020300     PERFORM PROCESS-ALL-VOUCHERS
020400         UNTIL WORK-FILE-AT-END = "Y".
020500
020600 PROCESS-ALL-VOUCHERS.
020700     PERFORM PROCESS-THIS-VOUCHER.
020800     PERFORM READ-NEXT-VALID-WORK.
020900
021000 PROCESS-THIS-VOUCHER.
021100     ADD 1 TO RECORD-COUNT.
021200     IF LINE-COUNT > MAXIMUM-LINES
021300         PERFORM START-NEXT-PAGE.
021400     PERFORM PRINT-THE-RECORD.
021500
021600 PRINT-THE-RECORD.
021700     PERFORM PRINT-LINE-1.
021800     PERFORM PRINT-LINE-2.
021900     PERFORM LINE-FEED.
022000
022100 PRINT-LINE-1.
022200     MOVE SPACE TO DETAIL-LINE.
022300     MOVE WORK-NUMBER TO PRINT-NUMBER.
022400
022500     MOVE WORK-VENDOR TO VENDOR-NUMBER.
022600     PERFORM READ-VENDOR-RECORD.
022700     IF VENDOR-RECORD-FOUND = "Y"
022800         MOVE VENDOR-NAME TO PRINT-NAME
022900     ELSE
023000         MOVE "*VENDOR NOT ON FILE*" TO PRINT-NAME.
023100
023200     MOVE WORK-DUE TO DATE-CCYYMMDD.
023300     PERFORM CONVERT-TO-MMDDCCYY.
023400     MOVE DATE-MMDDCCYY TO PRINT-DUE-DATE.
023500
023600     MOVE WORK-AMOUNT TO PRINT-AMOUNT.
023700     MOVE WORK-INVOICE TO PRINT-INVOICE.
023800
023900     IF WORK-SELECTED = "Y"
024000         MOVE WORK-SELECTED TO PRINT-SELECTED
024100     ELSE
024200         MOVE SPACE TO PRINT-SELECTED.
024300
024400     MOVE DETAIL-LINE TO PRINTER-RECORD.
024500     PERFORM WRITE-TO-PRINTER.
024600
024700 PRINT-LINE-2.
024800     MOVE SPACE TO DETAIL-LINE.
024900     MOVE WORK-FOR TO PRINT-NAME.
025000     MOVE DETAIL-LINE TO PRINTER-RECORD.
025100     PERFORM WRITE-TO-PRINTER.
025200
025300 WRITE-TO-PRINTER.
025400     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
025500     ADD 1 TO LINE-COUNT.
025600
025700 LINE-FEED.
025800     MOVE SPACE TO PRINTER-RECORD.
025900     PERFORM WRITE-TO-PRINTER.
026000
026100 START-NEXT-PAGE.
026200     PERFORM END-LAST-PAGE.
026300     PERFORM START-NEW-PAGE.
026400
026500 START-NEW-PAGE.
026600     ADD 1 TO PAGE-NUMBER.
026700     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
026800     MOVE TITLE-LINE TO PRINTER-RECORD.
026900     PERFORM WRITE-TO-PRINTER.
027000     PERFORM LINE-FEED.
027100     MOVE COLUMN-LINE TO PRINTER-RECORD.
027200     PERFORM WRITE-TO-PRINTER.
027300     PERFORM LINE-FEED.
027400
027500 END-LAST-PAGE.
027600     PERFORM FORM-FEED.
027700     MOVE ZERO TO LINE-COUNT.
027800
027900 FORM-FEED.
028000     MOVE SPACE TO PRINTER-RECORD.
028100     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
028200
028300*---------------------------------
028400* Read first, read next routines
028500*---------------------------------
028600 READ-FIRST-VALID-WORK.
028700     PERFORM READ-NEXT-VALID-WORK.
028800
028900 READ-NEXT-VALID-WORK.
029000     PERFORM READ-NEXT-WORK-RECORD.
029100     PERFORM READ-NEXT-WORK-RECORD
029200         UNTIL WORK-FILE-AT-END = "Y"
029300            OR WORK-PAID-DATE = ZEROES.
029400
029500 READ-NEXT-WORK-RECORD.
029600     MOVE "N" TO WORK-FILE-AT-END.
029700     READ WORK-FILE NEXT RECORD
029800         AT END MOVE "Y" TO WORK-FILE-AT-END.
029900
030000*---------------------------------
030100* Other File IO routines
030200*---------------------------------
030300 READ-VENDOR-RECORD.
030400     MOVE "Y" TO VENDOR-RECORD-FOUND.
030500     READ VENDOR-FILE RECORD
030600         INVALID KEY
030700         MOVE "N" TO VENDOR-RECORD-FOUND.
030800
030900*---------------------------------
031000* Utility Routines
031100*---------------------------------
031200     COPY "PLDATE01.CBL".
031300
