000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PHNBLD01.
000300*---------------------------------------------------
000400* This program creates a new data file and fills
000500* it with test data.
000600* The test records are written to the data file
000700* and echoed to the printer.
000800*--------------------------------------------------
000900 ENVIRONMENT DIVISION.
001000 INPUT-OUTPUT SECTION.
001100 FILE-CONTROL.
001200     SELECT PHONE-FILE
001300         ASSIGN TO "phone.dat"
001400         ORGANIZATION IS SEQUENTIAL.
001500
001600     SELECT PRINTER-FILE
001700         ASSIGN TO PRINTER
001800         ORGANIZATION IS LINE SEQUENTIAL.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200 FD  PHONE-FILE
002300     LABEL RECORDS ARE STANDARD.
002400 01  PHONE-RECORD.
002500     05  PHONE-LAST-NAME      PIC X(20).
002600     05  PHONE-FIRST-NAME     PIC X(20).
002700     05  PHONE-NUMBER         PIC X(15).
002800     05  PHONE-EXTENSION      PIC X(5).
002900
003000 FD  PRINTER-FILE
003100     LABEL RECORDS ARE OMITTED.
003200 01  PRINTER-RECORD           PIC X(80).
003300
003400 WORKING-STORAGE SECTION.
003500
003600 01  HOW-MANY                 PIC 999.
003700 01  ENTRY-FIELD              PIC ZZZ.
003800
003900 01  PRINT-LINES              PIC 99 VALUE ZEROES.
004000 01  FORMATTED-NUMBER.
004100     05  FILLER              PIC X(6) VALUE "(404) ".
004200     05  FILLER              PIC X(4) VALUE "555-".
004300     05  PHONE-COUNTER       PIC 9(4) VALUE ZERO.
004400
004500 PROCEDURE DIVISION.
004600 MAIN-LOGIC SECTION.
004700 PROGRAM-BEGIN.
004800
004900     PERFORM OPENING-PROCEDURE.
005000     PERFORM GET-HOW-MANY.
005100     MOVE ZEROES TO PRINT-LINES.
005200     PERFORM ADD-RECORDS
005300         VARYING PHONE-COUNTER
005400         FROM 1 BY 1 UNTIL
005500          PHONE-COUNTER > HOW-MANY.
005600     PERFORM CLOSING-PROCEDURE.
005700
005800 PROGRAM-DONE.
005900     ACCEPT OMITTED. STOP RUN.
006000
006100* OPENING AND CLOSING
006200
006300 OPENING-PROCEDURE.
006400     OPEN OUTPUT PHONE-FILE.
006500     OPEN OUTPUT PRINTER-FILE.
006600
006700 CLOSING-PROCEDURE.
006800     CLOSE PHONE-FILE.
006900     MOVE SPACE TO PRINTER-RECORD.
007000     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
007100     CLOSE PRINTER-FILE.
007200
007300 GET-HOW-MANY.
007400     DISPLAY "How many test entries (1-999)".
007500     ACCEPT ENTRY-FIELD.
007600*or  ACCEPT ENTRY-FIELD WITH CONVERSION.
007700     MOVE ENTRY-FIELD TO HOW-MANY.
007800
007900 ADD-RECORDS.
008000     PERFORM FORMAT-THE-RECORD.
008100     PERFORM ADD-THIS-RECORD.
008200
008300 FORMAT-THE-RECORD.
008400     MOVE "Joshua-------------X" TO PHONE-FIRST-NAME.
008500     MOVE "Johnson------------X" TO PHONE-LAST-NAME.
008600     MOVE "12345" TO PHONE-EXTENSION.
008700     MOVE FORMATTED-NUMBER TO PHONE-NUMBER.
008800
008900 ADD-THIS-RECORD.
009000     WRITE PHONE-RECORD.
009100     PERFORM PRINT-THIS-RECORD.
009200
009300 PRINT-THIS-RECORD.
009400     IF PRINT-LINES NOT < 55
009500         PERFORM NEW-PAGE.
009600     MOVE PHONE-RECORD TO PRINTER-RECORD.
009700     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
009800     ADD 1 TO PRINT-LINES.
009900
010000 NEW-PAGE.
010100     MOVE SPACE TO PRINTER-RECORD.
010200     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
010300     MOVE ZEROES TO PRINT-LINES.
010400
