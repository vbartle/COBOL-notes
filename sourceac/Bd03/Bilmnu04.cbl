000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. BILMNU04.
000300*---------------------------------
000400* Menu for the bill payment system.
000500* Including Vouchers
000600* Using full screen IO.
000700*---------------------------------
000800 ENVIRONMENT DIVISION.
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100
001200 DATA DIVISION.
001300 FILE SECTION.
001400
001500 WORKING-STORAGE SECTION.
001600
001700 77  MENU-PICK                    PIC 9 VALUE 0.
001800     88  MENU-PICK-IS-VALID       VALUES 0 THRU 3.
001900
002000 77  ERROR-MESSAGE      PIC X(79).
002100
002200 SCREEN SECTION.
002300 01  MENU-SCREEN.
002400     05  BLANK SCREEN.
002500     05  LINE  6 COL 20 VALUE "PLEASE SELECT:".
002600     05  LINE  8 COL 25 VALUE "1. STATE CODE MAINTENANCE".
002700     05  LINE  9 COL 25 VALUE "2. VENDOR MAINTENANCE".
002800     05  LINE 10 COL 25 VALUE "3. VOUCHER PROCESSING".
002900     05  LINE 12 COL 25 VALUE "0. EXIT".
003000     05  LINE 20 COL  1 VALUE "YOUR SELECTION".
003100     05  LINE 20 COL 16 PIC Z USING MENU-PICK.
003200     05  LINE 24 COL  1 PIC X(79) FROM ERROR-MESSAGE.
003300
003400 PROCEDURE DIVISION.
003500 PROGRAM-BEGIN.
003600     PERFORM OPENING-PROCEDURE.
003700     PERFORM MAIN-PROCESS.
003800     PERFORM CLOSING-PROCEDURE.
003900
004000 PROGRAM-EXIT.
004100     EXIT PROGRAM.
004200
004300 PROGRAM-DONE.
004400     ACCEPT OMITTED. STOP RUN.
004500
004600 OPENING-PROCEDURE.
004700
004800 CLOSING-PROCEDURE.
004900
005000 MAIN-PROCESS.
005100     PERFORM GET-MENU-PICK.
005200     PERFORM DO-THE-PICK
005300         UNTIL MENU-PICK = 0.
005400
005500*---------------------------------
005600* MENU
005700*---------------------------------
005800 GET-MENU-PICK.
005900     PERFORM DISPLAY-MENU-SCREEN.
006000     PERFORM ACCEPT-MENU-SCREEN.
006100     PERFORM RE-ACCEPT-MENU-SCREEN
006200         UNTIL MENU-PICK-IS-VALID.
006300
006400 DISPLAY-MENU-SCREEN.
006500
006600      DISPLAY MENU-SCREEN.
006700
006800 ACCEPT-MENU-SCREEN.
006900     ACCEPT MENU-SCREEN.
007000     MOVE SPACE TO ERROR-MESSAGE.
007100
007200 RE-ACCEPT-MENU-SCREEN.
007300     MOVE "INVALID SELECTION - PLEASE RE-TRY."
007400         TO ERROR-MESSAGE.
007500     PERFORM DISPLAY-MENU-SCREEN.
007600     PERFORM ACCEPT-MENU-SCREEN.
007700
007800 DO-THE-PICK.
007900     IF MENU-PICK = 1
008000         PERFORM STATE-MAINTENANCE
008100     ELSE
008200     IF MENU-PICK = 2
008300         PERFORM VENDOR-MAINTENANCE
008400     ELSE
008500     IF MENU-PICK = 3
008600         PERFORM VOUCHER-PROCESSING.
008700
008800     PERFORM GET-MENU-PICK.
008900
009000*---------------------------------
009100* STATE
009200*---------------------------------
009300 STATE-MAINTENANCE.
009400     CALL "STCMNT04".
009500
009600*---------------------------------
009700* VENDOR
009800*---------------------------------
009900 VENDOR-MAINTENANCE.
010000     CALL "VNDMNT05".
010100
010200*---------------------------------
010300* VOUCHERS
010400*---------------------------------
010500 VOUCHER-PROCESSING.
010600     CALL "VCHMNU01".
010700
