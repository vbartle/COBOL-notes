000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDMNT06.
000300*---------------------------------
000400* Add, Change, Inquire and Delete
000500* for the Vendor File.
000600* This includes Inquire by name
000700* The vendor report, and the vendor
000800* report in name order.
000900* Added Display all records.
001000* Uses full screen IO
001100*---------------------------------
001200 ENVIRONMENT DIVISION.
001300 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500
001600     COPY "SLVND02.CBL".
001700
001800     COPY "SLSTATE.CBL".
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200
002300     COPY "FDVND04.CBL".
002400
002500     COPY "FDSTATE.CBL".
002600
002700 WORKING-STORAGE SECTION.
002800
002900 77  MENU-PICK                    PIC 9 VALUE ZERO.
003000     88  MENU-PICK-IS-VALID       VALUES 0 THRU 8.
003100
003200 77  VENDOR-RECORD-FOUND          PIC X.
003300 77  STATE-RECORD-FOUND           PIC X.
003400
003500 77  SCREEN-ERROR                 PIC X.
003600 77  ERROR-MESSAGE                PIC X(79) VALUE SPACE.
003700
003800 77  CONTINUE-MESSAGE             PIC X(40) VALUE SPACE.
003900 77  OK-TO-CONTINUE               PIC X.
004000
004100 01  FOOTER-FIELD.
004200     05  FOOTER-1-FIELD           PIC X(40) VALUE SPACE.
004300     05  FOOTER-2-FIELD           PIC X(39) VALUE SPACE.
004400
004500     COPY "WSCASE01.CBL".
004600
004700 SCREEN SECTION.
004800 01  MENU-SCREEN.
004900     05  BLANK SCREEN.
005000     05  LINE  2 COLUMN 30 VALUE "VENDOR MAINTENANCE".
005100     05  LINE  6 COLUMN 20 VALUE "PLEASE SELECT:".
005200     05  LINE  8 COLUMN 25 VALUE "1. ADD RECORDS".
005300     05  LINE  9 COLUMN 25 VALUE "2. CHANGE A RECORD".
005400     05  LINE 10 COLUMN 25 VALUE "3. LOOK UP A RECORD".
005500     05  LINE 11 COLUMN 25 VALUE "4. DELETE A RECORD".
005600     05  LINE 12 COLUMN 25 VALUE "5. LOOK UP BY NAME".
005700     05  LINE 13 COLUMN 25 VALUE "6. PRINT RECORDS".
005800     05  LINE 14 COLUMN 25 VALUE "7. PRINT IN NAME ORDER".
005900     05  LINE 15 COLUMN 25 VALUE "8. DISPLAY ALL RECORDS".
006000     05  LINE 17 COLUMN 25 VALUE "0. EXIT".
006100     05  LINE 20 COLUMN  1 VALUE "YOUR SELECTION? ".
006200     05  LINE 20 COLUMN 17 PIC Z USING MENU-PICK.
006300     05  LINE 24 COLUMN  1 PIC X(79) FROM ERROR-MESSAGE.
006400
006500 01  KEY-SCREEN.
006600     05  BLANK SCREEN.
006700     05  LINE  4 COLUMN 20 VALUE "VENDOR NUMBER:".
006800     05  LINE  4 COLUMN 40 PIC Z(5) USING VENDOR-NUMBER.
006900     05  LINE 22 COLUMN  1 PIC X(79) FROM FOOTER-FIELD.
007000     05  LINE 24 COLUMN  1 PIC X(79) FROM ERROR-MESSAGE.
007100
007200 01  ENTRY-SCREEN.
007300     05  BLANK SCREEN.
007400     05  LINE  4 COLUMN 20 VALUE "VENDOR NUMBER:".
007500     05  LINE  4 COLUMN 40 PIC Z(5) FROM VENDOR-NUMBER.
007600     05  LINE  6 COLUMN 20 VALUE "VENDOR NAME: ".
007700     05  LINE  6 COLUMN 40 PIC X(30) USING VENDOR-NAME.
007800     05  LINE  8 COLUMN 20 VALUE "VENDOR ADDRESS:".
007900     05  LINE  8 COLUMN 40 PIC X(30) USING VENDOR-ADDRESS-1.
008000     05  LINE  9 COLUMN 40 PIC X(30) USING VENDOR-ADDRESS-2.
008100     05  LINE 10 COLUMN 20 VALUE "VENDOR CITY:".
008200     05  LINE 10 COLUMN 40 PIC X(20) USING VENDOR-CITY.
008300     05  LINE 12 COLUMN 20 VALUE "VENDOR STATE:".
008400     05  LINE 12 COLUMN 40 PIC XX USING VENDOR-STATE.
008500     05  LINE 12 COLUMN 45 PIC X(20) FROM STATE-NAME.
008600     05  LINE 14 COLUMN 20 VALUE "VENDOR ZIP:".
008700     05  LINE 14 COLUMN 40 PIC X(10) USING VENDOR-ZIP.
008800     05  LINE 16 COLUMN 20 VALUE "VENDOR CONTACT:".
008900     05  LINE 16 COLUMN 40 PIC X(30) USING VENDOR-CONTACT.
009000     05  LINE 18 COLUMN 20 VALUE "VENDOR PHONE:".
009100     05  LINE 18 COLUMN 40 PIC X(15) USING VENDOR-PHONE.
009200     05  LINE 22 COLUMN  1 PIC X(79) FROM FOOTER-FIELD.
009300     05  LINE 23 COLUMN  1 PIC X(40) FROM CONTINUE-MESSAGE.
009400     05  LINE 23 COLUMN 41 PIC X USING OK-TO-CONTINUE.
009500     05  LINE 24 COLUMN  1 PIC X(79) FROM ERROR-MESSAGE.
009600
009700 01  DISPLAY-SCREEN.
009800     05  BLANK SCREEN.
009900     05  LINE  4 COLUMN 20 VALUE "VENDOR NUMBER:".
010000     05  LINE  4 COLUMN 40 PIC Z(5) FROM VENDOR-NUMBER.
010100     05  LINE  6 COLUMN 20 VALUE "VENDOR NAME: ".
010200     05  LINE  6 COLUMN 40 PIC X(30) FROM VENDOR-NAME.
010300     05  LINE  8 COLUMN 20 VALUE "VENDOR ADDRESS:".
010400     05  LINE  8 COLUMN 40 PIC X(30) FROM VENDOR-ADDRESS-1.
010500     05  LINE  9 COLUMN 40 PIC X(30) FROM VENDOR-ADDRESS-2.
010600     05  LINE 10 COLUMN 20 VALUE "VENDOR CITY:".
010700     05  LINE 10 COLUMN 40 PIC X(20) FROM VENDOR-CITY.
010800     05  LINE 12 COLUMN 20 VALUE "VENDOR STATE:".
010900     05  LINE 12 COLUMN 40 PIC XX FROM VENDOR-STATE.
011000     05  LINE 12 COLUMN 45 PIC X(20) FROM STATE-NAME.
011100     05  LINE 14 COLUMN 20 VALUE "VENDOR ZIP:".
011200     05  LINE 14 COLUMN 40 PIC X(10) FROM VENDOR-ZIP.
011300     05  LINE 16 COLUMN 20 VALUE "VENDOR CONTACT:".
011400     05  LINE 16 COLUMN 40 PIC X(30) FROM VENDOR-CONTACT.
011500     05  LINE 18 COLUMN 20 VALUE "VENDOR PHONE:".
011600     05  LINE 18 COLUMN 40 PIC X(15) FROM VENDOR-PHONE.
011700     05  LINE 23 COLUMN  1 PIC X(40) FROM CONTINUE-MESSAGE.
011800     05  LINE 23 COLUMN 41 PIC X USING OK-TO-CONTINUE.
011900     05  LINE 24 COLUMN  1 PIC X(79) FROM ERROR-MESSAGE.
012000
012100 PROCEDURE DIVISION.
012200 PROGRAM-BEGIN.
012300     PERFORM OPENING-PROCEDURE.
012400     PERFORM MAIN-PROCESS.
012500     PERFORM CLOSING-PROCEDURE.
012600
012700 PROGRAM-EXIT.
012800     EXIT PROGRAM.
012900
013000 PROGRAM-DONE.
013100     ACCEPT OMITTED. STOP RUN.
013200
013300 OPENING-PROCEDURE.
013400     OPEN I-O VENDOR-FILE.
013500     OPEN I-O STATE-FILE.
013600
013700 CLOSING-PROCEDURE.
013800     CLOSE VENDOR-FILE.
013900     CLOSE STATE-FILE.
014000
014100 MAIN-PROCESS.
014200     PERFORM GET-MENU-PICK.
014300     PERFORM MAINTAIN-THE-FILE
014400         UNTIL MENU-PICK = 0.
014500
014600*---------------------------------
014700* MENU
014800*---------------------------------
014900 GET-MENU-PICK.
015000     PERFORM INITIALIZE-MENU-PICK.
015100     PERFORM DISPLAY-ACCEPT-MENU.
015200     PERFORM RE-DISPLAY-ACCEPT-MENU
015300         UNTIL MENU-PICK-IS-VALID.
015400
015500 INITIALIZE-MENU-PICK.
015600     MOVE 0 TO MENU-PICK.
015700
015800 DISPLAY-ACCEPT-MENU.
015900     DISPLAY MENU-SCREEN.
016000     ACCEPT MENU-SCREEN.
016100     MOVE SPACE TO ERROR-MESSAGE.
016200
016300 RE-DISPLAY-ACCEPT-MENU.
016400     MOVE "INVALID SELECTION - PLEASE RE-TRY."
016500           TO ERROR-MESSAGE.
016600     PERFORM DISPLAY-ACCEPT-MENU.
016700
016800 MAINTAIN-THE-FILE.
016900     PERFORM DO-THE-PICK.
017000     PERFORM GET-MENU-PICK.
017100
017200 DO-THE-PICK.
017300     IF MENU-PICK = 1
017400         PERFORM ADD-MODE
017500     ELSE
017600     IF MENU-PICK = 2
017700         PERFORM CHANGE-MODE
017800     ELSE
017900     IF MENU-PICK = 3
018000         PERFORM INQUIRE-MODE
018100     ELSE
018200     IF MENU-PICK = 4
018300         PERFORM DELETE-MODE
018400     ELSE
018500     IF MENU-PICK = 5
018600         PERFORM INQUIRE-BY-NAME
018700     ELSE
018800     IF MENU-PICK = 6
018900         PERFORM PRINT-VENDOR-REPORT
019000     ELSE
019100     IF MENU-PICK = 7
019200         PERFORM PRINT-BY-NAME
019300     ELSE
019400     IF MENU-PICK = 8
019500         PERFORM DISPLAY-ALL.
019600
019700*---------------------------------
019800* ADD
019900*---------------------------------
020000 ADD-MODE.
020100     PERFORM INITIALIZE-ADD-MODE.
020200     PERFORM GET-NEW-RECORD-KEY.
020300     PERFORM ADD-RECORDS
020400        UNTIL VENDOR-NUMBER = ZEROES.
020500
020600 INITIALIZE-ADD-MODE.
020700     MOVE "ENTER THE VENDOR NUMBER TO ADD"
020800       TO FOOTER-1-FIELD.
020900
021000 GET-NEW-RECORD-KEY.
021100     PERFORM ACCEPT-NEW-RECORD-KEY.
021200     PERFORM RE-ACCEPT-NEW-RECORD-KEY
021300        UNTIL VENDOR-NUMBER = ZEROES
021400           OR VENDOR-RECORD-FOUND = "N".
021500
021600 ACCEPT-NEW-RECORD-KEY.
021700     PERFORM INITIALIZE-VENDOR-FIELDS.
021800     PERFORM ENTER-VENDOR-NUMBER.
021900     IF VENDOR-NUMBER NOT = ZEROES
022000         PERFORM READ-VENDOR-RECORD.
022100
022200 RE-ACCEPT-NEW-RECORD-KEY.
022300     MOVE "RECORD ALREADY ON FILE" TO ERROR-MESSAGE.
022400     PERFORM ACCEPT-NEW-RECORD-KEY.
022500
022600 ADD-RECORDS.
022700     PERFORM INITIALIZE-TO-ADD-FIELDS.
022800     PERFORM ENTER-REMAINING-FIELDS.
022900     IF OK-TO-CONTINUE = "Y"
023000         PERFORM WRITE-VENDOR-RECORD.
023100     PERFORM GET-NEW-RECORD-KEY.
023200
023300 INITIALIZE-TO-ADD-FIELDS.
023400     MOVE "ADD NEW FIELDS" TO FOOTER-FIELD.
023500     MOVE "CONTINUE WITH ADDITIONS (Y/N)?"
023600         TO CONTINUE-MESSAGE.
023700     MOVE "Y" TO OK-TO-CONTINUE.
023800
023900*---------------------------------
024000* CHANGE
024100*---------------------------------
024200 CHANGE-MODE.
024300     PERFORM INITIALIZE-CHANGE-MODE.
024400     PERFORM GET-EXISTING-RECORD.
024500     PERFORM CHANGE-RECORDS
024600        UNTIL VENDOR-NUMBER = ZEROES.
024700
024800 INITIALIZE-CHANGE-MODE.
024900     MOVE "ENTER THE VENDOR NUMBER TO CHANGE"
025000        TO FOOTER-1-FIELD.
025100
025200 CHANGE-RECORDS.
025300     PERFORM INITIALIZE-TO-CHANGE-FIELDS.
025400     PERFORM ENTER-REMAINING-FIELDS.
025500     IF OK-TO-CONTINUE = "Y"
025600         PERFORM REWRITE-VENDOR-RECORD.
025700     PERFORM GET-EXISTING-RECORD.
025800
025900 INITIALIZE-TO-CHANGE-FIELDS.
026000     MOVE "CHANGE FIELDS" TO FOOTER-FIELD.
026100     MOVE "CONTINUE WITH CHANGES (Y/N)?"
026200        TO CONTINUE-MESSAGE.
026300     MOVE "Y" TO OK-TO-CONTINUE.
026400
026500*---------------------------------
026600* INQUIRE
026700*---------------------------------
026800 INQUIRE-MODE.
026900     PERFORM INITIALIZE-INQUIRE-MODE.
027000     PERFORM GET-EXISTING-RECORD.
027100     PERFORM INQUIRE-RECORDS
027200        UNTIL VENDOR-NUMBER = ZEROES.
027300
027400 INITIALIZE-INQUIRE-MODE.
027500     MOVE "ENTER THE VENDOR NUMBER TO DISPLAY"
027600        TO FOOTER-1-FIELD.
027700
027800 INQUIRE-RECORDS.
027900     PERFORM INITIALIZE-TO-INQUIRE.
028000     PERFORM DISPLAY-ACCEPT-ALL-FIELDS.
028100     PERFORM GET-EXISTING-RECORD.
028200
028300 INITIALIZE-TO-INQUIRE.
028400     MOVE "PRESS ENTER TO CONTINUE" TO CONTINUE-MESSAGE.
028500     MOVE SPACE TO OK-TO-CONTINUE.
028600
028700*---------------------------------
028800* DELETE
028900*---------------------------------
029000 DELETE-MODE.
029100     PERFORM INITIALIZE-DELETE-MODE.
029200     PERFORM GET-EXISTING-RECORD.
029300     PERFORM DELETE-RECORDS
029400        UNTIL VENDOR-NUMBER = ZEROES.
029500
029600 INITIALIZE-DELETE-MODE.
029700     MOVE "ENTER THE VENDOR NUMBER TO DELETE"
029800       TO FOOTER-1-FIELD.
029900
030000 DELETE-RECORDS.
030100     PERFORM INITIALIZE-TO-DELETE-RECORD.
030200     PERFORM ASK-OK-TO-DELETE.
030300     IF OK-TO-CONTINUE = "Y"
030400         PERFORM DELETE-VENDOR-RECORD.
030500     PERFORM GET-EXISTING-RECORD.
030600
030700 INITIALIZE-TO-DELETE-RECORD.
030800     MOVE "OK TO DELETE(Y/N)?" TO CONTINUE-MESSAGE.
030900     MOVE "N" TO OK-TO-CONTINUE.
031000
031100 ASK-OK-TO-DELETE.
031200     PERFORM DISPLAY-ACCEPT-ALL-FIELDS.
031300     PERFORM RE-DISPLAY-ACCEPT-ALL-FIELDS
031400        UNTIL OK-TO-CONTINUE = "Y" OR "N".
031500
031600 RE-DISPLAY-ACCEPT-ALL-FIELDS.
031700     MOVE "YOU MUST ENTER YES OR NO"
031800         TO ERROR-MESSAGE.
031900     PERFORM DISPLAY-ACCEPT-ALL-FIELDS.
032000
032100*---------------------------------
032200* Routines shared by all modes
032300*---------------------------------
032400 INITIALIZE-VENDOR-FIELDS.
032500     MOVE SPACE TO VENDOR-RECORD.
032600     MOVE ZEROES TO VENDOR-NUMBER.
032700     MOVE SPACE TO STATE-NAME.
032800
032900 ENTER-VENDOR-NUMBER.
033000     MOVE "ENTER 0 TO QUIT" TO FOOTER-2-FIELD.
033100     DISPLAY KEY-SCREEN.
033200     ACCEPT KEY-SCREEN.
033300     MOVE SPACE TO ERROR-MESSAGE.
033400
033500*---------------------------------
033600* Routines shared Add and Change
033700*---------------------------------
033800 ENTER-REMAINING-FIELDS.
033900     PERFORM DISPLAY-ACCEPT-ENTRY-SCREEN.
034000     PERFORM DISPLAY-ACCEPT-ENTRY-SCREEN
034100         UNTIL SCREEN-ERROR = "N"
034200            OR OK-TO-CONTINUE = "N".
034300
034400 DISPLAY-ACCEPT-ENTRY-SCREEN.
034500     DISPLAY ENTRY-SCREEN.
034600     ACCEPT ENTRY-SCREEN.
034700     MOVE SPACE TO ERROR-MESSAGE.
034800
034900     INSPECT OK-TO-CONTINUE
035000      CONVERTING LOWER-ALPHA TO UPPER-ALPHA.
035100
035200     IF OK-TO-CONTINUE = "Y"
035300         PERFORM EDIT-CHECK-FIELDS.
035400
035500 EDIT-CHECK-FIELDS.
035600     MOVE "N" TO SCREEN-ERROR.
035700     PERFORM EDIT-CHECK-VENDOR-NAME.
035800     IF SCREEN-ERROR NOT = "Y"
035900         PERFORM EDIT-CHECK-VENDOR-ADDRESS-1.
036000     IF SCREEN-ERROR NOT = "Y"
036100         PERFORM EDIT-CHECK-VENDOR-ADDRESS-2.
036200     IF SCREEN-ERROR NOT = "Y"
036300         PERFORM EDIT-CHECK-VENDOR-CITY.
036400     IF SCREEN-ERROR NOT = "Y"
036500         PERFORM EDIT-CHECK-VENDOR-STATE.
036600     IF SCREEN-ERROR NOT = "Y"
036700         PERFORM EDIT-CHECK-VENDOR-ZIP.
036800     IF SCREEN-ERROR NOT = "Y"
036900         PERFORM EDIT-CHECK-VENDOR-CONTACT.
037000     IF SCREEN-ERROR NOT = "Y"
037100         PERFORM EDIT-CHECK-VENDOR-PHONE.
037200
037300 EDIT-CHECK-VENDOR-NAME.
037400     INSPECT VENDOR-NAME
037500         CONVERTING LOWER-ALPHA
037600         TO         UPPER-ALPHA.
037700
037800     IF VENDOR-NAME = SPACES
037900         MOVE "Y" TO SCREEN-ERROR
038000         MOVE "VENDOR NAME MUST BE ENTERED"
038100            TO ERROR-MESSAGE.
038200 EDIT-CHECK-VENDOR-ADDRESS-1.
038300     INSPECT VENDOR-ADDRESS-1
038400         CONVERTING LOWER-ALPHA
038500         TO         UPPER-ALPHA.
038600     IF VENDOR-ADDRESS-1 = SPACE
038700         MOVE "Y" TO SCREEN-ERROR
038800         MOVE "VENDOR ADDRESS-1 MUST BE ENTERED"
038900           TO ERROR-MESSAGE.
039000
039100 EDIT-CHECK-VENDOR-ADDRESS-2.
039200     INSPECT VENDOR-ADDRESS-2
039300         CONVERTING LOWER-ALPHA
039400         TO         UPPER-ALPHA.
039500
039600 EDIT-CHECK-VENDOR-CITY.
039700     INSPECT VENDOR-CITY
039800         CONVERTING LOWER-ALPHA
039900         TO         UPPER-ALPHA.
040000
040100     IF VENDOR-CITY = SPACE
040200         MOVE "Y" TO SCREEN-ERROR
040300         MOVE  "VENDOR CITY MUST BE ENTERED"
040400           TO ERROR-MESSAGE.
040500
040600 EDIT-CHECK-VENDOR-STATE.
040700     PERFORM EDIT-VENDOR-STATE.
040800     PERFORM CHECK-VENDOR-STATE.
040900
041000 EDIT-VENDOR-STATE.
041100     INSPECT VENDOR-STATE
041200         CONVERTING LOWER-ALPHA
041300         TO         UPPER-ALPHA.
041400
041500 CHECK-VENDOR-STATE.
041600     PERFORM VENDOR-STATE-REQUIRED.
041700     IF VENDOR-STATE NOT = SPACES
041800         PERFORM VENDOR-STATE-ON-FILE.
041900
042000 VENDOR-STATE-REQUIRED.
042100     IF VENDOR-STATE = SPACE
042200         MOVE "Y" TO SCREEN-ERROR
042300         MOVE "VENDOR STATE MUST BE ENTERED"
042400           TO ERROR-MESSAGE.
042500
042600 VENDOR-STATE-ON-FILE.
042700     MOVE VENDOR-STATE TO STATE-CODE.
042800     PERFORM READ-STATE-RECORD.
042900     IF STATE-RECORD-FOUND = "N"
043000         MOVE "Y" TO SCREEN-ERROR
043100         MOVE "STATE CODE NOT FOUND IN CODES FILE"
043200           TO ERROR-MESSAGE.
043300
043400 EDIT-CHECK-VENDOR-ZIP.
043500     INSPECT VENDOR-ZIP
043600         CONVERTING LOWER-ALPHA
043700         TO         UPPER-ALPHA.
043800     IF VENDOR-ZIP = SPACE
043900         MOVE "Y" TO SCREEN-ERROR
044000         MOVE "VENDOR ZIP MUST BE ENTERED"
044100           TO ERROR-MESSAGE.
044200
044300 EDIT-CHECK-VENDOR-CONTACT.
044400     INSPECT VENDOR-CONTACT
044500         CONVERTING LOWER-ALPHA
044600         TO         UPPER-ALPHA.
044700
044800 EDIT-CHECK-VENDOR-PHONE.
044900     INSPECT VENDOR-PHONE
045000         CONVERTING LOWER-ALPHA
045100         TO         UPPER-ALPHA.
045200     IF VENDOR-PHONE = SPACE
045300         MOVE "Y" TO SCREEN-ERROR
045400         MOVE "ENTER VENDOR PHONE"
045500            TO ERROR-MESSAGE.
045600
045700*---------------------------------
045800* Routines shared by Change,
045900* Inquire and Delete
046000*---------------------------------
046100 GET-EXISTING-RECORD.
046200     PERFORM INITIALIZE-VENDOR-FIELDS.
046300     PERFORM ACCEPT-EXISTING-KEY.
046400     PERFORM RE-ACCEPT-EXISTING-KEY
046500         UNTIL VENDOR-RECORD-FOUND = "Y" OR
046600               VENDOR-NUMBER = ZEROES.
046700
046800 ACCEPT-EXISTING-KEY.
046900     PERFORM ENTER-VENDOR-NUMBER.
047000     IF VENDOR-NUMBER NOT = ZEROES
047100         PERFORM READ-VENDOR-AND-STATE.
047200
047300 RE-ACCEPT-EXISTING-KEY.
047400     MOVE "RECORD NOT FOUND" TO ERROR-MESSAGE.
047500     PERFORM ACCEPT-EXISTING-KEY.
047600
047700*---------------------------------
047800* Routines shared by delete and inquire
047900*---------------------------------
048000 DISPLAY-ACCEPT-ALL-FIELDS.
048100     DISPLAY DISPLAY-SCREEN.
048200     ACCEPT DISPLAY-SCREEN.
048300     MOVE SPACE TO ERROR-MESSAGE.
048400     INSPECT OK-TO-CONTINUE
048500      CONVERTING LOWER-ALPHA TO UPPER-ALPHA.
048600
048700*---------------------------------
048800* File I-O Routines
048900*---------------------------------
049000 READ-VENDOR-AND-STATE.
049100     PERFORM READ-VENDOR-RECORD.
049200     IF VENDOR-RECORD-FOUND = "Y"
049300         MOVE VENDOR-STATE TO STATE-CODE
049400         PERFORM READ-STATE-RECORD
049500         IF STATE-RECORD-FOUND = "N"
049600             MOVE "*** NOT FOUND ***" TO STATE-NAME.
049700
049800 READ-VENDOR-RECORD.
049900     MOVE "Y" TO VENDOR-RECORD-FOUND.
050000     READ VENDOR-FILE RECORD
050100       INVALID KEY
050200          MOVE "N" TO VENDOR-RECORD-FOUND.
050300
050400 WRITE-VENDOR-RECORD.
050500     WRITE VENDOR-RECORD
050600         INVALID KEY
050700         DISPLAY "RECORD ALREADY ON FILE".
050800
050900 REWRITE-VENDOR-RECORD.
051000     REWRITE VENDOR-RECORD
051100         INVALID KEY
051200         DISPLAY "ERROR REWRITING VENDOR RECORD".
051300
051400 DELETE-VENDOR-RECORD.
051500     DELETE VENDOR-FILE RECORD
051600         INVALID KEY
051700         DISPLAY "ERROR DELETING VENDOR RECORD".
051800
051900 READ-STATE-RECORD.
052000     MOVE "Y" TO STATE-RECORD-FOUND.
052100     READ STATE-FILE RECORD
052200       INVALID KEY
052300          MOVE "N" TO STATE-RECORD-FOUND.
052400
052500*---------------------------------
052600* CALLS TO OTHER PROGRAMS
052700*---------------------------------
052800
052900*---------------------------------
053000* INQUIRE BY NAME
053100*---------------------------------
053200 INQUIRE-BY-NAME.
053300     PERFORM CLOSING-PROCEDURE.
053400     CALL "VNINNM03".
053500     PERFORM OPENING-PROCEDURE.
053600
053700*---------------------------------
053800* PRINT
053900*---------------------------------
054000 PRINT-VENDOR-REPORT.
054100     PERFORM CLOSING-PROCEDURE.
054200     DISPLAY "VENDOR REPORT IN PROGRESS".
054300     CALL "VNDRPT04".
054400     PERFORM OPENING-PROCEDURE.
054500
054600*---------------------------------
054700* PRINT BY NAME
054800*---------------------------------
054900 PRINT-BY-NAME.
055000     PERFORM CLOSING-PROCEDURE.
055100     DISPLAY " REPORT BY NAME IN PROGRESS".
055200     CALL "VNBYNM02".
055300     PERFORM OPENING-PROCEDURE.
055400
055500*---------------------------------
055600* DISPLAY ALL
055700*---------------------------------
055800 DISPLAY-ALL.
055900     PERFORM CLOSING-PROCEDURE.
056000     CALL "VNDDSP03".
056100     DISPLAY "DISPLAY COMPLETE".
056200     DISPLAY "PRESS ENTER TO CONTINUE".
056300     ACCEPT OK-TO-CONTINUE.
056400     PERFORM OPENING-PROCEDURE.
056500
