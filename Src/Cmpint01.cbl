000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CMPINT01.
000300*------------------------------------------------
000400* Calculates compound interest
000500*------------------------------------------------
000600 ENVIRONMENT DIVISION.
000700 DATA DIVISION.
000800 WORKING-STORAGE SECTION.
000900
001000 01  YES-NO                           PIC X.
001100 01  THE-INTEREST                     PIC 99V9.
001200 01  INTEREST-AS-DECIMAL              PIC V999.
001300 01  THE-PRINCIPAL                    PIC 9(9)V99.
001400 01  THE-NEW-VALUE                    PIC 9(9)V99.
001500 01  EARNED-INTEREST                  PIC 9(9)V99.
001600 01  THE-PERIOD                       PIC 9999.
001700 01  NO-OF-PERIODS                    PIC 999.
001800
001900 01  ENTRY-FIELD                      PIC Z(9).ZZ.
002000 01  DISPLAY-VALUE                    PIC ZZZ,ZZZ,ZZ9.99.
002100
002200 PROCEDURE DIVISION.
002300 PROGRAM-BEGIN.
002400
002500     MOVE "Y" TO YES-NO.
002600     PERFORM GET-AND-DISPLAY-RESULT
002700         UNTIL YES-NO = "N".
002800
002900 PROGRAM-DONE.
003000     STOP RUN.
003100
003200 GET-AND-DISPLAY-RESULT.
003300     PERFORM GET-THE-PRINCIPAL.
003400     PERFORM GET-THE-INTEREST.
003500     PERFORM GET-THE-PERIODS.
003600     PERFORM CALCULATE-THE-RESULT.
003700     PERFORM DISPLAY-THE-RESULT.
003800     PERFORM GO-AGAIN.
003900
004000 GET-THE-PRINCIPAL.
004100     DISPLAY "Principal (.01 TO 999999.99)?".
004200     ACCEPT ENTRY-FIELD WITH CONVERSION.
004300     MOVE ENTRY-FIELD TO THE-PRINCIPAL.
004400     IF THE-PRINCIPAL < .01 OR
004500        THE-PRINCIPAL > 999999.99
004600         DISPLAY "INVALID ENTRY"
004700         GO TO GET-THE-PRINCIPAL.
004800
004900 GET-THE-INTEREST.
005000     DISPLAY "Interest (.1% TO 99.9%)?".
005100     ACCEPT ENTRY-FIELD WITH CONVERSION.
005200     MOVE ENTRY-FIELD TO THE-INTEREST.
005300     IF THE-INTEREST < .1 OR
005400        THE-INTEREST > 99.9
005500         DISPLAY "INVALID ENTRY"
005600         GO TO GET-THE-INTEREST
005700     ELSE
005800         COMPUTE INTEREST-AS-DECIMAL =
005900                 THE-INTEREST / 100.
006000
006100 GET-THE-PERIODS.
006200     DISPLAY "Number of periods (1 TO 999)?".
006300     ACCEPT ENTRY-FIELD WITH CONVERSION.
006400     MOVE ENTRY-FIELD TO NO-OF-PERIODS.
006500     IF NO-OF-PERIODS < 1 OR
006600        NO-OF-PERIODS > 999
006700         DISPLAY "INVALID ENTRY"
006800         GO TO GET-THE-PERIODS.
006900
007000 CALCULATE-THE-RESULT.
007100     PERFORM CALCULATE-ONE-PERIOD
007200         VARYING THE-PERIOD FROM 1 BY 1
007300          UNTIL THE-PERIOD > NO-OF-PERIODS.
007400
007500 CALCULATE-ONE-PERIOD.
007600     COMPUTE EARNED-INTEREST ROUNDED =
007700         THE-PRINCIPAL * INTEREST-AS-DECIMAL.
007800     COMPUTE THE-NEW-VALUE =
007900             THE-PRINCIPAL + EARNED-INTEREST.
008000     MOVE THE-NEW-VALUE TO THE-PRINCIPAL.
008100
008200 GO-AGAIN.
008300     DISPLAY "GO AGAIN?".
008400     ACCEPT YES-NO.
008500     IF YES-NO = "y"
008600         MOVE "Y" TO YES-NO.
008700     IF YES-NO NOT = "Y"
008800         MOVE "N" TO YES-NO.
008900
009000 DISPLAY-THE-RESULT.
009100     MOVE THE-NEW-VALUE TO DISPLAY-VALUE.
009200     DISPLAY "RESULTING VALUE IS " DISPLAY-VALUE.
009300
