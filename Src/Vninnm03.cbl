000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNINNM03.
000300*---------------------------------
000400* Inquire for the Vendor File
000500* using vendor name.
000600*---------------------------------
000700 ENVIRONMENT DIVISION.
000800 INPUT-OUTPUT SECTION.
000900 FILE-CONTROL.
001000
001100     COPY "SLVND02.CBL".
001200
001300     COPY "SLSTATE.CBL".
001400
001500 DATA DIVISION.
001600 FILE SECTION.
001700
001800     COPY "FDVND04.CBL".
001900
002000     COPY "FDSTATE.CBL".
002100
002200 WORKING-STORAGE SECTION.
002300
002400 77  VENDOR-FILE-AT-END           PIC X.
002500 77  STATE-RECORD-FOUND           PIC X.
002600
002700 77  SEE-NEXT-RECORD             PIC X.
002800
002900 77  VENDOR-NAME-FIELD            PIC X(30).
003000
003100     COPY "WSCASE01.CBL".
003200
003300 PROCEDURE DIVISION.
003400 PROGRAM-BEGIN.
003500     PERFORM OPENING-PROCEDURE.
003600     PERFORM MAIN-PROCESS.
003700     PERFORM CLOSING-PROCEDURE.
003800
003900 PROGRAM-EXIT.
004000     EXIT PROGRAM.
004100
004200 PROGRAM-DONE.
004300     STOP RUN.
004400
004500 OPENING-PROCEDURE.
004600     OPEN I-O VENDOR-FILE.
004700     OPEN I-O STATE-FILE.
004800
004900 CLOSING-PROCEDURE.
005000     CLOSE VENDOR-FILE.
005100     CLOSE STATE-FILE.
005200
005300 MAIN-PROCESS.
005400     PERFORM INQUIRE-BY-NAME.
005500*---------------------------------
005600* INQUIRE
005700*---------------------------------
005800 INQUIRE-BY-NAME.
005900     PERFORM GET-EXISTING-RECORD.
006000     PERFORM INQUIRE-RECORDS
006100        UNTIL VENDOR-NAME = SPACES.
006200
006300 INQUIRE-RECORDS.
006400     PERFORM SHOW-THIS-RECORD.
006500     PERFORM SHOW-NEXT-RECORD
006600        UNTIL SEE-NEXT-RECORD = "N" OR
006700              VENDOR-FILE-AT-END = "Y".
006800
006900     PERFORM GET-EXISTING-RECORD.
007000
007100
007200*---------------------------------
007300* Show records one by one
007400*---------------------------------
007500 SHOW-THIS-RECORD.
007600     PERFORM DISPLAY-ALL-FIELDS.
007700     PERFORM GET-SEE-NEXT-RECORD.
007800
007900 SHOW-NEXT-RECORD.
008000     PERFORM READ-NEXT-VENDOR-RECORD.
008100     IF VENDOR-FILE-AT-END NOT = "Y"
008200         PERFORM SHOW-THIS-RECORD.
008300
008400*---------------------------------
008500* Get valid record logic
008600*---------------------------------
008700 GET-EXISTING-RECORD.
008800     PERFORM ACCEPT-EXISTING-KEY.
008900     PERFORM RE-ACCEPT-EXISTING-KEY
009000         UNTIL VENDOR-FILE-AT-END NOT = "Y".
009100
009200 ACCEPT-EXISTING-KEY.
009300     PERFORM INIT-FOR-KEY-ENTRY.
009400     PERFORM ENTER-VENDOR-NAME.
009500     IF VENDOR-NAME NOT = SPACES
009600         PERFORM READ-FIRST-VENDOR-RECORD.
009700
009800 RE-ACCEPT-EXISTING-KEY.
009900     DISPLAY "RECORD NOT FOUND"
010000     PERFORM ACCEPT-EXISTING-KEY.
010100
010200*---------------------------------
010300* Field Entry logic
010400*---------------------------------
010500 ENTER-VENDOR-NAME.
010600     PERFORM ACCEPT-VENDOR-NAME.
010700
010800 ACCEPT-VENDOR-NAME.
010900     DISPLAY "ENTER VENDOR NAME".
011000     ACCEPT VENDOR-NAME.
011100     INSPECT VENDOR-NAME
011200         CONVERTING LOWER-ALPHA
011300         TO         UPPER-ALPHA.
011400
011500 GET-SEE-NEXT-RECORD.
011600     PERFORM ACCEPT-SEE-NEXT-RECORD.
011700     PERFORM RE-ACCEPT-SEE-NEXT-RECORD
011800         UNTIL SEE-NEXT-RECORD = "Y" OR "N".
011900
012000 ACCEPT-SEE-NEXT-RECORD.
012100     DISPLAY "DISPLAY NEXT RECORD (Y/N)?".
012200     ACCEPT SEE-NEXT-RECORD.
012300
012400     IF SEE-NEXT-RECORD = SPACE
012500         MOVE "Y" TO SEE-NEXT-RECORD.
012600
012700     INSPECT SEE-NEXT-RECORD
012800       CONVERTING LOWER-ALPHA
012900       TO         UPPER-ALPHA.
013000
013100 RE-ACCEPT-SEE-NEXT-RECORD.
013200     DISPLAY "MUST ENTER YES OR NO".
013300     PERFORM ACCEPT-SEE-NEXT-RECORD.
013400
013500*---------------------------------
013600* Display logic
013700*---------------------------------
013800 DISPLAY-ALL-FIELDS.
013900     DISPLAY " ".
014000     PERFORM DISPLAY-VENDOR-NUMBER.
014100     PERFORM DISPLAY-VENDOR-NAME.
014200     PERFORM DISPLAY-VENDOR-ADDRESS-1.
014300     PERFORM DISPLAY-VENDOR-ADDRESS-2.
014400     PERFORM DISPLAY-VENDOR-CITY.
014500     PERFORM DISPLAY-VENDOR-STATE.
014600     PERFORM DISPLAY-VENDOR-ZIP.
014700     PERFORM DISPLAY-VENDOR-CONTACT.
014800     PERFORM DISPLAY-VENDOR-PHONE.
014900     DISPLAY " ".
015000
015100 DISPLAY-VENDOR-NUMBER.
015200     DISPLAY "   VENDOR NUMBER: " VENDOR-NUMBER.
015300
015400 DISPLAY-VENDOR-NAME.
015500     DISPLAY "1. VENDOR NAME: " VENDOR-NAME.
015600
015700 DISPLAY-VENDOR-ADDRESS-1.
015800     DISPLAY "2. VENDOR ADDRESS-1: " VENDOR-ADDRESS-1.
015900
016000 DISPLAY-VENDOR-ADDRESS-2.
016100     DISPLAY "3. VENDOR ADDRESS-2: " VENDOR-ADDRESS-2.
016200
016300 DISPLAY-VENDOR-CITY.
016400     DISPLAY "4. VENDOR CITY: " VENDOR-CITY.
016500
016600 DISPLAY-VENDOR-STATE.
016700     MOVE VENDOR-STATE TO STATE-CODE.
016800     PERFORM READ-STATE-RECORD.
016900     IF STATE-RECORD-FOUND = "N"
017000         MOVE "**Not found**" TO STATE-NAME.
017100     DISPLAY "5. VENDOR STATE: "
017200             VENDOR-STATE " "
017300             STATE-NAME.
017400
017500 DISPLAY-VENDOR-ZIP.
017600     DISPLAY "6. VENDOR ZIP: " VENDOR-ZIP.
017700
017800 DISPLAY-VENDOR-CONTACT.
017900     DISPLAY "7. VENDOR CONTACT: " VENDOR-CONTACT.
018000
018100 DISPLAY-VENDOR-PHONE.
018200     DISPLAY "8. VENDOR PHONE: " VENDOR-PHONE.
018300
018400*---------------------------------
018500* File Related Routines
018600*---------------------------------
018700 INIT-FOR-KEY-ENTRY.
018800     MOVE SPACE TO VENDOR-RECORD.
018900     MOVE ZEROES TO VENDOR-NUMBER.
019000     MOVE "N" TO VENDOR-FILE-AT-END.
019100
019200 READ-FIRST-VENDOR-RECORD.
019300     MOVE "N" TO VENDOR-FILE-AT-END.
019400     START VENDOR-FILE
019500        KEY NOT < VENDOR-NAME
019600         INVALID KEY
019700          MOVE "Y" TO VENDOR-FILE-AT-END.
019800
019900     IF VENDOR-FILE-AT-END NOT = "Y"
020000         PERFORM READ-NEXT-VENDOR-RECORD.
020100
020200 READ-NEXT-VENDOR-RECORD.
020300     READ VENDOR-FILE NEXT RECORD
020400       AT END
020500          MOVE "Y" TO VENDOR-FILE-AT-END.
020600
020700 READ-STATE-RECORD.
020800     MOVE "Y" TO STATE-RECORD-FOUND.
020900     READ STATE-FILE RECORD
021000       INVALID KEY
021100          MOVE "N" TO STATE-RECORD-FOUND.
021200
