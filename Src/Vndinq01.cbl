000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDINQ01.
000300*---------------------------------
000400* Look Up records in Vendor File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND01.CBL".
001100
001200 DATA DIVISION.
001300 FILE SECTION.
001400
001500     COPY "FDVND02.CBL".
001600
001700 WORKING-STORAGE SECTION.
001800
001900 77  RECORD-FOUND        PIC X.
002000
002100 77  VENDOR-NUMBER-FIELD PIC Z(5).
002200
002300 PROCEDURE DIVISION.
002400 PROGRAM-BEGIN.
002500     OPEN I-O VENDOR-FILE.
002600     PERFORM GET-VENDOR-RECORD.
002700     PERFORM INQUIRE-RECORDS
002800        UNTIL VENDOR-NUMBER = ZEROES.
002900     CLOSE VENDOR-FILE.
003000
003100 PROGRAM-DONE.
003200     STOP RUN.
003300
003400*---------------------------------
003500* TO GET A VENDOR RECORD, ASK FOR
003600* VENDOR NUMBER, AND THEN TRY TO
003700* READ THE RECORD.
003800*---------------------------------
003900 GET-VENDOR-RECORD.
004000     PERFORM INIT-VENDOR-RECORD.
004100     PERFORM ENTER-VENDOR-NUMBER.
004200     MOVE "N" TO RECORD-FOUND.
004300     PERFORM FIND-VENDOR-RECORD
004400         UNTIL RECORD-FOUND = "Y" OR
004500               VENDOR-NUMBER = ZEROES.
004600
004700 INIT-VENDOR-RECORD.
004800     MOVE SPACE TO VENDOR-RECORD.
004900     MOVE ZEROES TO VENDOR-NUMBER.
005000
005100 ENTER-VENDOR-NUMBER.
005200     DISPLAY " ".
005300     DISPLAY "ENTER VENDOR NUMBER OF THE VENDOR" .
005400     DISPLAY "TO DISPLAY (1-99999)".
005500     DISPLAY "ENTER 0 TO STOP ENTRY".
005600     ACCEPT VENDOR-NUMBER-FIELD.
005700*OR  ACCEPT VENDOR-NUMBER-FIELD WITH CONVERSION.
005800
005900     MOVE VENDOR-NUMBER-FIELD TO VENDOR-NUMBER.
006000
006100 FIND-VENDOR-RECORD.
006200     PERFORM READ-VENDOR-RECORD.
006300     IF RECORD-FOUND = "N"
006400         DISPLAY "RECORD NOT FOUND"
006500         PERFORM ENTER-VENDOR-NUMBER.
006600
006700 READ-VENDOR-RECORD.
006800     MOVE "Y" TO RECORD-FOUND.
006900     READ VENDOR-FILE RECORD
007000       INVALID KEY
007100          MOVE "N" TO RECORD-FOUND.
007200
007300*or  READ VENDOR-FILE RECORD WITH LOCK
007400*      INVALID KEY
007500*         MOVE "N" TO RECORD-FOUND.
007600
007700*or  READ VENDOR-FILE RECORD WITH HOLD
007800*      INVALID KEY
007900*         MOVE "N" TO RECORD-FOUND.
008000
008100 INQUIRE-RECORDS.
008200     PERFORM DISPLAY-ALL-FIELDS.
008300
008400     PERFORM GET-VENDOR-RECORD.
008500
008600*---------------------------------
008700* DISPLAY ALL FIELDS WITH BLANK
008800* LINES ABOVE AND BELOW.
008900*---------------------------------
009000 DISPLAY-ALL-FIELDS.
009100     DISPLAY " ".
009200     PERFORM DISPLAY-VENDOR-NUMBER.
009300     PERFORM DISPLAY-VENDOR-NAME.
009400     PERFORM DISPLAY-VENDOR-ADDRESS-1.
009500     PERFORM DISPLAY-VENDOR-ADDRESS-2.
009600     PERFORM DISPLAY-VENDOR-CITY.
009700     PERFORM DISPLAY-VENDOR-STATE.
009800     PERFORM DISPLAY-VENDOR-ZIP.
009900     PERFORM DISPLAY-VENDOR-CONTACT.
010000     PERFORM DISPLAY-VENDOR-PHONE.
010100     DISPLAY " ".
010200
010300 DISPLAY-VENDOR-NUMBER.
010400     DISPLAY "   VENDOR NUMBER: " VENDOR-NUMBER.
010500
010600 DISPLAY-VENDOR-NAME.
010700     DISPLAY "1. VENDOR NAME: " VENDOR-NAME.
010800
010900 DISPLAY-VENDOR-ADDRESS-1.
011000     DISPLAY "2. VENDOR ADDRESS-1: " VENDOR-ADDRESS-1.
011100
011200 DISPLAY-VENDOR-ADDRESS-2.
011300     DISPLAY "3. VENDOR ADDRESS-2: " VENDOR-ADDRESS-2.
011400
011500 DISPLAY-VENDOR-CITY.
011600     DISPLAY "4. VENDOR CITY: " VENDOR-CITY.
011700
011800 DISPLAY-VENDOR-STATE.
011900     DISPLAY "5. VENDOR STATE: " VENDOR-STATE.
012000
012100 DISPLAY-VENDOR-ZIP.
012200     DISPLAY "6. VENDOR ZIP: " VENDOR-ZIP.
012300
012400 DISPLAY-VENDOR-CONTACT.
012500     DISPLAY "7. VENDOR CONTACT: " VENDOR-CONTACT.
012600
012700 DISPLAY-VENDOR-PHONE.
012800     DISPLAY "8. VENDOR PHONE: " VENDOR-PHONE.
012900
