000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDMNT04.
000300*---------------------------------
000400* Add, Change, Inquire and Delete
000500* for the Vendor File.
000600* Menu includes inquire by name,
000700* the vendor report, and the vendor
000800* report in name order.
000900*---------------------------------
001000 ENVIRONMENT DIVISION.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300
001400     COPY "SLVND02.CBL".
001500
001600     COPY "SLSTATE.CBL".
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100     COPY "FDVND04.CBL".
002200
002300     COPY "FDSTATE.CBL".
002400
002500 WORKING-STORAGE SECTION.
002600
002700 77  MENU-PICK                    PIC 9.
002800     88  MENU-PICK-IS-VALID       VALUES 0 THRU 7.
002900
003000 77  THE-MODE                     PIC X(7).
003100 77  WHICH-FIELD                  PIC 9.
003200 77  OK-TO-DELETE                 PIC X.
003300 77  VENDOR-RECORD-FOUND          PIC X.
003400 77  STATE-RECORD-FOUND           PIC X.
003500
003600
003700 77  VENDOR-NUMBER-FIELD          PIC Z(5).
003800
003900 77  ERROR-MESSAGE                PIC X(79) VALUE SPACE.
004000
004100     COPY "WSCASE01.CBL".
004200
004300 PROCEDURE DIVISION.
004400 PROGRAM-BEGIN.
004500     PERFORM OPENING-PROCEDURE.
004600     PERFORM MAIN-PROCESS.
004700     PERFORM CLOSING-PROCEDURE.
004800
004900 PROGRAM-EXIT.
005000     EXIT PROGRAM.
005100
005200 PROGRAM-DONE.
005300     STOP RUN.
005400
005500 OPENING-PROCEDURE.
005600     OPEN I-O VENDOR-FILE.
005700     OPEN I-O STATE-FILE.
005800
005900 CLOSING-PROCEDURE.
006000     CLOSE VENDOR-FILE.
006100     CLOSE STATE-FILE.
006200
006300 MAIN-PROCESS.
006400     PERFORM GET-MENU-PICK.
006500     PERFORM MAINTAIN-THE-FILE
006600         UNTIL MENU-PICK = 0.
006700
006800*---------------------------------
006900* MENU
007000*---------------------------------
007100 GET-MENU-PICK.
007200     PERFORM DISPLAY-THE-MENU.
007300     PERFORM ACCEPT-MENU-PICK.
007400     PERFORM RE-ACCEPT-MENU-PICK
007500         UNTIL MENU-PICK-IS-VALID.
007600
007700 DISPLAY-THE-MENU.
007800     PERFORM CLEAR-SCREEN.
007900     DISPLAY "    PLEASE SELECT:".
008000     DISPLAY " ".
008100     DISPLAY "          1.  ADD RECORDS".
008200     DISPLAY "          2.  CHANGE A RECORD".
008300     DISPLAY "          3.  LOOK UP A RECORD".
008400     DISPLAY "          4.  DELETE A RECORD".
008500     DISPLAY "          5.  LOOK UP BY NAME".
008600     DISPLAY "          6.  PRINT RECORDS".
008700     DISPLAY "          7.  PRINT IN NAME ORDER".
008800     DISPLAY " ".
008900     DISPLAY "          0.  EXIT".
009000     PERFORM SCROLL-LINE 8 TIMES.
009100
009200 ACCEPT-MENU-PICK.
009300     DISPLAY "YOUR CHOICE (0-7)?".
009400     ACCEPT MENU-PICK.
009500
009600 RE-ACCEPT-MENU-PICK.
009700     DISPLAY "INVALID SELECTION - PLEASE RE-TRY.".
009800     PERFORM ACCEPT-MENU-PICK.
009900
010000 CLEAR-SCREEN.
010100     PERFORM SCROLL-LINE 25 TIMES.
010200
010300 SCROLL-LINE.
010400     DISPLAY " ".
010500
010600 MAINTAIN-THE-FILE.
010700     PERFORM DO-THE-PICK.
010800     PERFORM GET-MENU-PICK.
010900
011000 DO-THE-PICK.
011100     IF MENU-PICK = 1
011200         PERFORM ADD-MODE
011300     ELSE
011400     IF MENU-PICK = 2
011500         PERFORM CHANGE-MODE
011600     ELSE
011700     IF MENU-PICK = 3
011800         PERFORM INQUIRE-MODE
011900     ELSE
012000     IF MENU-PICK = 4
012100         PERFORM DELETE-MODE
012200     ELSE
012300     IF MENU-PICK = 5
012400         PERFORM INQUIRE-BY-NAME
012500     ELSE
012600     IF MENU-PICK = 6
012700         PERFORM PRINT-VENDOR-REPORT
012800     ELSE
012900     IF MENU-PICK = 7
013000         PERFORM PRINT-BY-NAME.
013100
013200*---------------------------------
013300* ADD
013400*---------------------------------
013500 ADD-MODE.
013600     MOVE "ADD" TO THE-MODE.
013700     PERFORM GET-NEW-RECORD-KEY.
013800     PERFORM ADD-RECORDS
013900        UNTIL VENDOR-NUMBER = ZEROES.
014000
014100 GET-NEW-RECORD-KEY.
014200     PERFORM ACCEPT-NEW-RECORD-KEY.
014300     PERFORM RE-ACCEPT-NEW-RECORD-KEY
014400         UNTIL VENDOR-RECORD-FOUND = "N" OR
014500               VENDOR-NUMBER = ZEROES.
014600
014700 ACCEPT-NEW-RECORD-KEY.
014800     PERFORM INIT-VENDOR-RECORD.
014900     PERFORM ENTER-VENDOR-NUMBER.
015000     IF VENDOR-NUMBER NOT = ZEROES
015100         PERFORM READ-VENDOR-RECORD.
015200
015300 RE-ACCEPT-NEW-RECORD-KEY.
015400     DISPLAY "RECORD ALREADY ON FILE"
015500     PERFORM ACCEPT-NEW-RECORD-KEY.
015600
015700 ADD-RECORDS.
015800     PERFORM ENTER-REMAINING-FIELDS.
015900     PERFORM WRITE-VENDOR-RECORD.
016000     PERFORM GET-NEW-RECORD-KEY.
016100
016200 ENTER-REMAINING-FIELDS.
016300     PERFORM ENTER-VENDOR-NAME.
016400     PERFORM ENTER-VENDOR-ADDRESS-1.
016500     PERFORM ENTER-VENDOR-ADDRESS-2.
016600     PERFORM ENTER-VENDOR-CITY.
016700     PERFORM ENTER-VENDOR-STATE.
016800     PERFORM ENTER-VENDOR-ZIP.
016900     PERFORM ENTER-VENDOR-CONTACT.
017000     PERFORM ENTER-VENDOR-PHONE.
017100
017200*---------------------------------
017300* CHANGE
017400*---------------------------------
017500 CHANGE-MODE.
017600     MOVE "CHANGE" TO THE-MODE.
017700     PERFORM GET-EXISTING-RECORD.
017800     PERFORM CHANGE-RECORDS
017900        UNTIL VENDOR-NUMBER = ZEROES.
018000
018100 CHANGE-RECORDS.
018200     PERFORM GET-FIELD-TO-CHANGE.
018300     PERFORM CHANGE-ONE-FIELD
018400         UNTIL WHICH-FIELD = ZERO.
018500     PERFORM GET-EXISTING-RECORD.
018600
018700 GET-FIELD-TO-CHANGE.
018800     PERFORM DISPLAY-ALL-FIELDS.
018900     PERFORM ASK-WHICH-FIELD.
019000
019100 ASK-WHICH-FIELD.
019200     PERFORM ACCEPT-WHICH-FIELD.
019300     PERFORM RE-ACCEPT-WHICH-FIELD
019400         UNTIL WHICH-FIELD < 9.
019500
019600 ACCEPT-WHICH-FIELD.
019700     DISPLAY "ENTER THE NUMBER OF THE FIELD".
019800     DISPLAY "TO CHANGE (1-8) OR 0 TO EXIT".
019900     ACCEPT WHICH-FIELD.
020000
020100 RE-ACCEPT-WHICH-FIELD.
020200     DISPLAY "INVALID ENTRY".
020300     PERFORM ACCEPT-WHICH-FIELD.
020400
020500 CHANGE-ONE-FIELD.
020600     PERFORM CHANGE-THIS-FIELD.
020700     PERFORM GET-FIELD-TO-CHANGE.
020800
020900 CHANGE-THIS-FIELD.
021000     IF WHICH-FIELD = 1
021100         PERFORM ENTER-VENDOR-NAME.
021200     IF WHICH-FIELD = 2
021300         PERFORM ENTER-VENDOR-ADDRESS-1.
021400     IF WHICH-FIELD = 3
021500         PERFORM ENTER-VENDOR-ADDRESS-2.
021600     IF WHICH-FIELD = 4
021700         PERFORM ENTER-VENDOR-CITY.
021800     IF WHICH-FIELD = 5
021900         PERFORM ENTER-VENDOR-STATE.
022000     IF WHICH-FIELD = 6
022100         PERFORM ENTER-VENDOR-ZIP.
022200     IF WHICH-FIELD = 7
022300         PERFORM ENTER-VENDOR-CONTACT.
022400     IF WHICH-FIELD = 8
022500         PERFORM ENTER-VENDOR-PHONE.
022600
022700     PERFORM REWRITE-VENDOR-RECORD.
022800
022900*---------------------------------
023000* INQUIRE
023100*---------------------------------
023200 INQUIRE-MODE.
023300     MOVE "DISPLAY" TO THE-MODE.
023400     PERFORM GET-EXISTING-RECORD.
023500     PERFORM INQUIRE-RECORDS
023600        UNTIL VENDOR-NUMBER = ZEROES.
023700
023800 INQUIRE-RECORDS.
023900     PERFORM DISPLAY-ALL-FIELDS.
024000     PERFORM GET-EXISTING-RECORD.
024100
024200*---------------------------------
024300* DELETE
024400*---------------------------------
024500 DELETE-MODE.
024600     MOVE "DELETE" TO THE-MODE.
024700     PERFORM GET-EXISTING-RECORD.
024800     PERFORM DELETE-RECORDS
024900        UNTIL VENDOR-NUMBER = ZEROES.
025000
025100 DELETE-RECORDS.
025200     PERFORM DISPLAY-ALL-FIELDS.
025300
025400     PERFORM ASK-OK-TO-DELETE.
025500
025600     IF OK-TO-DELETE = "Y"
025700         PERFORM DELETE-VENDOR-RECORD.
025800
025900     PERFORM GET-EXISTING-RECORD.
026000
026100 ASK-OK-TO-DELETE.
026200     PERFORM ACCEPT-OK-TO-DELETE.
026300
026400     PERFORM RE-ACCEPT-OK-TO-DELETE
026500        UNTIL OK-TO-DELETE = "Y" OR "N".
026600
026700 ACCEPT-OK-TO-DELETE.
026800     DISPLAY "DELETE THIS RECORD (Y/N)?".
026900     ACCEPT OK-TO-DELETE.
027000     INSPECT OK-TO-DELETE
027100      CONVERTING LOWER-ALPHA TO UPPER-ALPHA.
027200
027300 RE-ACCEPT-OK-TO-DELETE.
027400     DISPLAY "YOU MUST ENTER YES OR NO".
027500     PERFORM ACCEPT-OK-TO-DELETE.
027600
027700*---------------------------------
027800* Routines shared by all modes
027900*---------------------------------
028000 INIT-VENDOR-RECORD.
028100     MOVE SPACE TO VENDOR-RECORD.
028200     MOVE ZEROES TO VENDOR-NUMBER.
028300
028400 ENTER-VENDOR-NUMBER.
028500     DISPLAY " ".
028600     DISPLAY "ENTER VENDOR NUMBER OF THE VENDOR" .
028700     DISPLAY "TO " THE-MODE " (1-99999)".
028800     DISPLAY "ENTER 0 TO STOP ENTRY".
028900     ACCEPT VENDOR-NUMBER-FIELD.
029000*OR  ACCEPT VENDOR-NUMBER-FIELD WITH CONVERSION.
029100
029200     MOVE VENDOR-NUMBER-FIELD TO VENDOR-NUMBER.
029300
029400*---------------------------------
029500* INQUIRE BY NAME
029600*---------------------------------
029700 INQUIRE-BY-NAME.
029800     PERFORM CLOSING-PROCEDURE.
029900     CALL "VNINNM03".
030000     PERFORM OPENING-PROCEDURE.
030100
030200*---------------------------------
030300* PRINT
030400*---------------------------------
030500 PRINT-VENDOR-REPORT.
030600     PERFORM CLOSING-PROCEDURE.
030700     DISPLAY "VENDOR REPORT IN PROGRESS".
030800     CALL "VNDRPT04".
030900     PERFORM OPENING-PROCEDURE.
031000
031100*---------------------------------
031200* PRINT BY NAME
031300*---------------------------------
031400 PRINT-BY-NAME.
031500     PERFORM CLOSING-PROCEDURE.
031600     DISPLAY " REPORT BY NAME IN PROGRESS".
031700     CALL "VNBYNM02".
031800     PERFORM OPENING-PROCEDURE.
031900
032000*---------------------------------
032100* Routines shared Add and Change
032200*---------------------------------
032300 ENTER-VENDOR-NAME.
032400     PERFORM ACCEPT-VENDOR-NAME.
032500     PERFORM RE-ACCEPT-VENDOR-NAME
032600         UNTIL VENDOR-NAME NOT = SPACE.
032700
032800 ACCEPT-VENDOR-NAME.
032900     DISPLAY "ENTER VENDOR NAME".
033000     ACCEPT VENDOR-NAME.
033100     INSPECT VENDOR-NAME
033200         CONVERTING LOWER-ALPHA
033300         TO         UPPER-ALPHA.
033400
033500 RE-ACCEPT-VENDOR-NAME.
033600     DISPLAY "VENDOR NAME MUST BE ENTERED".
033700     PERFORM ACCEPT-VENDOR-NAME.
033800
033900 ENTER-VENDOR-ADDRESS-1.
034000     PERFORM ACCEPT-VENDOR-ADDRESS-1.
034100     PERFORM RE-ACCEPT-VENDOR-ADDRESS-1
034200         UNTIL VENDOR-ADDRESS-1 NOT = SPACE.
034300
034400 ACCEPT-VENDOR-ADDRESS-1.
034500     DISPLAY "ENTER VENDOR ADDRESS-1".
034600     ACCEPT VENDOR-ADDRESS-1.
034700     INSPECT VENDOR-ADDRESS-1
034800         CONVERTING LOWER-ALPHA
034900         TO         UPPER-ALPHA.
035000
035100 RE-ACCEPT-VENDOR-ADDRESS-1.
035200     DISPLAY "VENDOR ADDRESS-1 MUST BE ENTERED".
035300     PERFORM ACCEPT-VENDOR-ADDRESS-1.
035400
035500 ENTER-VENDOR-ADDRESS-2.
035600     DISPLAY "ENTER VENDOR ADDRESS-2".
035700     ACCEPT VENDOR-ADDRESS-2.
035800     INSPECT VENDOR-ADDRESS-2
035900         CONVERTING LOWER-ALPHA
036000         TO         UPPER-ALPHA.
036100
036200 ENTER-VENDOR-CITY.
036300     PERFORM ACCEPT-VENDOR-CITY.
036400     PERFORM RE-ACCEPT-VENDOR-CITY
036500         UNTIL VENDOR-CITY NOT = SPACE.
036600
036700 ACCEPT-VENDOR-CITY.
036800     DISPLAY "ENTER VENDOR CITY".
036900     ACCEPT VENDOR-CITY.
037000     INSPECT VENDOR-CITY
037100         CONVERTING LOWER-ALPHA
037200         TO         UPPER-ALPHA.
037300
037400 RE-ACCEPT-VENDOR-CITY.
037500     DISPLAY "VENDOR CITY MUST BE ENTERED".
037600     PERFORM ACCEPT-VENDOR-CITY.
037700
037800 ENTER-VENDOR-STATE.
037900     PERFORM ACCEPT-VENDOR-STATE.
038000     PERFORM RE-ACCEPT-VENDOR-STATE
038100         UNTIL VENDOR-STATE NOT = SPACES AND
038200               STATE-RECORD-FOUND = "Y".
038300
038400 ACCEPT-VENDOR-STATE.
038500     DISPLAY "ENTER VENDOR STATE".
038600     ACCEPT VENDOR-STATE.
038700     PERFORM EDIT-CHECK-VENDOR-STATE.
038800
038900 RE-ACCEPT-VENDOR-STATE.
039000     DISPLAY ERROR-MESSAGE.
039100     PERFORM ACCEPT-VENDOR-STATE.
039200
039300 EDIT-CHECK-VENDOR-STATE.
039400     PERFORM EDIT-VENDOR-STATE.
039500     PERFORM CHECK-VENDOR-STATE.
039600
039700 EDIT-VENDOR-STATE.
039800     INSPECT VENDOR-STATE
039900         CONVERTING LOWER-ALPHA
040000         TO         UPPER-ALPHA.
040100
040200 CHECK-VENDOR-STATE.
040300     PERFORM VENDOR-STATE-REQUIRED.
040400     IF VENDOR-STATE NOT = SPACES
040500         PERFORM VENDOR-STATE-ON-FILE.
040600
040700 VENDOR-STATE-REQUIRED.
040800     IF VENDOR-STATE = SPACE
040900         MOVE "VENDOR STATE MUST BE ENTERED"
041000           TO ERROR-MESSAGE.
041100
041200 VENDOR-STATE-ON-FILE.
041300     MOVE VENDOR-STATE TO STATE-CODE.
041400     PERFORM READ-STATE-RECORD.
041500     IF STATE-RECORD-FOUND = "N"
041600         MOVE "STATE CODE NOT FOUND IN CODES FILE"
041700           TO ERROR-MESSAGE.
041800
041900 ENTER-VENDOR-ZIP.
042000     PERFORM ACCEPT-VENDOR-ZIP.
042100     PERFORM RE-ACCEPT-VENDOR-ZIP
042200         UNTIL VENDOR-ZIP NOT = SPACE.
042300
042400 ACCEPT-VENDOR-ZIP.
042500     DISPLAY "ENTER VENDOR ZIP".
042600     ACCEPT VENDOR-ZIP.
042700     INSPECT VENDOR-ZIP
042800         CONVERTING LOWER-ALPHA
042900         TO         UPPER-ALPHA.
043000
043100 RE-ACCEPT-VENDOR-ZIP.
043200     DISPLAY "VENDOR ZIP MUST BE ENTERED".
043300     PERFORM ACCEPT-VENDOR-ZIP.
043400
043500 ENTER-VENDOR-CONTACT.
043600     DISPLAY "ENTER VENDOR CONTACT".
043700     ACCEPT VENDOR-CONTACT.
043800     INSPECT VENDOR-CONTACT
043900         CONVERTING LOWER-ALPHA
044000         TO         UPPER-ALPHA.
044100
044200 ENTER-VENDOR-PHONE.
044300     PERFORM ACCEPT-VENDOR-PHONE.
044400     PERFORM RE-ACCEPT-VENDOR-PHONE
044500         UNTIL VENDOR-PHONE NOT = SPACE.
044600
044700 ACCEPT-VENDOR-PHONE.
044800     DISPLAY "ENTER VENDOR PHONE".
044900     ACCEPT VENDOR-PHONE.
045000     INSPECT VENDOR-PHONE
045100         CONVERTING LOWER-ALPHA
045200         TO         UPPER-ALPHA.
045300
045400 RE-ACCEPT-VENDOR-PHONE.
045500     DISPLAY "VENDOR PHONE MUST BE ENTERED".
045600     PERFORM ACCEPT-VENDOR-PHONE.
045700
045800*---------------------------------
045900* Routines shared by Change,
046000* Inquire and Delete
046100*---------------------------------
046200 GET-EXISTING-RECORD.
046300     PERFORM ACCEPT-EXISTING-KEY.
046400     PERFORM RE-ACCEPT-EXISTING-KEY
046500         UNTIL VENDOR-RECORD-FOUND = "Y" OR
046600               VENDOR-NUMBER = ZEROES.
046700
046800 ACCEPT-EXISTING-KEY.
046900     PERFORM INIT-VENDOR-RECORD.
047000     PERFORM ENTER-VENDOR-NUMBER.
047100     IF VENDOR-NUMBER NOT = ZEROES
047200         PERFORM READ-VENDOR-RECORD.
047300
047400 RE-ACCEPT-EXISTING-KEY.
047500     DISPLAY "RECORD NOT FOUND"
047600     PERFORM ACCEPT-EXISTING-KEY.
047700
047800 DISPLAY-ALL-FIELDS.
047900     DISPLAY " ".
048000     PERFORM DISPLAY-VENDOR-NUMBER.
048100     PERFORM DISPLAY-VENDOR-NAME.
048200     PERFORM DISPLAY-VENDOR-ADDRESS-1.
048300     PERFORM DISPLAY-VENDOR-ADDRESS-2.
048400     PERFORM DISPLAY-VENDOR-CITY.
048500     PERFORM DISPLAY-VENDOR-STATE.
048600     PERFORM DISPLAY-VENDOR-ZIP.
048700     PERFORM DISPLAY-VENDOR-CONTACT.
048800     PERFORM DISPLAY-VENDOR-PHONE.
048900     DISPLAY " ".
049000
049100 DISPLAY-VENDOR-NUMBER.
049200     DISPLAY "   VENDOR NUMBER: " VENDOR-NUMBER.
049300
049400 DISPLAY-VENDOR-NAME.
049500     DISPLAY "1. VENDOR NAME: " VENDOR-NAME.
049600
049700 DISPLAY-VENDOR-ADDRESS-1.
049800     DISPLAY "2. VENDOR ADDRESS-1: " VENDOR-ADDRESS-1.
049900
050000 DISPLAY-VENDOR-ADDRESS-2.
050100     DISPLAY "3. VENDOR ADDRESS-2: " VENDOR-ADDRESS-2.
050200
050300 DISPLAY-VENDOR-CITY.
050400     DISPLAY "4. VENDOR CITY: " VENDOR-CITY.
050500
050600 DISPLAY-VENDOR-STATE.
050700     PERFORM VENDOR-STATE-ON-FILE.
050800     IF STATE-RECORD-FOUND = "N"
050900         MOVE "**Not found**" TO STATE-NAME.
051000     DISPLAY "5. VENDOR STATE: "
051100             VENDOR-STATE " "
051200             STATE-NAME.
051300
051400 DISPLAY-VENDOR-ZIP.
051500     DISPLAY "6. VENDOR ZIP: " VENDOR-ZIP.
051600
051700 DISPLAY-VENDOR-CONTACT.
051800     DISPLAY "7. VENDOR CONTACT: " VENDOR-CONTACT.
051900
052000 DISPLAY-VENDOR-PHONE.
052100     DISPLAY "8. VENDOR PHONE: " VENDOR-PHONE.
052200
052300*---------------------------------
052400* File I-O Routines
052500*---------------------------------
052600 READ-VENDOR-RECORD.
052700     MOVE "Y" TO VENDOR-RECORD-FOUND.
052800     READ VENDOR-FILE RECORD
052900       INVALID KEY
053000          MOVE "N" TO VENDOR-RECORD-FOUND.
053100
053200*or  READ VENDOR-FILE RECORD WITH LOCK
053300*      INVALID KEY
053400*         MOVE "N" TO VENDOR-RECORD-FOUND.
053500
053600*or  READ VENDOR-FILE RECORD WITH HOLD
053700*      INVALID KEY
053800*         MOVE "N" TO VENDOR-RECORD-FOUND.
053900
054000 WRITE-VENDOR-RECORD.
054100     WRITE VENDOR-RECORD
054200         INVALID KEY
054300         DISPLAY "RECORD ALREADY ON FILE".
054400
054500 REWRITE-VENDOR-RECORD.
054600     REWRITE VENDOR-RECORD
054700         INVALID KEY
054800         DISPLAY "ERROR REWRITING VENDOR RECORD".
054900
055000 DELETE-VENDOR-RECORD.
055100     DELETE VENDOR-FILE RECORD
055200         INVALID KEY
055300         DISPLAY "ERROR DELETING VENDOR RECORD".
055400
055500 READ-STATE-RECORD.
055600     MOVE "Y" TO STATE-RECORD-FOUND.
055700     READ STATE-FILE RECORD
055800       INVALID KEY
055900          MOVE "N" TO STATE-RECORD-FOUND.
056000
