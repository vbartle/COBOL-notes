000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNINNM01.
000300*---------------------------------
000400* Inquire for the Vendor File
000500* using vendor name.
000600*---------------------------------
000700 ENVIRONMENT DIVISION.
000800 INPUT-OUTPUT SECTION.
000900 FILE-CONTROL.
001000
001100     COPY "SLVND02.CBL".
001200
001300     COPY "SLSTATE.CBL".
001400
001500 DATA DIVISION.
001600 FILE SECTION.
001700
001800     COPY "FDVND04.CBL".
001900
002000     COPY "FDSTATE.CBL".
002100
002200 WORKING-STORAGE SECTION.
002300
002400 77  VENDOR-FILE-AT-END           PIC X.
002500 77  STATE-RECORD-FOUND           PIC X.
002600
002700
002800 77  VENDOR-NAME-FIELD            PIC X(30).
002900
003000     COPY "WSCASE01.CBL".
003100
003200 PROCEDURE DIVISION.
003300 PROGRAM-BEGIN.
003400     PERFORM OPENING-PROCEDURE.
003500     PERFORM MAIN-PROCESS.
003600     PERFORM CLOSING-PROCEDURE.
003700
003800 PROGRAM-DONE.
003900     STOP RUN.
004000
004100 OPENING-PROCEDURE.
004200     OPEN I-O VENDOR-FILE.
004300     OPEN I-O STATE-FILE.
004400
004500 CLOSING-PROCEDURE.
004600     CLOSE VENDOR-FILE.
004700     CLOSE STATE-FILE.
004800
004900 MAIN-PROCESS.
005000     PERFORM INQUIRE-BY-NAME.
005100*---------------------------------
005200* INQUIRE
005300*---------------------------------
005400 INQUIRE-BY-NAME.
005500     PERFORM GET-EXISTING-RECORD.
005600     PERFORM INQUIRE-RECORDS
005700        UNTIL VENDOR-NAME = SPACES.
005800
005900 INQUIRE-RECORDS.
006000     PERFORM DISPLAY-ALL-FIELDS.
006100     PERFORM GET-EXISTING-RECORD.
006200
006300*---------------------------------
006400* Locate a record logic
006500*---------------------------------
006600 GET-EXISTING-RECORD.
006700     PERFORM ACCEPT-EXISTING-KEY.
006800     PERFORM RE-ACCEPT-EXISTING-KEY
006900         UNTIL VENDOR-FILE-AT-END NOT = "Y".
007000
007100 ACCEPT-EXISTING-KEY.
007200     PERFORM INIT-FOR-KEY-ENTRY.
007300     PERFORM ENTER-VENDOR-NAME.
007400     IF VENDOR-NAME NOT = SPACES
007500         PERFORM READ-FIRST-VENDOR-RECORD.
007600
007700 RE-ACCEPT-EXISTING-KEY.
007800     DISPLAY "RECORD NOT FOUND"
007900     PERFORM ACCEPT-EXISTING-KEY.
008000
008100*---------------------------------
008200* Field Entry logic
008300*---------------------------------
008400 ENTER-VENDOR-NAME.
008500     PERFORM ACCEPT-VENDOR-NAME.
008600
008700 ACCEPT-VENDOR-NAME.
008800     DISPLAY "ENTER VENDOR NAME".
008900     ACCEPT VENDOR-NAME.
009000     INSPECT VENDOR-NAME
009100         CONVERTING LOWER-ALPHA
009200         TO         UPPER-ALPHA.
009300
009400*---------------------------------
009500* Display logic
009600*---------------------------------
009700 DISPLAY-ALL-FIELDS.
009800     DISPLAY " ".
009900     PERFORM DISPLAY-VENDOR-NUMBER.
010000     PERFORM DISPLAY-VENDOR-NAME.
010100     PERFORM DISPLAY-VENDOR-ADDRESS-1.
010200     PERFORM DISPLAY-VENDOR-ADDRESS-2.
010300     PERFORM DISPLAY-VENDOR-CITY.
010400     PERFORM DISPLAY-VENDOR-STATE.
010500     PERFORM DISPLAY-VENDOR-ZIP.
010600     PERFORM DISPLAY-VENDOR-CONTACT.
010700     PERFORM DISPLAY-VENDOR-PHONE.
010800     DISPLAY " ".
010900
011000 DISPLAY-VENDOR-NUMBER.
011100     DISPLAY "   VENDOR NUMBER: " VENDOR-NUMBER.
011200
011300 DISPLAY-VENDOR-NAME.
011400     DISPLAY "1. VENDOR NAME: " VENDOR-NAME.
011500
011600 DISPLAY-VENDOR-ADDRESS-1.
011700     DISPLAY "2. VENDOR ADDRESS-1: " VENDOR-ADDRESS-1.
011800
011900 DISPLAY-VENDOR-ADDRESS-2.
012000     DISPLAY "3. VENDOR ADDRESS-2: " VENDOR-ADDRESS-2.
012100
012200 DISPLAY-VENDOR-CITY.
012300     DISPLAY "4. VENDOR CITY: " VENDOR-CITY.
012400
012500 DISPLAY-VENDOR-STATE.
012600     MOVE VENDOR-STATE TO STATE-CODE.
012700     PERFORM READ-STATE-RECORD.
012800     IF STATE-RECORD-FOUND = "N"
012900         MOVE "**Not found**" TO STATE-NAME.
013000     DISPLAY "5. VENDOR STATE: "
013100             VENDOR-STATE " "
013200             STATE-NAME.
013300
013400 DISPLAY-VENDOR-ZIP.
013500     DISPLAY "6. VENDOR ZIP: " VENDOR-ZIP.
013600
013700 DISPLAY-VENDOR-CONTACT.
013800     DISPLAY "7. VENDOR CONTACT: " VENDOR-CONTACT.
013900
014000 DISPLAY-VENDOR-PHONE.
014100     DISPLAY "8. VENDOR PHONE: " VENDOR-PHONE.
014200
014300*---------------------------------
014400* File Related Routines
014500*---------------------------------
014600 INIT-FOR-KEY-ENTRY.
014700     MOVE SPACE TO VENDOR-RECORD.
014800     MOVE ZEROES TO VENDOR-NUMBER.
014900     MOVE "N" TO VENDOR-FILE-AT-END.
015000
015100 READ-FIRST-VENDOR-RECORD.
015200     MOVE "N" TO VENDOR-FILE-AT-END.
015300     START VENDOR-FILE
015400        KEY NOT < VENDOR-NAME
015500         INVALID KEY
015600          MOVE "Y" TO VENDOR-FILE-AT-END.
015700
015800     IF VENDOR-FILE-AT-END NOT = "Y"
015900         PERFORM READ-NEXT-VENDOR-RECORD.
016000
016100 READ-NEXT-VENDOR-RECORD.
016200     READ VENDOR-FILE NEXT RECORD
016300       AT END
016400          MOVE "Y" TO VENDOR-FILE-AT-END.
016500
016600 READ-STATE-RECORD.
016700     MOVE "Y" TO STATE-RECORD-FOUND.
016800     READ STATE-FILE RECORD
016900       INVALID KEY
017000          MOVE "N" TO STATE-RECORD-FOUND.
017100
