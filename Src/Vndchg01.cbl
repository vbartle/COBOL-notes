000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDCHG01.
000300*---------------------------------
000400* Change records in Vendor File.
000500*---------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     COPY "SLVND01.CBL".
001100
001200 DATA DIVISION.
001300 FILE SECTION.
001400
001500     COPY "FDVND02.CBL".
001600
001700 WORKING-STORAGE SECTION.
001800
001900 77  WHICH-FIELD         PIC 9.
002000 77  RECORD-FOUND        PIC X.
002100
002200 77  VENDOR-NUMBER-FIELD PIC Z(5).
002300
002400 PROCEDURE DIVISION.
002500 PROGRAM-BEGIN.
002600     OPEN I-O VENDOR-FILE.
002700     PERFORM GET-VENDOR-RECORD.
002800     PERFORM CHANGE-RECORDS
002900        UNTIL VENDOR-NUMBER = ZEROES.
003000     CLOSE VENDOR-FILE.
003100
003200 PROGRAM-DONE.
003300     STOP RUN.
003400
003500*---------------------------------
003600* TO GET A VENDOR RECORD, ASK FOR
003700* VENDOR NUMBER, AND THEN TRY TO
003800* READ THE RECORD.
003900*---------------------------------
004000 GET-VENDOR-RECORD.
004100     PERFORM INIT-VENDOR-RECORD.
004200     PERFORM ENTER-VENDOR-NUMBER.
004300     MOVE "N" TO RECORD-FOUND.
004400     PERFORM FIND-VENDOR-RECORD
004500         UNTIL RECORD-FOUND = "Y" OR
004600               VENDOR-NUMBER = ZEROES.
004700
004800 INIT-VENDOR-RECORD.
004900     MOVE SPACE TO VENDOR-RECORD.
005000     MOVE ZEROES TO VENDOR-NUMBER.
005100
005200 ENTER-VENDOR-NUMBER.
005300     DISPLAY " ".
005400     DISPLAY "ENTER VENDOR NUMBER OF THE VENDOR" .
005500     DISPLAY "TO CHANGE (1-99999)".
005600     DISPLAY "ENTER 0 TO STOP ENTRY".
005700     ACCEPT VENDOR-NUMBER-FIELD.
005800*OR  ACCEPT VENDOR-NUMBER-FIELD WITH CONVERSION.
005900
006000     MOVE VENDOR-NUMBER-FIELD TO VENDOR-NUMBER.
006100
006200 FIND-VENDOR-RECORD.
006300     PERFORM READ-VENDOR-RECORD.
006400     IF RECORD-FOUND = "N"
006500         DISPLAY "RECORD NOT FOUND"
006600         PERFORM ENTER-VENDOR-NUMBER.
006700
006800 READ-VENDOR-RECORD.
006900     MOVE "Y" TO RECORD-FOUND.
007000     READ VENDOR-FILE RECORD
007100       INVALID KEY
007200          MOVE "N" TO RECORD-FOUND.
007300
007400*or  READ VENDOR-FILE RECORD WITH LOCK
007500*      INVALID KEY
007600*         MOVE "N" TO RECORD-FOUND.
007700
007800*or  READ VENDOR-FILE RECORD WITH HOLD
007900*      INVALID KEY
008000*         MOVE "N" TO RECORD-FOUND.
008100
008200 CHANGE-RECORDS.
008300     PERFORM GET-FIELD-TO-CHANGE.
008400     PERFORM CHANGE-ONE-FIELD
008500         UNTIL WHICH-FIELD = ZERO.
008600     PERFORM GET-VENDOR-RECORD.
008700
008800*---------------------------------
008900* DISPLAY ALL FIELDS, ASK THE USER
009000* WHICH TO CHANGE.
009100*---------------------------------
009200 GET-FIELD-TO-CHANGE.
009300     PERFORM DISPLAY-ALL-FIELDS.
009400     PERFORM ASK-WHICH-FIELD.
009500
009600*---------------------------------
009700* DISPLAY ALL FIELDS WITH BLANK
009800* LINES ABOVE AND BELOW.
009900*---------------------------------
010000 DISPLAY-ALL-FIELDS.
010100     DISPLAY " ".
010200     PERFORM DISPLAY-VENDOR-NUMBER.
010300     PERFORM DISPLAY-VENDOR-NAME.
010400     PERFORM DISPLAY-VENDOR-ADDRESS-1.
010500     PERFORM DISPLAY-VENDOR-ADDRESS-2.
010600     PERFORM DISPLAY-VENDOR-CITY.
010700     PERFORM DISPLAY-VENDOR-STATE.
010800     PERFORM DISPLAY-VENDOR-ZIP.
010900     PERFORM DISPLAY-VENDOR-CONTACT.
011000     PERFORM DISPLAY-VENDOR-PHONE.
011100     DISPLAY " ".
011200
011300 DISPLAY-VENDOR-NUMBER.
011400     DISPLAY "   VENDOR NUMBER: " VENDOR-NUMBER.
011500
011600 DISPLAY-VENDOR-NAME.
011700     DISPLAY "1. VENDOR NAME: " VENDOR-NAME.
011800
011900 DISPLAY-VENDOR-ADDRESS-1.
012000     DISPLAY "2. VENDOR ADDRESS-1: " VENDOR-ADDRESS-1.
012100
012200 DISPLAY-VENDOR-ADDRESS-2.
012300     DISPLAY "3. VENDOR ADDRESS-2: " VENDOR-ADDRESS-2.
012400
012500 DISPLAY-VENDOR-CITY.
012600     DISPLAY "4. VENDOR CITY: " VENDOR-CITY.
012700
012800 DISPLAY-VENDOR-STATE.
012900     DISPLAY "5. VENDOR STATE: " VENDOR-STATE.
013000
013100 DISPLAY-VENDOR-ZIP.
013200     DISPLAY "6. VENDOR ZIP: " VENDOR-ZIP.
013300
013400 DISPLAY-VENDOR-CONTACT.
013500     DISPLAY "7. VENDOR CONTACT: " VENDOR-CONTACT.
013600
013700 DISPLAY-VENDOR-PHONE.
013800     DISPLAY "8. VENDOR PHONE: " VENDOR-PHONE.
013900
014000 ASK-WHICH-FIELD.
014100     DISPLAY "ENTER THE NUMBER OF THE FIELD".
014200     DISPLAY "TO CHANGE (1-8) OR 0 TO EXIT".
014300     ACCEPT WHICH-FIELD.
014400     IF WHICH-FIELD > 8
014500         DISPLAY "INVALID ENTRY".
014600
014700*---------------------------------
014800* GET DATA ENTRY FOR THE NEW FIELD.
014900* THEN ASK FOR THE NEXT FIELD
015000* TO CHANGE.
015100* CONTINUE UNTIL USER ENTERS 0.
015200*---------------------------------
015300 CHANGE-ONE-FIELD.
015400     PERFORM CHANGE-THIS-FIELD.
015500     PERFORM GET-FIELD-TO-CHANGE.
015600
015700*---------------------------------
015800* ACCEPT DATA ENTRY FOR THE FIELD
015900* SPECIFIED, AND THEN REWRITE THE
016000* RECORD.
016100*---------------------------------
016200 CHANGE-THIS-FIELD.
016300     IF WHICH-FIELD = 1
016400         PERFORM ENTER-VENDOR-NAME.
016500     IF WHICH-FIELD = 2
016600         PERFORM ENTER-VENDOR-ADDRESS-1.
016700     IF WHICH-FIELD = 3
016800         PERFORM ENTER-VENDOR-ADDRESS-2.
016900     IF WHICH-FIELD = 4
017000         PERFORM ENTER-VENDOR-CITY.
017100     IF WHICH-FIELD = 5
017200         PERFORM ENTER-VENDOR-STATE.
017300     IF WHICH-FIELD = 6
017400         PERFORM ENTER-VENDOR-ZIP.
017500     IF WHICH-FIELD = 7
017600         PERFORM ENTER-VENDOR-CONTACT.
017700     IF WHICH-FIELD = 8
017800         PERFORM ENTER-VENDOR-PHONE.
017900
018000     PERFORM REWRITE-VENDOR-RECORD.
018100
018200 ENTER-VENDOR-NAME.
018300     DISPLAY "ENTER VENDOR NAME".
018400     ACCEPT VENDOR-NAME.
018500
018600 ENTER-VENDOR-ADDRESS-1.
018700     DISPLAY "ENTER VENDOR ADDRESS-1".
018800     ACCEPT VENDOR-ADDRESS-1.
018900
019000 ENTER-VENDOR-ADDRESS-2.
019100     DISPLAY "ENTER VENDOR ADDRESS-2".
019200     ACCEPT VENDOR-ADDRESS-2.
019300
019400 ENTER-VENDOR-CITY.
019500     DISPLAY "ENTER VENDOR CITY".
019600     ACCEPT VENDOR-CITY.
019700
019800 ENTER-VENDOR-STATE.
019900     DISPLAY "ENTER VENDOR STATE".
020000     ACCEPT VENDOR-STATE.
020100
020200 ENTER-VENDOR-ZIP.
020300     DISPLAY "ENTER VENDOR ZIP".
020400     ACCEPT VENDOR-ZIP.
020500
020600 ENTER-VENDOR-CONTACT.
020700     DISPLAY "ENTER VENDOR CONTACT".
020800     ACCEPT VENDOR-CONTACT.
020900
021000 ENTER-VENDOR-PHONE.
021100     DISPLAY "ENTER VENDOR PHONE".
021200     ACCEPT VENDOR-PHONE.
021300
021400 REWRITE-VENDOR-RECORD.
021500     REWRITE VENDOR-RECORD
021600         INVALID KEY
021700         DISPLAY "ERROR REWRITING VENDOR RECORD".
021800
