000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. SLSRPT06.
000300*---------------------------------
000400* Print test sales data
000500* Uses qualified data names.
000600*---------------------------------
000700 ENVIRONMENT DIVISION.
000800 INPUT-OUTPUT SECTION.
000900 FILE-CONTROL.
001000
001100*---------------------------------
001200* SLSALES.CBL
001300*---------------------------------
001400     SELECT SALES-FILE
001500         ASSIGN TO "SALES"
001600         ORGANIZATION IS SEQUENTIAL.
001700
001800     SELECT WORK-FILE
001900         ASSIGN TO "WORK"
002000         ORGANIZATION IS SEQUENTIAL.
002100
002200     SELECT SORT-FILE
002300         ASSIGN TO "SORT".
002400
002500     SELECT PRINTER-FILE
002600         ASSIGN TO PRINTER
002700         ORGANIZATION IS LINE SEQUENTIAL.
002800
002900 DATA DIVISION.
003000 FILE SECTION.
003100
003200*---------------------------------
003300* FDSALES.CBL
003400* Temporary daily sales file.
003500*---------------------------------
003600 FD  SALES-FILE
003700     LABEL RECORDS ARE STANDARD.
003800 01  SALES-RECORD.
003900     05  SALES-STORE              PIC 9(2).
004000     05  SALES-DIVISION           PIC 9(2).
004100     05  SALES-DEPARTMENT         PIC 9(2).
004200     05  SALES-CATEGORY           PIC 9(2).
004300     05  SALES-AMOUNT             PIC S9(6)V99.
004400
004500 FD  WORK-FILE
004600     LABEL RECORDS ARE STANDARD.
004700 01  WORK-RECORD.
004800     05  SALES-STORE              PIC 9(2).
004900     05  SALES-DIVISION           PIC 9(2).
005000     05  SALES-DEPARTMENT         PIC 9(2).
005100     05  SALES-CATEGORY           PIC 9(2).
005200     05  SALES-AMOUNT             PIC S9(6)V99.
005300
005400 SD  SORT-FILE.
005500     
005600 01  SORT-RECORD.
005700     05  SALES-STORE              PIC 9(2).
005800     05  SALES-DIVISION           PIC 9(2).
005900     05  SALES-DEPARTMENT         PIC 9(2).
006000     05  SALES-CATEGORY           PIC 9(2).
006100     05  SALES-AMOUNT             PIC S9(6)V99.
006200
006300 FD  PRINTER-FILE
006400     LABEL RECORDS ARE OMITTED.
006500 01  PRINTER-RECORD              PIC X(80).
006600
006700 WORKING-STORAGE SECTION.
006800
006900 01  THE-DIVISIONS.
007000     05  FILLER       PIC 99 VALUE 01.
007100     05  FILLER       PIC X(15) VALUE "ATHLETICS".
007200     05  FILLER       PIC 99 VALUE 02.
007300     05  FILLER       PIC X(15) VALUE "SPORTING GOODS".
007400     05  FILLER       PIC 99 VALUE 03.
007500     05  FILLER       PIC X(15) VALUE "CAMPING".
007600 01  FILLER REDEFINES THE-DIVISIONS.
007700     05  DIVISION-TABLE OCCURS 3 TIMES
007800          INDEXED BY DIVISION-INDEX.
007900         10  DIVISION-NUMBER          PIC 99.
008000         10  DIVISION-NAME            PIC X(15).
008100
008200 01  THE-DEPARTMENTS.
008300     05  FILLER       PIC 99 VALUE 01.
008400     05  FILLER       PIC X(15) VALUE "EXERCISE".
008500     05  FILLER       PIC 99 VALUE 02.
008600     05  FILLER       PIC X(15) VALUE "MISCELLANEOUS".
008700     05  FILLER       PIC 99 VALUE 03.
008800     05  FILLER       PIC X(15) VALUE "SPORT CLOTHES".
008900     05  FILLER       PIC 99 VALUE 04.
009000     05  FILLER       PIC X(15) VALUE "EQUIPMENT".
009100     05  FILLER       PIC 99 VALUE 05.
009200     05  FILLER       PIC X(15) VALUE "CAMP EQUIPMENT".
009300     05  FILLER       PIC 99 VALUE 06.
009400     05  FILLER       PIC X(15) VALUE "CAMPING CLOTHES".
009500 01  FILLER REDEFINES THE-DEPARTMENTS.
009600     05  DEPARTMENT-TABLE OCCURS 6 TIMES
009700          INDEXED BY DEPARTMENT-INDEX.
009800         10  DEPARTMENT-NUMBER          PIC 99.
009900         10  DEPARTMENT-NAME            PIC X(15).
010000
010100 01  THE-CATEGORIES.
010200     05  FILLER       PIC 99 VALUE 01.
010300     05  FILLER       PIC X(15) VALUE "WEIGHTS".
010400     05  FILLER       PIC 99 VALUE 02.
010500     05  FILLER       PIC X(15) VALUE "MACHINES".
010600     05  FILLER       PIC 99 VALUE 03.
010700     05  FILLER       PIC X(15) VALUE "SUN GLASSES".
010800     05  FILLER       PIC 99 VALUE 04.
010900     05  FILLER       PIC X(15) VALUE "VITAMINS".
011000     05  FILLER       PIC 99 VALUE 05.
011100     05  FILLER       PIC X(15) VALUE "MEN'S CLOTHES".
011200     05  FILLER       PIC 99 VALUE 06.
011300     05  FILLER       PIC X(15) VALUE "WOMEN'S CLOTHES".
011400     05  FILLER       PIC 99 VALUE 07.
011500     05  FILLER       PIC X(15) VALUE "TENNIS".
011600     05  FILLER       PIC 99 VALUE 08.
011700     05  FILLER       PIC X(15) VALUE "SOCCER".
011800     05  FILLER       PIC 99 VALUE 09.
011900     05  FILLER       PIC X(15) VALUE "TENTS".
012000     05  FILLER       PIC 99 VALUE 10.
012100     05  FILLER       PIC X(15) VALUE "SLEEPING BAGS".
012200     05  FILLER       PIC 99 VALUE 11.
012300     05  FILLER       PIC X(15) VALUE "CLOTHING".
012400     05  FILLER       PIC 99 VALUE 12.
012500     05  FILLER       PIC X(15) VALUE "HIKING BOOTS".
012600 01  FILLER REDEFINES THE-CATEGORIES.
012700     05  CATEGORY-TABLE OCCURS 12 TIMES
012800          INDEXED BY CATEGORY-INDEX.
012900         10  CATEGORY-NUMBER          PIC 99.
013000         10  CATEGORY-NAME            PIC X(15).
013100
013200 77  OK-TO-PROCESS         PIC X.
013300
013400     COPY "WSCASE01.CBL".
013500
013600 01  LEGEND-LINE.
013700     05  FILLER            PIC X(6) VALUE "STORE:".
013800     05  FILLER            PIC X(1) VALUE SPACE.
013900     05  PRINT-STORE       PIC Z9.
014000
014100 01  DETAIL-LINE.
014200     05  FILLER               PIC X(3) VALUE SPACE.
014300     05  PRINT-DIVISION       PIC Z9.
014400     05  FILLER               PIC X(4) VALUE SPACE.
014500     05  FILLER               PIC X(3) VALUE SPACE.
014600     05  PRINT-DEPARTMENT     PIC Z9.
014700     05  FILLER               PIC X(6) VALUE SPACE.
014800     05  FILLER               PIC X(3) VALUE SPACE.
014900     05  PRINT-CATEGORY       PIC Z9.
015000     05  FILLER               PIC X(4) VALUE SPACE.
015100     05  PRINT-CATEGORY-NAME  PIC X(15).
015200     05  FILLER               PIC X(1) VALUE SPACE.
015300     05  PRINT-AMOUNT         PIC ZZZ,ZZ9.99-.
015400
015500 01  COLUMN-LINE.
015600     05  FILLER         PIC X(8)  VALUE "DIVISION".
015700     05  FILLER         PIC X(1)  VALUE SPACE.
015800     05  FILLER         PIC X(10) VALUE "DEPARTMENT".
015900     05  FILLER         PIC X(1)  VALUE SPACE.
016000     05  FILLER         PIC X(8)  VALUE "CATEGORY".
016100     05  FILLER         PIC X(1)  VALUE SPACE.
016200     05  FILLER         PIC X(15)  VALUE SPACE.
016300     05  FILLER         PIC X(5)  VALUE SPACE.
016400     05  FILLER         PIC X(6)  VALUE "AMOUNT".
016500
016600 01  TITLE-LINE.
016700     05  FILLER              PIC X(4) VALUE "RUN:".
016800     05  FORMATTED-RUN-DATE  PIC X(10).
016900     05  FILLER              PIC X(4) VALUE " AT ".
017000     05  FORMATTED-RUN-TIME  PIC X(8).
017100     05  FILLER              PIC X(10) VALUE SPACE.
017200     05  FILLER              PIC X(12)
017300         VALUE "SALES REPORT".
017400     05  FILLER              PIC X(10) VALUE SPACE.
017500     05  FILLER              PIC X(5) VALUE "PAGE:".
017600     05  FILLER              PIC X(1) VALUE SPACE.
017700     05  PRINT-PAGE-NUMBER   PIC ZZZ9.
017800
017900 01  TOTAL-LINE.
018000     05  FILLER              PIC X(11) VALUE SPACE.
018100     05  TOTAL-TYPE          PIC X(8).
018200     05  FILLER              PIC X(1) VALUE SPACE.
018300     05  TOTAL-NUMBER        PIC Z9.
018400     05  FILLER              PIC X(1) VALUE SPACE.
018500     05  TOTAL-NAME          PIC X(15) VALUE SPACE.
018600     05  FILLER              PIC X(1) VALUE SPACE.
018700     05  TOTAL-LITERAL       PIC X(5) VALUE "TOTAL".
018800     05  FILLER              PIC X(1) VALUE SPACE.
018900     05  PRINT-TOTAL         PIC ZZZ,ZZ9.99-.
019000
019100 77  GRAND-TOTAL-LITERAL      PIC X(8) VALUE "   GRAND".
019200 77  STORE-TOTAL-LITERAL      PIC X(8) VALUE "   STORE".
019300 77  DIVISION-TOTAL-LITERAL   PIC X(8) VALUE "DIVISION".
019400 77  DEPARTMENT-TOTAL-LITERAL PIC X(8) VALUE "    DEPT".
019500
019600 77  WORK-FILE-AT-END        PIC X.
019700
019800 77  LINE-COUNT              PIC 999 VALUE ZERO.
019900 77  PAGE-NUMBER             PIC 9999 VALUE ZERO.
020000 77  MAXIMUM-LINES           PIC 999 VALUE 55.
020100
020200 77  RECORD-COUNT            PIC 9999 VALUE ZEROES.
020300
020400* Control break current values for store, division
020500* department.
020600 77  CURRENT-STORE          PIC 99.
020700 77  CURRENT-DIVISION       PIC 99.
020800 77  CURRENT-DEPARTMENT     PIC 99.
020900
021000* Control break accumulators
021100* GRAND TOTAL is the level 1 accumulator for the whole file
021200* STORE TOTAL is the level 2 accumulator
021300* DIVISION TOTAL is the level 3 accumulator
021400* DEPARTMENT TOTAL is the level 4 accumulator.
021500 77  GRAND-TOTAL            PIC S9(6)V99.
021600 77  STORE-TOTAL            PIC S9(6)V99.
021700 77  DIVISION-TOTAL         PIC S9(6)V99.
021800 77  DEPARTMENT-TOTAL       PIC S9(6)V99.
021900
022000* System date and time
022100 77  RUN-DATE           PIC 9(6).
022200 77  RUN-TIME           PIC 9(8).
022300
022400*---------------------------------
022500* Fields for date routines.
022600*---------------------------------
022700 77  FORMATTED-DATE     PIC Z9/99/9999.
022800 77  DATE-MMDDCCYY      PIC 9(8).
022900 01  DATE-CCYYMMDD      PIC 9(8).
023000 01  FILLER REDEFINES DATE-CCYYMMDD.
023100     05  DATE-CC        PIC 99.
023200     05  DATE-YY        PIC 99.
023300     05  DATE-MM        PIC 99.
023400     05  DATE-DD        PIC 99.
023500
023600*---------------------------------
023700* Fields for TIME routines.
023800*---------------------------------
023900 77  FORMATTED-TIME     PIC Z9/99/99.
024000
024100 01  TIME-HHMMSS      PIC 9(6).
024200 01  FILLER REDEFINES TIME-HHMMSS.
024300     05  TIME-HH        PIC 99.
024400     05  TIME-MM        PIC 99.
024500     05  TIME-SS        PIC 99.
024600
024700 PROCEDURE DIVISION.
024800 PROGRAM-BEGIN.
024900
025000     PERFORM OPENING-PROCEDURE.
025100     PERFORM MAIN-PROCESS.
025200     PERFORM CLOSING-PROCEDURE.
025300
025400 PROGRAM-EXIT.
025500     EXIT PROGRAM.
025600
025700 PROGRAM-DONE.
025800     STOP RUN.
025900
026000 OPENING-PROCEDURE.
026100
026200     OPEN OUTPUT PRINTER-FILE.
026300
026400 MAIN-PROCESS.
026500     PERFORM GET-OK-TO-PROCESS.
026600     PERFORM PROCESS-THE-FILE
026700         UNTIL OK-TO-PROCESS = "N".
026800
026900 CLOSING-PROCEDURE.
027000     CLOSE PRINTER-FILE.
027100
027200 GET-OK-TO-PROCESS.
027300     PERFORM ACCEPT-OK-TO-PROCESS.
027400     PERFORM RE-ACCEPT-OK-TO-PROCESS
027500         UNTIL OK-TO-PROCESS = "Y" OR "N".
027600
027700 ACCEPT-OK-TO-PROCESS.
027800     DISPLAY "PRINT SALES REPORT (Y/N)?".
027900     ACCEPT OK-TO-PROCESS.
028000     INSPECT OK-TO-PROCESS
028100       CONVERTING LOWER-ALPHA
028200       TO         UPPER-ALPHA.
028300
028400 RE-ACCEPT-OK-TO-PROCESS.
028500     DISPLAY "YOU MUST ENTER YES OR NO".
028600     PERFORM ACCEPT-OK-TO-PROCESS.
028700
028800 PROCESS-THE-FILE.
028900     PERFORM START-THE-FILE.
029000     PERFORM PRINT-ONE-REPORT.
029100     PERFORM END-THE-FILE.
029200
029300*    PERFORM GET-OK-TO-PROCESS.
029400     MOVE "N" TO OK-TO-PROCESS.
029500
029600 START-THE-FILE.
029700     PERFORM SORT-DATA-FILE.
029800     OPEN INPUT WORK-FILE.
029900
030000 END-THE-FILE.
030100     CLOSE WORK-FILE.
030200
030300 SORT-DATA-FILE.
030400     SORT SORT-FILE
030500         ON ASCENDING KEY SALES-STORE OF SORT-RECORD
030600            ASCENDING KEY SALES-DIVISION OF SORT-RECORD
030700            ASCENDING KEY SALES-DEPARTMENT OF SORT-RECORD
030800            ASCENDING KEY SALES-CATEGORY OF SORT-RECORD
030900          USING SALES-FILE
031000          GIVING WORK-FILE.
031100
031200* LEVEL 1 CONTROL BREAK
031300 PRINT-ONE-REPORT.
031400     PERFORM START-ONE-REPORT.
031500     PERFORM PROCESS-ALL-STORES
031600         UNTIL WORK-FILE-AT-END = "Y".
031700     PERFORM END-ONE-REPORT.
031800
031900 START-ONE-REPORT.
032000     PERFORM READ-FIRST-VALID-WORK.
032100     MOVE ZEROES TO GRAND-TOTAL.
032200
032300     PERFORM START-NEW-REPORT.
032400
032500 START-NEW-REPORT.
032600     MOVE SPACE TO DETAIL-LINE.
032700     MOVE ZEROES TO LINE-COUNT PAGE-NUMBER.
032800
032900     ACCEPT RUN-DATE FROM DATE.
033000     MOVE RUN-DATE TO DATE-CCYYMMDD.
033100     IF DATE-YY > 90
033200         MOVE 19 TO DATE-CC
033300     ELSE
033400         MOVE 20 TO DATE-CC.
033500
033600     PERFORM FORMAT-THE-DATE.
033700     MOVE FORMATTED-DATE TO FORMATTED-RUN-DATE.
033800
033900     ACCEPT RUN-TIME FROM TIME.
034000     COMPUTE TIME-HHMMSS = RUN-TIME / 100.
034100     PERFORM FORMAT-THE-TIME.
034200     MOVE FORMATTED-TIME TO FORMATTED-RUN-TIME.
034300
034400
034500 END-ONE-REPORT.
034600     IF RECORD-COUNT = ZEROES
034700         MOVE "NO RECORDS FOUND" TO PRINTER-RECORD
034800         PERFORM WRITE-TO-PRINTER
034900     ELSE
035000         PERFORM PRINT-GRAND-TOTAL.
035100
035200     PERFORM END-LAST-PAGE.
035300
035400 PRINT-GRAND-TOTAL.
035500     MOVE SPACE TO TOTAL-LINE.
035600     MOVE GRAND-TOTAL TO PRINT-TOTAL.
035700     MOVE GRAND-TOTAL-LITERAL TO TOTAL-TYPE.
035800     MOVE "TOTAL" TO TOTAL-LITERAL.
035900     MOVE TOTAL-LINE TO PRINTER-RECORD.
036000     PERFORM WRITE-TO-PRINTER.
036100     PERFORM LINE-FEED 2 TIMES.
036200     MOVE SPACE TO DETAIL-LINE.
036300
036400* LEVEL 2 CONTROL BREAK
036500 PROCESS-ALL-STORES.
036600     PERFORM START-ONE-STORE.
036700
036800     PERFORM PROCESS-ALL-DIVISIONS
036900         UNTIL WORK-FILE-AT-END = "Y"
037000            OR SALES-STORE OF WORK-RECORD
037100                NOT = CURRENT-STORE.
037200
037300     PERFORM END-ONE-STORE.
037400
037500 START-ONE-STORE.
037600     MOVE SALES-STORE OF WORK-RECORD TO CURRENT-STORE.
037700     MOVE ZEROES TO STORE-TOTAL.
037800     MOVE SALES-STORE OF WORK-RECORD TO PRINT-STORE.
037900
038000     PERFORM START-NEXT-PAGE.
038100
038200 END-ONE-STORE.
038300     PERFORM PRINT-STORE-TOTAL.
038400     ADD STORE-TOTAL TO GRAND-TOTAL.
038500
038600 PRINT-STORE-TOTAL.
038700     MOVE SPACE TO TOTAL-LINE.
038800     MOVE STORE-TOTAL TO PRINT-TOTAL.
038900     MOVE CURRENT-STORE TO TOTAL-NUMBER.
039000     MOVE STORE-TOTAL-LITERAL TO TOTAL-TYPE.
039100     MOVE "TOTAL" TO TOTAL-LITERAL.
039200     MOVE TOTAL-LINE TO PRINTER-RECORD.
039300     PERFORM WRITE-TO-PRINTER.
039400     PERFORM LINE-FEED.
039500     MOVE SPACE TO DETAIL-LINE.
039600
039700* LEVEL 3 CONTROL BREAK
039800 PROCESS-ALL-DIVISIONS.
039900     PERFORM START-ONE-DIVISION.
040000
040100     PERFORM PROCESS-ALL-DEPARTMENTS
040200         UNTIL WORK-FILE-AT-END = "Y"
040300            OR SALES-STORE OF WORK-RECORD
040400              NOT = CURRENT-STORE
040500            OR SALES-DIVISION OF WORK-RECORD
040600              NOT = CURRENT-DIVISION.
040700
040800     PERFORM END-ONE-DIVISION.
040900
041000 START-ONE-DIVISION.
041100     MOVE SALES-DIVISION OF WORK-RECORD TO CURRENT-DIVISION.
041200     MOVE ZEROES TO DIVISION-TOTAL.
041300     MOVE SALES-DIVISION OF WORK-RECORD TO PRINT-DIVISION.
041400
041500 END-ONE-DIVISION.
041600     PERFORM PRINT-DIVISION-TOTAL.
041700     ADD DIVISION-TOTAL TO STORE-TOTAL.
041800
041900 PRINT-DIVISION-TOTAL.
042000     MOVE SPACE TO TOTAL-LINE.
042100     MOVE DIVISION-TOTAL TO PRINT-TOTAL.
042200     MOVE CURRENT-DIVISION TO TOTAL-NUMBER.
042300     MOVE DIVISION-TOTAL-LITERAL TO TOTAL-TYPE.
042400     MOVE "TOTAL" TO TOTAL-LITERAL.
042500     PERFORM LOAD-DIVISION-NAME.
042600     MOVE TOTAL-LINE TO PRINTER-RECORD.
042700     PERFORM WRITE-TO-PRINTER.
042800     PERFORM LINE-FEED.
042900     MOVE SPACE TO DETAIL-LINE.
043000
043100 LOAD-DIVISION-NAME.
043200     SET DIVISION-INDEX TO 1.
043300     SEARCH DIVISION-TABLE
043400         AT END
043500           MOVE "NOT FOUND" TO TOTAL-NAME
043600         WHEN
043700           DIVISION-NUMBER(DIVISION-INDEX) =
043800              CURRENT-DIVISION
043900              MOVE DIVISION-NAME(DIVISION-INDEX) TO
044000                   TOTAL-NAME.
044100
044200* LEVEL 4 CONTROL BREAK
044300 PROCESS-ALL-DEPARTMENTS.
044400     PERFORM START-ONE-DEPARTMENT.
044500
044600     PERFORM PROCESS-ALL-CATEGORIES
044700         UNTIL WORK-FILE-AT-END = "Y"
044800            OR SALES-STORE OF WORK-RECORD
044900              NOT = CURRENT-STORE
045000            OR SALES-DIVISION OF WORK-RECORD
045100              NOT = CURRENT-DIVISION
045200            OR SALES-DEPARTMENT OF WORK-RECORD
045300              NOT = CURRENT-DEPARTMENT.
045400
045500     PERFORM END-ONE-DEPARTMENT.
045600
045700 START-ONE-DEPARTMENT.
045800     MOVE SALES-DEPARTMENT OF WORK-RECORD
045900       TO CURRENT-DEPARTMENT.
046000     MOVE ZEROES TO DEPARTMENT-TOTAL.
046100     MOVE SALES-DEPARTMENT OF WORK-RECORD
046200       TO PRINT-DEPARTMENT.
046300
046400 END-ONE-DEPARTMENT.
046500     PERFORM PRINT-DEPARTMENT-TOTAL.
046600     ADD DEPARTMENT-TOTAL TO DIVISION-TOTAL.
046700
046800 PRINT-DEPARTMENT-TOTAL.
046900     MOVE SPACE TO TOTAL-LINE.
047000     MOVE DEPARTMENT-TOTAL TO PRINT-TOTAL.
047100     MOVE CURRENT-DEPARTMENT TO TOTAL-NUMBER.
047200     MOVE DEPARTMENT-TOTAL-LITERAL TO TOTAL-TYPE.
047300     MOVE "TOTAL" TO TOTAL-LITERAL.
047400     PERFORM LOAD-DEPARTMENT-NAME.
047500     MOVE TOTAL-LINE TO PRINTER-RECORD.
047600     PERFORM WRITE-TO-PRINTER.
047700     PERFORM LINE-FEED.
047800     MOVE SPACE TO DETAIL-LINE.
047900
048000 LOAD-DEPARTMENT-NAME.
048100     SET DEPARTMENT-INDEX TO 1.
048200     SEARCH DEPARTMENT-TABLE
048300         AT END
048400           MOVE "NOT FOUND" TO TOTAL-NAME
048500         WHEN
048600           DEPARTMENT-NUMBER(DEPARTMENT-INDEX) =
048700              CURRENT-DEPARTMENT
048800              MOVE DEPARTMENT-NAME(DEPARTMENT-INDEX) TO
048900                   TOTAL-NAME.
049000
049100* PROCESS ONE RECORD LEVEL
049200 PROCESS-ALL-CATEGORIES.
049300     PERFORM PROCESS-THIS-CATEGORY.
049400     ADD SALES-AMOUNT OF WORK-RECORD
049500      TO DEPARTMENT-TOTAL.
049600     ADD 1 TO RECORD-COUNT.
049700     PERFORM READ-NEXT-VALID-WORK.
049800
049900 PROCESS-THIS-CATEGORY.
050000     IF LINE-COUNT > MAXIMUM-LINES
050100         PERFORM START-NEXT-PAGE.
050200     PERFORM PRINT-THE-RECORD.
050300
050400 PRINT-THE-RECORD.
050500     MOVE SALES-CATEGORY OF WORK-RECORD
050600       TO PRINT-CATEGORY.
050700
050800     PERFORM LOAD-CATEGORY-NAME.
050900
051000     MOVE SALES-AMOUNT OF WORK-RECORD
051100      TO PRINT-AMOUNT.
051200
051300     MOVE DETAIL-LINE TO PRINTER-RECORD.
051400     PERFORM WRITE-TO-PRINTER.
051500     MOVE SPACE TO DETAIL-LINE.
051600
051700 LOAD-CATEGORY-NAME.
051800     SET CATEGORY-INDEX TO 1.
051900     SEARCH CATEGORY-TABLE
052000         AT END
052100           MOVE "NOT FOUND" TO TOTAL-NAME
052200         WHEN
052300           CATEGORY-NUMBER(CATEGORY-INDEX) =
052400              SALES-CATEGORY OF WORK-RECORD
052500              MOVE CATEGORY-NAME(CATEGORY-INDEX) TO
052600                   PRINT-CATEGORY-NAME.
052700
052800* PRINTING ROUTINES
052900 WRITE-TO-PRINTER.
053000     WRITE PRINTER-RECORD BEFORE ADVANCING 1.
053100     ADD 1 TO LINE-COUNT.
053200
053300 LINE-FEED.
053400     MOVE SPACE TO PRINTER-RECORD.
053500     PERFORM WRITE-TO-PRINTER.
053600
053700 START-NEXT-PAGE.
053800     PERFORM END-LAST-PAGE.
053900     PERFORM START-NEW-PAGE.
054000
054100 START-NEW-PAGE.
054200     ADD 1 TO PAGE-NUMBER.
054300     MOVE PAGE-NUMBER TO PRINT-PAGE-NUMBER.
054400     MOVE TITLE-LINE TO PRINTER-RECORD.
054500     PERFORM WRITE-TO-PRINTER.
054600     PERFORM LINE-FEED.
054700     MOVE LEGEND-LINE TO PRINTER-RECORD.
054800     PERFORM WRITE-TO-PRINTER.
054900     PERFORM LINE-FEED.
055000     MOVE COLUMN-LINE TO PRINTER-RECORD.
055100     PERFORM WRITE-TO-PRINTER.
055200     PERFORM LINE-FEED.
055300
055400 END-LAST-PAGE.
055500     IF PAGE-NUMBER > 0
055600         PERFORM FORM-FEED.
055700     MOVE ZERO TO LINE-COUNT.
055800
055900 FORM-FEED.
056000     MOVE SPACE TO PRINTER-RECORD.
056100     WRITE PRINTER-RECORD BEFORE ADVANCING PAGE.
056200
056300*---------------------------------
056400* Read first, read next routines
056500*---------------------------------
056600 READ-FIRST-VALID-WORK.
056700     PERFORM READ-NEXT-VALID-WORK.
056800
056900 READ-NEXT-VALID-WORK.
057000     PERFORM READ-NEXT-WORK-RECORD.
057100
057200 READ-NEXT-WORK-RECORD.
057300     MOVE "N" TO WORK-FILE-AT-END.
057400     READ WORK-FILE NEXT RECORD
057500         AT END MOVE "Y" TO WORK-FILE-AT-END.
057600
057700* Date and time routines
057800 FORMAT-THE-DATE.
057900     PERFORM CONVERT-TO-MMDDCCYY.
058000     MOVE DATE-MMDDCCYY TO FORMATTED-DATE.
058100
058200 CONVERT-TO-MMDDCCYY.
058300     COMPUTE DATE-MMDDCCYY =
058400             DATE-CCYYMMDD * 10000.0001.
058500
058600 FORMAT-THE-TIME.
058700     MOVE TIME-HHMMSS TO FORMATTED-TIME.
058800     INSPECT FORMATTED-TIME
058900       REPLACING ALL "/" BY ":".
059000
