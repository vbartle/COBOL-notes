000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. VNDNEW02.
000300*------------------------------------------------
000400* Add a record to an indexed Vendor File.
000500*------------------------------------------------
000600 ENVIRONMENT DIVISION.
000700 INPUT-OUTPUT SECTION.
000800 FILE-CONTROL.
000900
001000     SELECT VENDOR-FILE
001100         ASSIGN TO "vendor"
001200         ORGANIZATION IS INDEXED
001300         RECORD KEY IS VENDOR-NUMBER
001400         ACCESS MODE IS DYNAMIC.
001500
001600 DATA DIVISION.
001700 FILE SECTION.
001800
001900 FD  VENDOR-FILE
002000     LABEL RECORDS ARE STANDARD.
002100 01  VENDOR-RECORD.
002200     05  VENDOR-NUMBER                    PIC 9(5).
002300     05  VENDOR-NAME                      PIC X(30).
002400     05  VENDOR-ADDRESS-1                 PIC X(30).
002500     05  VENDOR-ADDRESS-2                 PIC X(30).
002600     05  VENDOR-CITY                      PIC X(20).
002700     05  VENDOR-STATE                     PIC X(2).
002800     05  VENDOR-ZIP                       PIC X(10).
002900     05  VENDOR-CONTACT                   PIC X(30).
003000     05  VENDOR-PHONE                     PIC X(15).
003100
003200 WORKING-STORAGE SECTION.
003300
003400 01  VENDOR-NUMBER-FIELD                  PIC Z(5).
003500
003600 PROCEDURE DIVISION.
003700 PROGRAM-BEGIN.
003800     OPEN I-O VENDOR-FILE.
003900     PERFORM GET-NEW-VENDOR-NUMBER.
004000     PERFORM ADD-RECORDS
004100        UNTIL VENDOR-NUMBER = ZEROES.
004200     CLOSE VENDOR-FILE.
004300
004400 PROGRAM-DONE.
004500     STOP RUN.
004600
004700 GET-NEW-VENDOR-NUMBER.
004800     PERFORM INIT-VENDOR-RECORD.
004900     PERFORM ENTER-VENDOR-NUMBER.
005000
005100 INIT-VENDOR-RECORD.
005200     MOVE SPACE TO VENDOR-RECORD.
005300     MOVE ZEROES TO VENDOR-NUMBER.
005400
005500 ENTER-VENDOR-NUMBER.
005600     DISPLAY "ENTER VENDOR NUMBER (1-99999)".
005700     DISPLAY "ENTER 0 TO STOP ENTRY".
005800     ACCEPT VENDOR-NUMBER-FIELD.
005900*OR  ACCEPT VENDOR-NUMBER-FIELD WITH CONVERSION.
006000
006100     MOVE VENDOR-NUMBER-FIELD TO VENDOR-NUMBER.
006200*OR  MOVE WITH CONVERSION VENDOR-NUMBER-FIELD
006300*       TO VENDOR-NUMBER.
006400
006500 ADD-RECORDS.
006600     PERFORM ENTER-REMAINING-FIELDS.
006700     PERFORM WRITE-VENDOR-RECORD.
006800     PERFORM GET-NEW-VENDOR-NUMBER.
006900
007000 WRITE-VENDOR-RECORD.
007100     WRITE VENDOR-RECORD
007200         INVALID KEY
007300         DISPLAY "RECORD ALREADY ON FILE".
007400
007500 ENTER-REMAINING-FIELDS.
007600     PERFORM ENTER-VENDOR-NAME.
007700     PERFORM ENTER-VENDOR-ADDRESS-1.
007800     PERFORM ENTER-VENDOR-ADDRESS-2.
007900     PERFORM ENTER-VENDOR-CITY.
008000     PERFORM ENTER-VENDOR-STATE.
008100     PERFORM ENTER-VENDOR-ZIP.
008200     PERFORM ENTER-VENDOR-CONTACT.
008300     PERFORM ENTER-VENDOR-PHONE.
008400
008500 ENTER-VENDOR-NAME.
008600     DISPLAY "ENTER VENDOR NAME".
008700     ACCEPT VENDOR-NAME.
008800
008900 ENTER-VENDOR-ADDRESS-1.
009000     DISPLAY "ENTER VENDOR ADDRESS-1".
009100     ACCEPT VENDOR-ADDRESS-1.
009200
009300 ENTER-VENDOR-ADDRESS-2.
009400     DISPLAY "ENTER VENDOR ADDRESS-2".
009500     ACCEPT VENDOR-ADDRESS-2.
009600
009700 ENTER-VENDOR-CITY.
009800     DISPLAY "ENTER VENDOR CITY".
009900     ACCEPT VENDOR-CITY.
010000
010100 ENTER-VENDOR-STATE.
010200     DISPLAY "ENTER VENDOR STATE".
010300     ACCEPT VENDOR-STATE.
010400
010500 ENTER-VENDOR-ZIP.
010600     DISPLAY "ENTER VENDOR ZIP".
010700     ACCEPT VENDOR-ZIP.
010800
010900 ENTER-VENDOR-CONTACT.
011000     DISPLAY "ENTER VENDOR CONTACT".
011100     ACCEPT VENDOR-CONTACT.
011200
011300 ENTER-VENDOR-PHONE.
011400     DISPLAY "ENTER VENDOR PHONE".
011500     ACCEPT VENDOR-PHONE.
011600
